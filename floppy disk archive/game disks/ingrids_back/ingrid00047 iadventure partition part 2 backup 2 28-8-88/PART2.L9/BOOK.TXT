; 
; Code for book protection system
;
; Copyright (C) 1986 Level 9 Computing
;
; For Gnome Ranger - taken from Knight Orc version
;
; 15/9/87
;
; table of comparison words start at 4300 (MATCHOFFSET)
;
CONST
 MaxPage=40
 MinPage=2
 MaxLine=10
 MaxWord=8
 MaxAttemptsPerWord=3
 TotalWords=100
 totalwordsplus1=101
 ListBookProtect=0 ; ************* 1 to test, 0 in released game
 TestBookProtect=0 ; ************* 1 to test, 0 in released game 
;
VAR
 PAGE
 LINE
 WORD
 RANDOMSEED
 count TESTcount SAVEcount
 ATTEMPTS

BEGIN

.BOOKPROTECT

 cif ListBookProtect

.TEST 
 C1=1 
 TESTcount=1 
.TEST1 
 count=TESTcount 
 GOSUB @getWORDcount 
 print testcount
 message space
 GOSUB @displayWORD 
.TEST2 
 ADD TESTcount,C1 
 IF TESTcount<TOTALWORDSplus1 THEN TEST1 

 cend 
 cif TestBookProtect 

.TRYALLWORDS
.TESTUSER
 TESTcount=1
.TAW1
 count=TESTcount
 GOSUB @getWORDcount
 print testcount
 message space
 count=TESTcount
 GOSUB @DISPLAYWORD
 count=TESTcount
 GOSUB @COMPAREPROTECTIONWORD
 PRINT VALUE
 MESSAGE CR
 if value=testcount then taw2
 prs "!!!!! FAIL  !!!!!!!!!!!!!!!!!!!!!!!!. Press 1 to skip, "
 prs "anything else to try again. "
 prs "Should be: "
 x1=4300
 add x1,TestCount
 message x1
 message cr
.tawpause
 gosub @osrdch
 if x1=0 then tawpause
 if x1<>49 then @taw1
.taw2
 ADD TESTcount,C1
; *******************************************************
 if testcount=11 then taw2 ; add exceptions in this form. 
 if testcount=16 then taw2 
 if testcount=17 then taw2 
 if testcount=18 then taw2 
 if testcount=22 then taw2 
 if testcount=23 then taw2 
 if testcount=24 then taw2 
 if testcount=25 then taw2 
 if testcount=32 then taw2 
 if testcount=35 then taw2 
 if testcount=40 then taw2 
 if testcount=41 then taw2 
 if testcount=42 then taw2 
 if testcount=49 then taw2 
 if testcount=53 then taw2 
 if testcount=54 then taw2 
 if testcount=56 then taw2 
 if testcount=57 then taw2 
 if testcount=59 then taw2 
 if testcount=60 then taw2 
 if testcount=63 then taw2 
 if testcount=67 then taw2 
 if testcount=69 then taw2 
 if testcount=75 then taw2 
 if testcount=81 then taw2 
 if testcount=83 then taw2 
 if testcount=84 then taw2 
 if testcount=88 then taw2 
 if testcount=89 then taw2 
 if testcount=90 then taw2 
 if testcount=91 then taw2 
 if testcount=92 then taw2 
 if testcount=97 then taw2 
 if testcount=99 then taw2 
; *******************************************************
 GOTO @TAW1

 cend
 cif includeprotection ; see MCONST.TXT 
;
;; >>> BOOKPROTECT main entry point (in normal use) <<<

 IF ALREADYDONEPROTECT=FALSE THEN DOBOOKPROTECT
 RETURN ; ALREADY BEEN ASKED
.DOBOOKPROTECT
;
 IF TESTcount<>0 THEN TESTUSER1
 GOSUB @getRANDOMword ; only pick a new word on startup, or on success
 TESTcount=count
;
.TESTUSER1
 ATTEMPTS=0
 MESSAGE 4200 ; whatsit appears
.TESTUSER2
 MESSAGE 4207 ; tell me the...
 GOSUB @DISPLAYWORD
 count=TESTcount
 GOSUB @COMPAREPROTECTIONWORD
 IF VALUE<>NULLVALUE THEN TESTUSEROK
 ADD ATTEMPTS,C1
 IF ATTEMPTS<MAXATTEMPTSPERWORD THEN TESTUSERAGAIN
 MESSAGE 4208 ; whatsit kills yer
 GOTO @userdeath
;
.TESTUSERAGAIN
 MESSAGE 4205 ; wrong. Try again
 GOTO TESTUSER2
;
.TESTUSEROK
 MESSAGE 4206 ; correct
 GOSUB getRANDOMWORD ; set up a new word for subsequent attempts
 TESTcount=count
 RESULT=TRUE ; tell caller all was well
 RETURN
;
.COMPAREPROTECTIONWORD
; return VALUE=word entered, or zero if failed
.CPWgetWORD
 NOMOREINPUT=FALSE
 WORDNOTPROCESSED=FALSE
 SUPRESSCHECKING=TRUE
 GOSUB @getNEXTWORD
 INDEX=0
 GOSUB @readINPUTlist
 IF VALUE=NULLvalue THEN CPWgetWORD
.CPW1
 SEARCHTYPE=MATCHTYPE
 GOSUB @CHECKTYPEMORE
 IF VALUE=NULLVALUE THEN CPWFAIL
 IF VALUE=count THEN CPWOK
 GOTO CPW1
.CPWFAIL
 VALUE=NULLVALUE
.CPWOK
 SUPRESSCHECKING=FALSE
 RETURN
; 
;---
; 
.getRANDOMword
 X2=TotalWords
 GOSUB @randomX1modX2
 count=X1
 if count=0 then @GetRandomWord
; ***********************************************************
; Avoid random choice of unsuitable words...
 if count=11 then getRandomWord ; add exceptions in this form. 
 if count=16 then getRandomWord 
 if count=17 then getRandomWord 
 if count=18 then getRandomWord 
 if count=22 then getRandomWord 
 if count=23 then getRandomWord 
 if count=24 then getRandomWord 
 if count=25 then getRandomWord 
 if count=32 then getRandomWord 
 if count=35 then getRandomWord 
 if count=40 then getRandomWord 
 if count=41 then getRandomWord 
 if count=42 then getRandomWord 
 if count=49 then getRandomWord 
 if count=53 then getRandomWord 
 if count=54 then getRandomWord 
 if count=56 then getRandomWord 
 if count=57 then getRandomWord 
 if count=59 then getRandomWord 
 if count=60 then getRandomWord 
 if count=63 then getRandomWord 
 if count=67 then getRandomWord 
 if count=69 then getRandomWord 
 if count=75 then getRandomWord 
 if count=81 then getRandomWord 
 if count=83 then getRandomWord 
 if count=84 then getRandomWord 
 if count=88 then getRandomWord 
 if count=89 then @getRandomWord 
 if count=90 then @getRandomWord 
 if count=91 then @getRandomWord 
 if count=92 then @getRandomWord 
 if count=97 then @getRandomWord 
 if count=99 then @getRandomWord 
; ***********************************************************
 SAVEcount=count
 GOSUB getWORDcount
 count=SAVEcount
 RETURN
;---
.DISPLAYWORD
 MESSAGE 4201 ; 'page '
 PRINT PAGE
 MESSAGE 4202 ; ', line '
 PRINT LINE
 MESSAGE 4203 ; ', word '
 PRINT WORD
 MESSAGE 4204 ; '". '
 RETURN
;
;---
; 
.getWORDcount
; step through pseudo-random sequence to set PAGE, LINE and WORD 
; for the COUNTth word
 RANDOMSEED=13971 ; 10293
.GWC1
 GOSUB @RANDOM
 SUB count,C1
 IF count>0 THEN GWC1
;
; now get page number
.getPAGEnumber
 GOSUB RANDOM
 X2=maxPAGE
 GOSUB @X1modX2
 PAGE=X1
 ADD PAGE,C1; So 1 <= PAGE <= maxPAGE
 if PAGE<minPAGE then getPageNumber 
;
.getLINEnumber
 GOSUB RANDOM
 X2=maxLINE
 GOSUB @X1modX2
 LINE=X1
 ADD LINE,C1; So 1 <= LINE <= maxLINE 
;
.getWORDnumber
 GOSUB RANDOM
 X2=maxWORD
 GOSUB @X1modX2
 WORD=X1
 ADD WORD,C1; Now 1 <= WORD <= maxWORD 
 RETURN
;
.RANDOM ; return random X1, 0<=X1<=255. And update RANDOMSEED
 X1=RANDOMSEED 
 ADD X1,X1 ; X1:=RANDOMSEED*256 (Hi-byte := Low-byte. Low-byte := 10)
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1 
 X2=10
 ADD X1,X2 
; shuffle bits about
 SUB X1,RANDOMSEED 
 ADD X1,X1
 ADD X1,X1
 ADD X1,RANDOMSEED
 ADD X1,C1
 RANDOMSEED=X1
; and return low-order byte of seed as a random number
 X2=31
 LIST9(X2)=X1
 X1=LIST9(X2)
 RETURN

 cend

 goto @returntrue ; ok if code not compiled
;-----------