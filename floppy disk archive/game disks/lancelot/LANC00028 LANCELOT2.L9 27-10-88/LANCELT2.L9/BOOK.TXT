; 
; Code for book protection system
;
; Copyright (C) 1986 Level 9 Computing
;
; For Gnome Ranger - taken from Knight Orc version
;
; 15/9/87
;
; table of comparison words start at 4300 (MATCHOFFSET)
;
CONST
 MaxPage=19
 MinPage=1 
 MaxLine=10
 MaxWord=8
 MaxAttemptsPerWord=3
 TotalWords=100
 ListBookProtect=0 ; ************* 1 to test, 0 in released game
 TestBookProtect=0 ; ************* 1 to test, 0 in released game 
;
VAR
 PAGE
 LINE
 WORD
 RANDOMSEED
 count TESTcount SAVEcount
 ATTEMPTS

BEGIN

.BOOKPROTECT

 cif ListBookProtect

.TEST 
 C1=1 
 TESTcount=1 
.TEST1 
 count=TESTcount 
 GOSUB @getWORDcount 
 GOSUB @displayWORD 
.TEST2 
 ADD TESTcount,C1 
 IF TESTcount<TOTALWORDS THEN TEST1 

 cend 
 cif TestBookProtect 

.TRYALLWORDS
.TESTUSER
 TESTcount=1
.TAW1
 count=TESTcount
 GOSUB @getWORDcount
 PRINT TESTcount
 MESSAGE SPACE
 count=TESTcount
 GOSUB @DISPLAYWORD
 count=TESTcount
 GOSUB @COMPAREPROTECTIONWORD
 PRINT VALUE
 MESSAGE CR
 if value=testcount then taw2
 prs "!!!!! FAIL  !!!!!!!!!!!!!!!!!!!!!!!!. Press 1 to skip, "
 prs "anything else to try again. "
 prs "Should be: "
 x1=4300
 add x1,TestCount
 message x1
 message cr
.tawpause
 gosub @osrdch
 if x1=0 then tawpause
 if x1<>49 then @taw1
.taw2
 ADD TESTcount,C1
; *******************************************************
 if testcount=10 then taw2 ; add exceptions in this form. 
 if testcount=18 then taw2 
 if testcount=19 then taw2 
 if testcount=28 then taw2 
 if testcount=37 then taw2 
 if testcount=41 then taw2 
 if testcount=47 then taw2 
 if testcount=53 then taw2 
 if testcount=68 then taw2 
 if testcount=69 then taw2 
 if testcount=73 then taw2 
 if testcount=83 then taw2 
 if testcount=93 then taw2 
 if testcount=98 then taw2 
; *******************************************************
 GOTO @TAW1

 cend
 cif includeprotection ; see MCONST.TXT 
;
;; >>> BOOKPROTECT main entry point (in normal use) <<<
;
 IF TESTcount<>0 THEN TESTUSER1
 GOSUB @getRANDOMword ; only pick a new word on startup, or on success
 TESTcount=count
;
.TESTUSER1
 ATTEMPTS=0
 MESSAGE 4200 ; whatsit appears
.TESTUSER2
 MESSAGE 4207 ; tell me the...
 GOSUB @DISPLAYWORD
 count=TESTcount
 GOSUB @COMPAREPROTECTIONWORD
 IF VALUE<>NULLVALUE THEN TESTUSEROK
 ADD ATTEMPTS,C1
 IF ATTEMPTS<MAXATTEMPTSPERWORD THEN TESTUSERAGAIN
 MESSAGE 4208 ; whatsit kills yer
 GOTO @userdeath
;
.TESTUSERAGAIN
 MESSAGE 4205 ; wrong. Try again
 GOTO TESTUSER2
;
.TESTUSEROK
 MESSAGE 4206 ; correct
 GOSUB getRANDOMWORD ; set up a new word for subsequent attempts
 TESTcount=count
 RESULT=TRUE ; tell caller all was well
 RETURN
;
.COMPAREPROTECTIONWORD
; return VALUE=word entered, or zero if failed
.CPWgetWORD
 NOMOREINPUT=FALSE
 WORDNOTPROCESSED=FALSE
 SUPRESSCHECKING=TRUE
 GOSUB @getNEXTWORD
 INDEX=0
 GOSUB @readINPUTlist
 IF VALUE=NULLvalue THEN CPWgetWORD
.CPW1
 SEARCHTYPE=MATCHTYPE
 GOSUB @CHECKTYPEMORE
 IF VALUE=NULLVALUE THEN CPWFAIL
 IF VALUE=count THEN CPWOK
 GOTO CPW1
.CPWFAIL
 VALUE=NULLVALUE
.CPWOK
 SUPRESSCHECKING=FALSE
 RETURN
; 
;---
; 
.getRANDOMword
 X2=TotalWords
 GOSUB @randomX1modX2
 count=X1
 if count=0 then @GetRandomWord
; ***********************************************************
; Avoid random choice of unsuitable words...
 if count=10 then getRandomWord ; add exceptions in this form. 
 if count=18 then getRandomWord 
 if count=19 then getRandomWord 
 if count=28 then getRandomWord 
 if count=37 then getRandomWord 
 if count=41 then getRandomWord 
 if count=47 then getRandomWord 
 if count=53 then getRandomWord 
 if count=68 then getRandomWord 
 if count=69 then getRandomWord 
 if count=73 then getRandomWord 
 if count=83 then getRandomWord 
 if count=93 then getRandomWord 
 if count=98 then getRandomWord 
; ***********************************************************
 SAVEcount=count
 GOSUB getWORDcount
 count=SAVEcount
 RETURN
;---
.DISPLAYWORD
 MESSAGE 4201 ; 'page '
 PRINT PAGE
 MESSAGE 4202 ; ', line '
 PRINT LINE
 MESSAGE 4203 ; ', word '
 PRINT WORD
 MESSAGE 4204 ; '". '
 RETURN
;
;---
; 
.getWORDcount
; step through pseudo-random sequence to set PAGE, LINE and WORD 
; for the COUNTth word
 RANDOMSEED=10293
.GWC1
 GOSUB @RANDOM
 SUB count,C1
 IF count>0 THEN GWC1
;
; now get page number
.getPAGEnumber
 GOSUB RANDOM
 X2=maxPAGE
 GOSUB @X1modX2
 PAGE=X1
 ADD PAGE,C1; So 1 <= PAGE <= maxPAGE
 if PAGE<minPAGE then getPageNumber 
;
.getLINEnumber
 GOSUB RANDOM
 X2=maxLINE
 GOSUB @X1modX2
 LINE=X1
 ADD LINE,C1; So 1 <= LINE <= maxLINE 
;
.getWORDnumber
 GOSUB RANDOM
 X2=maxWORD
 GOSUB @X1modX2
 WORD=X1
 ADD WORD,C1; Now 1 <= WORD <= maxWORD 
 RETURN
;
.RANDOM ; return random X1, 0<=X1<=255. And update RANDOMSEED
 X1=RANDOMSEED 
 ADD X1,X1 ; X1:=RANDOMSEED*256 (Hi-byte := Low-byte. Low-byte := 10)
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1
 ADD X1,X1 
 X2=10
 ADD X1,X2 
; shuffle bits about
 SUB X1,RANDOMSEED 
 ADD X1,X1
 ADD X1,X1
 ADD X1,RANDOMSEED
 ADD X1,C1
 RANDOMSEED=X1
; and return low-order byte of seed as a random number
 X2=31
 LIST9(X2)=X1
 X1=LIST9(X2)
 RETURN

 cend

 goto @returntrue ; ok if code not compiled
