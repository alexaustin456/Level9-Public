;Colossal Adventure 27/05/86
;Converted from version on 17/01/84
;
;>> extra messages 694,695,696 added at 27/8/86 by MJA
;>> (for the 16bit versions). If the small text games are re-issued,
;>> we will have to make the appropriate changes in their text
;
CONST
 LIST3MAX=33 ;>>MIKE 27/8/86

BEGIN
;Copyright (C) 1984,1986 Level 9 Computing
.STARTGAME
 CLEAR ;>> moved from L70
; C0=0 ;>>
 C1=1 ;>>
 C2=2 ;>>
 C3=3 ;>>
 VERSION=GAMECODE ;>>Must be before 'RETURNTOSTART'
 GAMESOLVED=JUSTPLAYING ;>>

.RETURNTOSTART ;>> Only for 'CHAIN'

 SCREEN G C0 ;>> NICK 1/7/86
 CLS G ;>> NICK 1/7/86
 LASTPICTURE=501 ;>> NICK 1/7/86
 PICTURE LASTPICTURE ;>> NICK 1/7/86

 GOSUB @INITPARSER ;>>
;
;>.L10
;.L20
;.L40
;>> .L50 GOTO L70 ;COLD START
;>> .L60 GOTO @L450 ;WARM
;>> moved to STARTGAME .L70 CLEAR
;>>.L80
;>> C1=1 ;moved to STARTGAME
;>>.L85
;>> C2=2 ;moved to STARTGAME
;>> C3=3 ;moved to STARTGAME
 C8=255

; X1=C0                   ;>>
;.CLEARALL                ;>>
; IF X1>255 THEN CLEAREND ;>>
; LIST1(X1)=C0            ;>>
; ADD X1,C1               ;>>
; GOTO CLEARALL           ;>>
;.CLEAREND                ;>>

 X1=C1
.L120 X2=LIST2(X1) ;OBJECT ROOMS
 LIST3(X1)=X2
 LIST5(X1)=C9
 LIST1(X1)=C9
 ADD X1,C1
 IF X1<34 THEN L120 ;Only 33 bytes long (?)
.L180 LIST1(X1)=C9
 ADD X1,C1
 IF X1<110 THEN L180
 T1=120 ;TIME LEFT
 ;M1=0 ;NO. OF MOVES LEFT 
 ;F1=0 ;BOTTLE CONTENTS 
 ;F2=0 ;CAGE   
 F3=C2 ;BEAR CHAINED   
 ;D1=0 ;NUMDIE      
 W1=30 ;WITT'S END PROB.
 ;D2=0 ;FIRST DWARF
 D3=C2 ;DWARF ANGRY
 ;D4=0 ;NO DWARVES IN ROOM 
 ;F4=0 ;TROLL LURKING 
 ;F5=0 ;CLAM 
 ;F6=0 ;PLANT SMALL
 ;F7=0 ;FEE FLAG 
;>> .L350
;L1=0 ;LAMP OFF 
;L2=0 ;LAMP MOVES
;S1=0 ;HALL MIST VISITED?
;E5=0 ;NORTH CHAINED ELVES 
;E6=0 ;SOUTH CHAINED ELVES 
;E4=0 ;NO KING 
;S2=0 ;NO QUIT YET
 S6=C1 ;NO SAVE YET
 ;S3=0 ;ENDGAME SCORE     
 S5=216 ;SPIDER ROOM
 ;T2=0 ;NOT IN ENDGAME 
 ;E1=0 ;NO BLAST YET 
 ;E2=0 ;SKELETONS 
 ;B1=0 ;NO ENDGAME BRIDGE YET  
 ;E3=0 ;ENDGAME DOOR OPEN 
 MESSAGE C1 ;Copyright message
 MESSAGE 12 ;CR
 R1=C1
 GOSUB @INITOOPS ;>>
.L450 GOTO @L4000
.L500 GOSUB @L22000 ;CHECK IF ROOM IS WATERED
 W5=W4
 IF R1<>50 THEN L505
 S1=C1;HALL OF MISTS
.L505 IF R1>165 THEN L510
 IF R1>49 THEN L530
.L510 D4=C0 ;NO DWARVES
 GOTO L650
.L530 RANDOM X1
 IF X1>5 THEN L650
 IF X1<C2 THEN L700 ;PIRATE
.L550 IF D2>C0 THEN L600
 ; FIRST DWARF
 MESSAGE 91
 LIST3(26)=R1 ;AXE APPEARS
 GOTO L630
.L600 ; ANOTHER DWARF
 MESSAGE 92
 ADD D4,C1 ;ANOTHER DWARF IN ROOM
.L630 ADD D2,C1 ;NO. OF DWARVES SEEN
.L650
 GOSUB @L33000 ;LAMP OUT ?
.L655 GOSUB @L32500 ;VOICE SAYS PLUGH
.L659 ;>> MESSAGE 13 ; WHAT NOW ?
.L660
.RESETSTACK
 STACK
 GOSUB @OOPSCHECK ;>>

.I4CHECK ;>>
 GOSUB @GETCOMMAND ;>> INPUT I1 I2 I3 I4
 IF I4=C0 THEN I4CHECK ;>>

.L665 GOSUB @L32000 ;HANDLE FEE ETC. PART 1
.L670 IF I4=C0 THEN L660
.L690 ;
 JUMP @L800 I1

.L700 ; PIRATE
 X1=LIST5(24) ;HAS CHEST BEEN SEEN
 IF X1=C1 THEN L550
 R2=C8
 R3=C8
 GOSUB @L11000
 IF N3=C0 THEN L550
 R3=115 ;CHEST
 LIST3(24)=R3 ;CHEST APPEARS IN MAZE
 GOSUB @L11000 ;TREASURE
 MESSAGE 93
 GOTO L650
.L800 DATA @L1100,@L2000,@L2000,@L2000,@L2000 ;1-4
 DATA @L2000,@L2000,@L2000,@L2000,@L2000 ;5-9
 DATA @L2000,@L2000,@L2000,@L2000,@L2000 ;10-14
 DATA @L2000,@L5000,@L5500,@L6000,@L6500 ;15-19
 DATA @L23000,@L7000,@L7100,@L7200,@L7500 ;20-24
 DATA @L7900,@L8000,@L8600,@L7800,@L7600 ;25-29
 DATA @L8700,@L9500,@L9600,@L9700,@L9800 ;30-34
 DATA @LIGHT,@EXTINGUISH,@TURN,@L1300,@L1300 ;35-39
 DATA @L1300,@L1300,@L1300,@L27000,@L1300 ;40-44
 DATA @L1300,@L1300,@L1300,@L1300,@L1300 ;45-49
 DATA @L1300,@L1300,@L1300,@L1300,@L1300 ;50-54
 DATA @L1300,@L1300,@L1300,@L1300,@L1300 ;55-59
 DATA @L1300,@L1300,@L1300,@L1300,@L1300 ;60-64
 DATA @L1300,@L1300,@L1300,@L15800,@L1300 ;65-69
 DATA @L1300,@L4500,@L1300,@L1300,@L15900 ;70-74
 DATA @L1300,@L1300,@L16500,@LOOK,@L16200 ;75-79
 DATA @L16200,@L16200,@L16200,@L19000,@L19500 ;80-84
 DATA @L1300,@L1300,@L15000,@L15400,@L15700 ;85-89
 DATA @L16000,@L1100,@L29000,@RESTORE,@L27500 ;90-94
 DATA @EXAMINE,@RAMCOMMAND,@OOPS,@WORDS,@PICTURES ;95-99
 DATA @STARTGAME,@AUTHORS,@ILLEGALCOMMAND,@ILLEGALCOMMAND ;100-103
 DATA @ILLEGALCOMMAND,@HELP ; 104-105

.ILLEGALCOMMAND
 GOSUB @CANCELINPUT
.ENDCOMMAND ;>>
.L1000 ; AFTER PLAYER ACTION,
 GOSUB @L20000 ;ENDGAME BITS+PIECES
.L1010 ; TRY THE EVIL LITTLE DWARVES
 IF R1<50 THEN @L500
 IF D4=C0 THEN @L500
 RANDOM X1
 IF X1>80 THEN @L500 ;DONT THROW KNIVES
 MESSAGE 94 ;Knife thrown
 X1=20
 IF D3=C0 THEN L1070
 X1=50
.L1070 IF D4=C1 THEN L1090
 ADD X1,X1
.L1090 RANDOM X2
 IF X2>X1 THEN L1098 ;MISSES
 MESSAGE 95 ;Hits
 GOTO L1500 ;DIE
.L1098 MESSAGE 96 ;Misses
 GOTO @L500
.L1100 MESSAGE 5 ;Don't understand
 GOTO ILLEGALCOMMAND ;>>L1000

.L1200
 MESSAGE 6 ;Be specific
 GOTO L1000
.SPECIALALL
 MESSAGE 2022 ; doesn't seem to be anything here
 GOTO L1000

.L1250 MESSAGE 100 ;Can't
 GOTO ILLEGALCOMMAND ;>>L1000
.L1300 MESSAGE 7 ;What ?
 GOTO ILLEGALCOMMAND ;>>L1000
.L1350 MESSAGE 8 ;I don't see it here
 GOTO L1000
.L1400 MESSAGE 9 ;I don't have it
 GOTO L1000
.L1450 MESSAGE 77 ;Nothing to attack
 GOTO L1000
;-----
.L1500 ; DEATH COMES HERE
 R2=R1
 R1=19 ;HEAVEN
 IF T2>C0 THEN L1700 ;ENDGAME
 ADD D1,C1
 X1=52
 ADD X1,D1
 MESSAGE X1 ;Sarcastic comment
.L1551 
 MESSAGE 664 ;RESSURECT?

 GOSUB @ASKINPUT ;>>
 IF RESULT=FALSE THEN L1551 ;>>

 GOSUB @L18000 ;GET YES/NO RESPONSE
 IF Y1=C2 THEN L1551
 IF Y1=C0 THEN L1700 ;NO
 IF D1<C3 THEN L1565
 MESSAGE 666 ;NO RESURECT ON DIE 3
 GOTO L1700
.L1565 MESSAGE 665;RESURRECTED
 R3=R2
 GOSUB @L11000 ;UNIDROP
 LIST3(2)=C1 ;LAMP AT END OF ROAD
 D3=C0
 R1=14
 GOTO @L450
.DIE ;>>
.L1700 MESSAGE 82 ;You scored
 GOSUB @L9000 ;CALCULATE SCORE
 PRINT S4
 MESSAGE 81 ;out of 1000
 MESSAGE S7 ;RATING
 IF S7<>682 THEN @QUITPLAYAGAIN ;>> 676
 GOTO @ASKFINAL ;>>Option to SAVE then QUITPLAYAGAIN

;=====

;>>.L19030
;>> MESSAGE 13;REALLY QUIT?
;>> INPUT I1 I2 I3 I4
;>> IF I1=C1 THEN @L3000;NO!
;>> IF I1=29 THEN @L51000;ANOTHER GAME
;>> MESSAGE 9;YES OR NO!
;>> GOTO L19030

;-----

.EXAMINE ;>>
 IF I4=C2 THEN EXAMINE1
 IF EVERYTHING=TRUE THEN SPECIALALL ; MIKE 9/9/86
 MESSAGE 6 ;I don't understand
 GOTO @L1000
.EXAMINE1

 IF I2<>69 THEN NOTEXAMINEDWARF ;>> NICK 15/7/86
 IF D4=C0 THEN NOTEXAMINEDWARF ;>> NICK 15/7/86
 MESSAGE 1633 ;>> NICK 15/7/86 notices your gaze
 GOTO @L1000 ;>> NICK 15/7/86
.NOTEXAMINEDWARF ;>> NICK 15/7/86
 IF I2<>29 THEN EXAMINENWATER
 MESSAGE 1700 ; water message
 GOTO AFTEREXAMINE

.EXAMINENWATER
 IF I2<>28 THEN EXAMINENOIL
 MESSAGE 1701 ; oil examine message
 GOTO AFTEREXAMINE

.EXAMINENOIL
 OBJECT=I2
 GOSUB @CHECKFIXEDOBJECT
 IF RESULT=FALSE THEN EXAMINE2
 GOSUB @L5300 ;Convert object number
 GOTO EXAMINE4

.EXAMINE2
 GOSUB @L5300 ;Convert I2 to object number
 IF I2>LIST3MAX THEN @SILLY
 X1=LIST3(I2)
 IF X1=R1 THEN EXAMINE4 ;on floor
 IF X1=C8 THEN EXAMINE4 ;carried
 MESSAGE 8 ;I don't see it here
 GOTO @L1000

.EXAMINE4
 X1=1599 ;1600=Start of examine messages
 ADD I2,X1
 MESSAGE I2
.AFTEREXAMINE
 MESSAGE DOT
 GOTO @L1000

;-----
.L2000 ; MOVE
;>>MIKE 9/7/86 GOSUB @SAVEOOPS ;>>
 EXIT R1 I1 X1 R2
 IF R1=77 THEN @L3300 ;WITT'S END
 IF R1>165 THEN L2022 ;DIFF MAZE FORMULA
 IF R1<145 THEN L2022
 IF R2<>C0 THEN L2022
 R2=R1
 ADD R2,I1
.L2017 IF R2<166 THEN L2022
 X1=21
 SUB R2,X1
 GOTO L2017
.L2022
 GOSUB @L26000 ;ENDGAME SPECIAL CASES
 IF R2=C0 THEN L2070
 IF X1<C2 THEN L2100
 IF R1>110 THEN L2100
 X2=LIST1(R1)
 IF X2=C1 THEN L2100 ;GRATES OPEN
 GOSUB L2300 ;SPECIAL CASES 1
 GOTO @L1000
.L2070 MESSAGE 4 ;Notpos
 GOTO @L1000
.L2100 IF R2<>C8 THEN L2110
 MESSAGE 28 ;DIES
 GOTO @L1500
.L2110 GOSUB @L12000 ;LIGHT IN R1:L3=1,R2:L4=1
 IF L3=C1 THEN L2200
 IF L4=C1 THEN L2200
 RANDOM X1
 IF X1>85 THEN L2200
 MESSAGE 52 ;Die
 GOTO @L1500
.L2200
 GOSUB L2500 ;SPECIAL CASES 2
.L2210

 IF R1<>33 THEN NOTLENSMOVE ;>>
 IF R2<>34 THEN NOTLENSMOVE ;>>
 GOSUB @LENSLOKAPPOLOGY ;>>MIKE 15/9/86 MESSAGE 2330 ; sorry about this!
 GOSUB @LENSLOK ;>>
 IF RESULT=FALSE THEN @L4000 ;>>
.NOTLENSMOVE ;>>
 GOSUB @SAVEOOPS ;>> MIKE 9/7/86
 R1=R2
 GOTO @L4000 ;DESC ROOM
.L2300 ; MESSAGES FOR GRATES ETC.
 IF R1<>4 THEN L2340
 IF R2<>17 THEN L2340
.L2330 MESSAGE 16 ;Grate locked
 GOTO @L1000
.L2340
 IF R1<>17 THEN L2370
 IF R2=4 THEN L2330 ;GRATE LOCKED
.L2370
 IF R1<>52 THEN L2390
 MESSAGE 32 ;SNAKE
.L2390
 IF R1<>58 THEN L2420
 IF R2<>59 THEN L2420
.L2410 GOTO L2070 ;NOTPOS-CRYSTAL BRIDGE
.L2420
 IF R1<>59 THEN L2430
 IF R2=58 THEN L2410 ;CRYSTAL BRIDGE
.L2430 IF R1<>92 THEN L2440
 IF R2<>94 THEN L2440
 MESSAGE 57 ;NOPLANT
.L2440 IF R1<>107 THEN L2450
 IF R2<>71 THEN L2450
 MESSAGE 58 ;GATE
.L2450
 RETURN
.L2500 ; SPECIAL CASES
 GOSUB @L28100 ;SPIDER SPECIAL CASES
 X1=LIST3(8) ;GOLD
 IF X1<>C8 THEN L2600
 IF R1<>50 THEN L2560
 IF R2<>31 THEN L2560
 MESSAGE 56 ;DOME UNCLIMABLE
 GOTO @L1000
.L2560 IF R1<>31 THEN L2600
 IF R2<>50 THEN L2600
 MESSAGE 28 ;DIE
 GOTO @L1500
 ;
.L2600 IF R2<>244 THEN L2700 ;RANDOM BEDQUILT
.L2610 RANDOM X1
 IF X1>9 THEN L2610
 R2=97
 ADD R2,X1
 RETURN
 ;
.L2700
 IF R1<>104 THEN L2800
 IF R2<>101 THEN L2750
.L2720 RANDOM X2 ;SWISS TO ORIENTAL
 IF X2<120 THEN @L2070 ;NOTPOS
 RETURN
 ;
.L2750 IF R2=85 THEN L2720 ;SWISS-TALL EW
 ;
.L2800 IF R1=78 THEN L2820 ;ALCOVE
 IF R1<>18 THEN L2900
.L2820 IF R2=18 THEN L2835
 IF R2<>78 THEN L2900
.L2835 R4=R2
 R2=C8
 R3=C8
 GOSUB @L11000 ;OBJECT COUNT
 IF N5=C0 THEN L2895 ;POSSIBLE INDEED!
 IF N5>C1 THEN L2897 ;NOTPOS
 X1=LIST3(14) ;EGG JEWEL
 IF X1<>C8 THEN L2897 ;NOTPOS
.L2895 R2=R4
 RETURN
.L2897 MESSAGE 649 ;SOMETHING TOO BIG
 R2=R1
 RETURN
.L2900 ; WEST FROM SW REPOSITORY
 IF R2<>252 THEN L3000
 IF E1<>C1 THEN @L2070 ;NOTPOS
 R2=171 ;CORRIDOR
 RETURN
.L3000 ; TROLL BRIDGE
 IF R1=93 THEN L3030
 IF R1<>32 THEN L3500 ;NOT TROLL B.
.L3030 IF R2=32 THEN L3050
 IF R2<>93 THEN L3500 ;NOT TROLL B.
.L3050 IF F4=C0 THEN L3100 ;TROLL APPEARS
 IF F4=C1 THEN L3150 ;BLOCKS WAY
 X1=LIST3(12) ;BEAR
 IF X1=C8 THEN L3200
 IF F4<>C2 THEN L3090
 F4=C0
.L3090 RETURN
 ;
.L3100 ; TROLL APPEARS
 MESSAGE 62
 F4=C1
 LIST1(93)=C1
 LIST1(32)=C1
 GOTO @L1000
 ;
.L3150 ; TROLL ALREADY THERE
 MESSAGE 63
 GOTO @L1000
 ;
.L3200 ; CARRYING BEAR OVER TROLL BRIDGE
 MESSAGE 90 ;Bridge collapses
 LIST3(12)=C9 ;KILL BEAR   
 GOTO @L1500 ;DEATH
 ;
.L3300 ; WITT'S END
 IF R1<>77 THEN @L2110
 IF R2<>254 THEN L3360
 W1=180
.L3340 MESSAGE 43 ;Maze,NoGo
 GOTO @L1000
.L3360 IF R2<>76 THEN L3340
 RANDOM X1
 IF X1>178 THEN L3340 ;>>
;>> IF X1>100 THEN L3340 
 GOTO @L2210 ;MOVE
;-----
.L3500 ; OUT OF GRATE IN ENDGAME
 IF E1=C0 THEN L3090 ;RETURN
 IF R1<>17 THEN L3090 ;RETURN
 IF R2<>4 THEN L3090 ;RETURN
 E4=C1 ;MET KING
 MESSAGE 687 ;ELVES WAITING FOR YOU
 CLS G ;>> PETE 7/7/86
 LASTPICTURE=501 ;>> DRAW TREES AT GAME END. PETE 7/7/86
 PICTURE LASTPICTURE ;>> PETE 7/7/86
 GOTO @L1700 ;END GAME
;-----
.LOOK ;>>
 IF I2<>2900 THEN L4000 ;preposition ?
 I2=I3
 I3=0
 SUB I4,C1
 GOTO @EXAMINE
;-----

.DESCRIBE ;>>
.L4000 ; DESCRIBE
 GOSUB @L12000 ;LIGHT DETECTION
 IF L3<>C0 THEN L4050
 CLS G ;>>
 LASTPICTURE=0 ;>>Redraw picture if user moves back to previous room
 PICTURE LASTPICTURE ;>>MIKE 2/8/86
 MESSAGE 59 ;Dark
; and go to a black screen...
 X1=19
 LIST9(C0)=X1
 LIST9(C1)=C0
 LIST9(C2)=C0
 DRIVER

 GOTO @L1000
.L4050

 X1=500         ;>>
 ADD X1,R1      ;>>
 IF LASTPICTURE=X1 THEN SAMEPICTURE ;>>
 CLS G          ;>>
 PICTURE X1     ;>>
 LASTPICTURE=X1 ;>>
.SAMEPICTURE    ;>>

 X1=100
 ADD X1,R1
 MESSAGE X1 ;Room description
 MESSAGE DOT ;>>
 MESSAGE 12 ;CR
 ; MESSAGE DESCRIPTION FOR FEATURE
 IF R1<>199 THEN L4109
 IF E3=C1 THEN L4105
 MESSAGE 672 ;DOOR OPEN
 GOTO L4109
.L4105 MESSAGE 673 ;DOOR LOCKED
.L4109 IF R1>110 THEN L4200
 X1=LIST1(R1)
 X2=330 ;GATES ETC. CLOSED
 IF X1<>C1 THEN L4150
 X2=440 ;GATES ETC. OPEN
.L4150 ADD X2,R1
 MESSAGE X2
;
 IF R1<>92 THEN L4200 ;PLANT ROOM
 X4=642
 IF F6=C0 THEN L4190
 X4=643
 IF F6<>C1 THEN L4200
.L4190 MESSAGE X4;INTERMEDIATE PLANT DESCRIPTIONS
.L4200 IF R1=195 THEN L4202
 IF R1<>197 THEN L4205
.L4202 IF B1=C0 THEN L4205
 MESSAGE 498 ;CRYSTAL BRIDGE IS THERE
.L4205 IF R1<>191 THEN L4209 ;ELVES
 IF E2=C1 THEN L4209
 MESSAGE 663 ;PILES OF SKELETONS
.L4209 GOSUB @L28500 ;CHAINED ELVES DESCR.
 ; LIST ANY OBJECTS LYING AROUND
 MESSAGE 12 ;CR
 R3=R1
.L4220 X1=C1
.L4250 GOSUB @L13000 ;DESCRIBE OBJECT
 ADD X1,C1
 IF X1<33 THEN L4250
 ;
 ; DESCRIBE ANY DWARVES
 GOSUB @L28400 ;MESSAGE SPIDER DESC
 IF R1<50 THEN @L1000
 IF D4=C0 THEN @L1000
 IF D4>C1 THEN L4350
 MESSAGE 97 ;ONE DWARF HERE
 GOTO @L1000
.L4350 MESSAGE 98 ;There are
 PRINT D4
 MESSAGE 99 ;Dwarves here
 GOTO @L1000
 ;
.L4500 ; INVENTORY
 MESSAGE 10 ;You have
 MESSAGE 12 ;CR
 R2=C8
 R3=C8
 GOSUB @L11000 ;OBJECT COUNT
 IF N5>C0 THEN L4220 ;LIST OBJECTS
 MESSAGE 11 ;Nothing at all !
 GOTO @L1000
;-----
.L5000 ; TAKE OBJECT
 IF I2<>C0 THEN L5040
 IF I4=C2 THEN L5035 ;2 ARGS GIVEN
 IF EVERYTHING=TRUE THEN @SPECIALALL ;>>MIKE 9/9/86
 MESSAGE 5 ;DON'T UNDERSTAND
 GOTO @L1000
.L5035 ; SECOND OF TWO ARGS NOT UNDERSTOOD
 MESSAGE 60 ;NO-OBJ
 GOTO @L1000
.L5040

 IF I2<>69 THEN NOTTAKEDWARF ;>> NICK 15/7/86
 IF D4=C0 THEN NOTTAKEDWARF ;>> NICK 15/7/86
 MESSAGE 78 ;>> NICK 15/7/86 Too agile
 GOTO @L1000 ;>> NICK 15/7/86
.NOTTAKEDWARF ;>> NICK 15/7/86

 GOSUB @L5300 ;CONVERT I2
 IF R1<>40 THEN L5049 ;BEAR ROOM
 IF F3<>C2 THEN L5049 ;BEAR PLACID
 MESSAGE 47 ;BEAR SNAPS ETC.
 GOTO @L1000
.L5049
 IF I2>LIST3MAX THEN @SILLY ;>>MIKE 27/8/86
 X1=LIST3(I2)
 IF X1=R1 THEN L5120
 IF X1<>C8 THEN L5090
.ALREADYHAVEIT
 MESSAGE 61 ;Already have it
 GOTO @L1000
.L5090 MESSAGE 8 ;Don't see it
 GOTO @L1000
.L5120 IF I2<>7 THEN L5150 ;NOT BIRD
 IF F2=C0 THEN L5150
 GOTO ALREADYHAVEIT ;>>MIKE 26/8/86 - L5090 ;CAN'T GET BIRD IF CAGED
.L5150 LIST5(I2)=C1 ;OBJECT SEEN
 GOSUB @L21000 ;SEE IF ALL OBJECTS FOUND
 R2=C8
 R3=C8
 GOSUB @L11000 ;COUNT OBJECTS
 IF N5<4 THEN L5250
 MESSAGE 46 ;Hands full
 GOTO @L1000
.L5250 GOSUB @L17000 ;BIRD IS SPECIAL
 LIST3(I2)=C8 ;CARRIED
 MESSAGE ok ;>>mike 19/9/86 - was 648 ;I HAVE IT NOW
 GOTO @L1000
 ;
.L5300 ; TRANSLATE I2 TO OBJECT NO.
 GOSUB @CONVERTI2 ;>>
 IF I2=65529 THEN @L1200 ;BE MORE SPECIFIC
 RETURN
;-----
.L5500 ; DROP OBJECT
 GOSUB L5300 ;CONVERT I2
 IF I2>LIST3MAX THEN @SILLY ;>>MIKE 27/8/86
 X1=LIST3(I2)
 IF X1=C8 THEN L5650
 MESSAGE 9 ;I don't have it
 GOTO @L1000
 ;
.L5600 ; ACTUALLY DROP IT
 MESSAGE 645 ;Dropped
.SILENTDROP LIST3(I2)=R1
 GOTO @L1000

.L5650 ; BIRD
 IF S5<>C8 THEN L5663
 IF R1=209 THEN @L28300 ;SPIDER EATS HIM
.L5663 IF I2<>7 THEN L5700 ;BIRD
 F2=C0
 IF R1=68 THEN L5695 ;DRAG?
 IF R1<>52 THEN L5600 ;KINGS
 MESSAGE 34 ;BIRD ATTACKS
 LIST1(52)=C1
 GOTO SILENTDROP ;>>L5600
.L5695 X1=LIST1(R1)
 IF X1=C8 THEN L5600
 MESSAGE 39 ;BIRD GETS EATEN
 LIST3(7)=C9
 GOTO @L1000
.L5700 ; BEAR+TROLL
 IF I2<>12 THEN L5750
 IF R1<>32 THEN L5600
 IF F4>1 THEN L5600 ;TROLL NOT THERE
 MESSAGE 50 ;Bear attacks
 LIST1(32)=C9
 LIST1(93)=C9
 F4=C3 ;TROLL GONE
 GOTO SILENTDROP ;>>L5600
.L5750 ; MING VASE
 IF I2<>17 THEN L5850
 X1=LIST3(16) ;PILLOW
 IF X1=R1 THEN L5600
 IF R1=84 THEN L5600 ;SOFT ROOM
 MESSAGE 45 ;VASE BREAKS
 LIST3(17)=C9 ;GET RID OF VASE   
 GOTO @L1000
 ;
.L5850 ; COINS
 IF I2<>21 THEN L5900
 IF R1<>145 THEN L5600 ;VENDING M/C
 L2=C8 ;NEW BATTERIES IN LAMP
 LIST3(21)=C9 ;KILL COINS         
 MESSAGE 650 ;TELL HIM ABOUT IT
 GOTO @L1000
 ;
.L5900 ; CAGE
 IF I2<>5 THEN L5950 ;CAGE
 IF F2=C0 THEN L5950 ;CAGE EMPTY
 LIST3(7)=R1
 GOTO @L5600
.L5950 ; ELIXIR
 IF I2<>29 THEN L5990
 IF R1<>191 THEN L5970
 MESSAGE 662 ;ELVES ARISE
 E2=C1 ;ELVES FREE
.L5965 LIST3(29)=C9 ;KILL ELIXIR
 GOTO @L1000
.L5970 MESSAGE 661
 GOTO L5965
.L5990 ; LITTER IN PICNIC SPOT
 IF R1<>13 THEN @L5600
 MESSAGE 683 ;LIGHTENING GETS YOU
 GOTO @L1500
;-----
.L6000 ; THROW
 IF R1<>40 THEN L6000A ;>>MIKE 9/7/86
 IF I2=38 THEN @FEEDBEAR ;>> "THROW FOOD" = "FEED BEAR"
.L6000A ;>>MIKE 9/7/86
 GOSUB @L5300 ;CONVERT I2
 O1=I2
 GOSUB @L14000 ;CHECK O1 CARRIED
 GOSUB @L31000 ;DIE IN REPOSITORY
 IF O1=28 THEN @L28200 ;JADE SPIDER
.L6040
 IF R1=93 THEN L6250 ;TROLL
 IF R1=32 THEN L6250 ;TROLL
 IF R1=68 THEN L6350 ;DRAGON
 IF I2<>26 THEN @L5650;NOT AXE,SO DROP IT
.L6080
 IF D4=C0 THEN THROWATDRAGON ;>>MIKE 27/8/86 -@L5650 ;NO DWARVES TO KILL
 ; KILL DWARVES
 RANDOM X1
 IF X1>30 THEN L6140
 MESSAGE 65 ;Missed
 GOTO @SILENTDROP ;>> @L5650 ;DROP AXE
.L6140 X1=66 ;THE DWARF DIES
 IF D4=C1 THEN L6170
 X1=67 ;ONE OF THE DWARVES
.L6170 MESSAGE X1
 SUB D4,C1 ;ONE LESS DWARF
 D3=C1 ;DWARVES MAD
 GOTO @SILENTDROP ;>> @L5650 ;DROP AXE
 ;
.THROWATDRAGON ; throw axe at or snake or bear, if here.
; no dwarves around.
 X1=LIST1(R1)
 IF X1<>C0 THEN @L5650 ; drop it
 IF R1=52 THEN THROWSNAKE
 IF R1=68 THEN THROWSNAKE
 GOTO @L5650 ; drop it
.THROWSNAKE
 MESSAGE 696 ; bounces off
 GOTO @SILENTDROP
;---
.L6250 ; TROLL
 IF F4<>C1 THEN L6080 ;NO TROLL
 IF I2=12 THEN @L5700 ;BEAR
 IF I2<>26 THEN L6300
 MESSAGE 64 ;TROLL RETURNS AXE
 GOTO @L1000
 ;
.L6300 X1=LIST4(I2)
 IF X1=C0 THEN L6345 ;WORTHLESS
 LIST3(I2)=C9 ;TROLL GETS TREASURE
 MESSAGE 89 ;TROLL THANKS YOU
 F4=2
 LIST1(93)=C9
 LIST1(32)=C9
 GOTO @L1000
.L6345 MESSAGE 88 ;NOT WORTH ANYTHING
 GOTO @L1000
 ;
.L6350 ; DRAGON
 IF I2<>26 THEN @L5650 ;NOT AXE
 X1=LIST1(R1)
 IF X1<>C0 THEN @L5650 ;NO DRAGON
 MESSAGE 40 ;AXE BOUNCES OFF SCALES
 GOTO @SILENTDROP ;>> @L5600
.L6500 ; OPEN,UNLOCK
 IF I2=65 THEN @L15700 ;SESAME
 IF R1=193 THEN @L6950 ;N ELVES
 IF R1=194 THEN @L6970 ;S ELVES
 IF I2<>35 THEN L6650 ;GRATE
 IF R1=4 THEN L6550
 IF R1=17 THEN L6550
 GOTO @L1250;CAN'T
.L6550 X1=LIST3(C1)
 IF X1=C8 THEN L6600
.L6570 MESSAGE 19 ;NOKEYS
 GOTO @L1000
 ;
.L6600 ; UNLOCK THAT GRATE NOW
 LIST1(4)=C1
 LIST1(17)=C1
 MESSAGE 17 ;TELL HIM ABOUT IT
 GOTO @L1000
 ;
.L6650 ; CHAIN
 IF I2=47 THEN L6665 ;BEAR
 IF I2<>57 THEN L6800;CHAIN
.L6665 IF R1<>40 THEN L6800
 X1=LIST3(C1)
 IF X1<>C8 THEN L6570
 IF F3<>C2 THEN L6690
 MESSAGE 47 ;CAN'T GET TO CHAIN
 GOTO @L1000
.L6690 ; CHAIN APPEARS
 F3=C0 ;BEAR PASSIVE
 X1=40
 LIST3(22)=X1 ;CHAIN
 MESSAGE 48 ;CHAIN CLICKS OPEN
 GOTO @L1000
 ;
.L6800 ; CLAM
 IF I2<>85 THEN @L1250;CAN'T 
 IF R1<>103 THEN @L1350
 X1=LIST1(103)
 IF X1=C0 THEN L6870
 MESSAGE 68 ;It's already open
 GOTO @L1000
.L6870 X1=LIST3(18) ;TRIDENT
 IF X1=C8 THEN L6910
 MESSAGE 41 ;Won't open
 GOTO @L1000
.L6910 MESSAGE 42 ;CLAM OPENS
 X1=81 ;CUL-DE-SAC
 LIST3(19)=X1
 LIST1(103)=C1 ;CLAM=OYSTER NOW
 GOTO @L1000
 ;
.L6950 ; UNLOCK N ELVES
 IF E5=C1 THEN @L1000
 MESSAGE 689 ;ELVES RUSH OFF
 E5=C1
 GOTO @L1000
.L6970 ; UNLOCK S ELVES
 IF E6=C1 THEN @L1000
 MESSAGE 689 ;ELVES RUSH OFF
 E6=C1
 GOTO @L1000
;-----
.TURN
; e.g. TURN LAMP ON/OFF
 IF NOUN1=37 THEN TURNLAMP
 IF NOUN2<>37 THEN @NOTHINGHAPPENS
.TURNLAMP
 IF PREP=2901 THEN L7000 ; on
 IF PREP=2906 THEN L7100 ; off
 GOTO @NOTHINGHAPPENS
;---
.EXTINGUISH
 IF NOUN1=37 THEN L7100 ; lamp off
 GOTO @NOTHINGHAPPENS
;---
.LIGHT
 IF PREP=2906 THEN L7100 ; light off
 IF PREP=2901 THEN L7000 ; light on
 IF NOUN1<>37 THEN @NOTHINGHAPPENS ; not lamp
; drop through to light lamp
.L7000 ; LIGHT ON
 O1=C2
 GOSUB @L14000 ;CHECK OBJECT CARIED
 IF L2=254 THEN @L1250;CAN'T. LAMP DEAD  
 L1=C1
 MESSAGE 14;LAMP NOW ON
 GOTO @DESCRIBE ;>> GOTO @L1000
 ;
.L7100 ; LIGHT OFF
 O1=C2
 GOSUB @L14000 ;CHECK OBJECT CARRIED
 L1=C0
 MESSAGE 15;LAMP NOW OFF
 GOTO @DESCRIBE ;>> @L1000 Clear graphics if now dark
 ;
.L7200 ; FILL
 GOSUB @L5300 ;CONVERT I2
 O1=4 ;BOTTLE
 IF I2<>O1 THEN @L1100
 GOSUB @L14000 ;CHECK OBJECT CARRIED
 IF R1<4 THEN L7300
 IF R1=70 THEN L7300 ;RESERVOIR
 IF R1=73 THEN L7300 ;LITTLE PIT
 IF R1=71 THEN L7300 ;MAG CAVERN
 IF R1=90 THEN L7350 ;OIL WELL
 MESSAGE 69 ;NO FLUIDS
 GOTO @L1000
.L7300 ; FILL WITH WATER
 MESSAGE OK ;>>MIKE 23/6/86
 F1=C1
 GOTO @L1000
.L7350 ; FILL WITH OIL
 MESSAGE OK ;>>MIKE 23/6/86
 F1=C2
 GOTO @L1000
;-----
.L7500 ; EMPTY
 GOSUB @L5300 ;CONVERT I2
 O1=4
 IF O1<>I2 THEN @L1100;DON'T UNDERSTAND
 GOSUB @L14000 ;CHECK O1 CARRIED
 IF F1=C1 THEN L7600
 IF F1=C2 THEN L7800
 MESSAGE 70 ;ALREADY EMPTY
 GOTO @L1000
 ;
.L7600 ; WATER
 IF F1<>C1 THEN L7930  ;HAVE NONE
 IF R1<>92 THEN L7720
 IF F6=C0 THEN L7660 ;PLANT STATUS
 IF F6=C1 THEN L7690
 MESSAGE 74 ;SHRIVELS
 F6=C0
 LIST1(92)=C9 ;KILL ROOM STATUS
 GOTO L7730
.L7660 MESSAGE 72 ;PLANT GROWS
 F6=C1
 GOTO L7730
.L7690 MESSAGE 73 ;PLANT FINISHES GROWS
 F6=C2
 LIST1(92)=C1 ;SET ROOM STATUS
 GOTO L7730
.L7720 MESSAGE 21 ;WATER ON GROUND
.L7730 F1=C0 ;BOTTLE EMPTY
 GOTO @L1000
 ;
.L7800 ; OIL
 IF F1<>C2 THEN L7930 ;NO OIL
 IF R1<>107 THEN L7850
 MESSAGE 75 ;GATE OPENS
 LIST1(107)=C1
 LIST1(71)=C1
 GOTO L7730
.L7850 IF R1<>92 THEN L7880
 MESSAGE 76 ;PLANT+OIL
 GOTO L7730
.L7880 MESSAGE 22 ;OIL ONGROUND
 GOTO L7730
;-----
.L7900 ; POUR WATER/OIL
 X1=LIST3(4)
 IF X1=C8 THEN L7950
.L7930 MESSAGE 71 ;HAVEN'T GOT ANY
 GOTO @L1000
.L7950 IF I2=29 THEN L7980 ;WATER
 IF I2<>28 THEN @L1100 ;NOT OIL
 IF F1<>C2 THEN L7930
 GOTO L7800
.L7980 IF F1<>C1 THEN L7930
 GOTO L7600
;-----
.L8000 ; ATTACK
 GOSUB @L31000 ;DIE IN REPOSITORY
 IF I2<>42 THEN L8100 ;BIRD
 X1=LIST3(7)
 IF X1<>R1 THEN @L1450
 MESSAGE 26 ;BIRD DIES
 LIST3(7)=C9 ;AND VANISHES
 GOTO @L1000
 ;
.L8100 IF I2<>73 THEN L8200 ;SNAKE
 IF R1<>52 THEN @L1450 ;KINGS
.L8120 X1=LIST1(R1)
 IF X1<>C0 THEN @L1450
 MESSAGE 33;Dont bother attacking snake
 GOTO @L1000
 ;
.L8200 IF I2<>72 THEN L8300 ;DRAGON
 IF R1<>68 THEN @L1450 ;SECRET NS
.L8220 X1=LIST1(R1)
 IF X1<>C0 THEN @L1450

.ASKDRAGON ;>>
 MESSAGE 693 ;>>With bare hands ?
 GOSUB @ASKINPUT ;>>
 IF RESULT=FALSE THEN ASKDRAGON ;>>
 IF I1=INO THEN @L1000 ;>>
 IF I1=INORTH THEN @L1000 ;>>
 IF I1<>IYES THEN ASKDRAGON ;>>

 MESSAGE 38 ;KILL DRAGON WITH BARE HANDS
 LIST1(R1)=C1
 LIST3(23)=R1
 GOTO @L1000
 ;
.L8300 IF I2<>47 THEN L8400 ;BEAR
 X1=LIST3(12)
 IF X1=C8 THEN L8340
 IF X1<>R1 THEN @L1450
.L8340 MESSAGE 49 ;BEAR DODGES
 GOTO @L1000
 ;
.L8400 IF I2<>69 THEN L8500 ;DWARF
 IF D4=C0 THEN @L1450
.L8420 MESSAGE 78 ;DWARVES EVADE YOU
 D3=C1
 GOTO @L1000
 ;
.L8500 IF I2<>C0 THEN @L1250;CAN'T
 IF R1=52 THEN L8120
 IF R1=68 THEN L8220
 IF D4<>C0 THEN L8420
 GOTO @L1450
 ;
.L8600 ; READ
; IF I2=?? THEN @EXAMINE ;>>MIKE 18/8/86
 IF I2<>55 THEN @L1250;CAN'T
 X1=LIST3(20) ;MAG
 IF X1<>C8 THEN @L1400;DON'T HAVE IT
 MESSAGE 79 ;magazine message
 GOTO @L1000
 ;
.L8700 ; SCORE
 MESSAGE 80 ;If you were to stop now,
 GOSUB L9000 ;CALCULATE SCORE
 PRINT S4
 MESSAGE 81 ;OUT OF A MAX OF 350
 GOTO @L1000
;-----
.L9000 ; SCORING
 S4=30
 X1=D1 ;NUMDIE
 ADD X1,X1 ;X1:=D1*10
 X2=X1
 ADD X2,X2
 ADD X2,X2
 ADD X2,X1
 SUB S4,X2
 ;
 IF S1<>C1 THEN L9130
 X1=30;TO MISTS
 ADD S4,X1
.L9130 ;
 IF S2<>C0 THEN L9170
 X1=9 ;HE QUIT
 ADD S4,X1
.L9170 ; HAS ENDGAME CLOCK BEEN STARTED?
 IF T2=C0 THEN L9210
 X1=50
 ADD S4,X1
.L9210 X1=LIST3(20) ;MAGAZINE
 IF X1<>77 THEN L9240 ;WITT'S END
 X1=5
 ADD S4,X1
.L9240 ; HAS HE SAVED/RESTORED?1 LESS IF SO
 ADD S4,S6
 ; ADD ENDGAME SCORE (IF ANY)
 ADD S4,S3
 ;
 ; NOW THE TREASURES
 ; 5 POINTS FOR SEEING EACH
 ; 10 FOR TAKING BACK TO BUILDING
 X1=C1
.L9340 X2=LIST4(X1);VALUE
 IF X2=C0 THEN L9440
 X2=LIST5(X1) ;HAS IT BEEN FOUND ?
 IF X2=C0 THEN L9440
 IF X1>28 THEN L9400 ;NO FOUND FOR CROWNS
 X3=5
 ADD S4,X3
.L9400
 X2=LIST3(X1) ; list3 is always within range
 IF X2=C8 THEN L9420 ;CARRIED
 IF X2<>14 THEN L9440 ;BUILDING
.L9420 X3=10
 IF X1<30 THEN L9430
 X3=50 ;SCORE FOR CROWN JEWELS
.L9430 ADD S4,X3
.L9440 ADD X1,C1
 IF X1<33 THEN L9340
 ; HAS HE FREED THE ELVES?
 X3=100
 IF E2=C0 THEN L9464
 ADD S4,X3
.L9464 IF E5=C0 THEN L9466
 ADD S4,X3
.L9466 IF E6=C0 THEN L9468
 ADD S4,X3
.L9468 IF E4=C0 THEN L9470 ;ELVEN KING ?
 ADD S4,X3
.L9470 ;
 GOSUB @L30000;CALCULATE RATING
 RETURN

;-----

.L9500 ; QUIT
 MESSAGE 83;REALLY QUIT?
;>> INPUT I1 I2 I3 I4
 GOSUB @ASKINPUT ;>>
 IF RESULT=FALSE THEN L9500 ;>>
 IF I1=INORTH THEN @ENDCOMMAND ;North
 IF I1=INO THEN @ENDCOMMAND ;No
 IF I1=IYES THEN @ABORTGAME ;Play again
 MESSAGE 2312;YES OR NO!
 GOTO L9500

;-----

;>>.L9500 ; QUIT
;>> MESSAGE 83 ;Really want to quit now ?
;>>.L9512
;>> GOSUB @CLEARINPUT ;>>
;>> GOSUB GETI1I2I3I4 ;>> INPUT I1 I2 I3 I4
;>> GOSUB @L18000 ;GET YES/NO RESPONSE
;>> IF Y1=C2 THEN L9512
;>> IF Y1=C0 THEN @L1000 ;NO
;>> ;MUST BE YES
;>> S2=C1 ;HE QUIT
;>> GOTO @L1700 ;SCORE+STOP

;-----

.L9600 ; PLUGH
;>>MIKE 9/7/86 GOSUB @SAVEOOPS ;>>
 GOSUB @L31000 ;DIE IN REPOSITORY
 IF T2>C0 THEN L9670
 IF R1<>14 THEN L9640 ;BUILDING
 R1=55
.L9630
 GOTO @L4000 ;DESCRIBE
.L9640 IF R1<>55 THEN L9670 ;Y2
 R1=14
 GOTO L9630
.L9670 MESSAGE 23 ;Nothing happens
 GOTO @L1000
 ;
.L9700 ; XYZZY
 GOSUB @L31000 ;DIE IN REPOSITORY
 IF T2>C0 THEN L9670
 IF R1<>14 THEN L9740 ;BUILDING
 R1=28
 GOTO L9630
.L9740 IF R1<>28 THEN L9670 ;DEBRIS ROOM
 R1=14
 GOTO L9630
 ;
.L9800 ; PLOVER
 GOSUB @L31000 ;DIE IN REPOSITORY
 IF T2>C0 THEN L9670 ;ENDGAME
 R3=R1
 IF R1<>55 THEN L9840 ;Y2
 R1=18
 GOTO L9860
.L9840 IF R1<>18 THEN L9670 ;NOTHING HAPPENS
 R1=55
.L9860 R2=C8
 O1=LIST3(13) ;PYRAMID
 GOSUB L11000 ;DROP TRASURE
 LIST3(13)=O1
 GOTO L9630
 ; SET UP CONSTANTS
 C1=1
 C2=2
 C8=255
 C9=C0
 RETURN
;-----
.L11000 ; ALL OBJECTS CARRIED GO TO R2
 ; TREASURE GOES TO R3
 ; RETURNS N3=NO. OF TREASURES
 ; RETURNS N4=NO. OBJECTS,ALLOWING FOR
 ;  BIRD
 ; RETURNS N5=TOTAL NO. CARRIED
 X1=C1
 N3=C0
 N4=C0
.L11070 IF X1=7 THEN L11150 ;BIRD
 X2=LIST3(X1) ; list3 is always within range
 IF X2<>C8 THEN L11150
 X2=LIST4(X1) ;VALUE OF OBJECT
 IF X2=C0 THEN L11140 ;WORTHLESS
 ADD N3,C1 ;ANOTHER TREASURE
 LIST3(X1)=R3
 GOTO L11150
.L11140 ADD N4,C1 ;ANOTHER OBJECT
 LIST3(X1)=R2
.L11150 ADD X1,C1
 IF X1<33 THEN L11070
 N5=N3
 ADD N5,N4
 RETURN
;-----
.L12000 ; IF LIGHT IN R1,L3=1
 ; IF LIGHT IN R2,L4=1
 R3=R1
 GOSUB L12500
 L3=L6
 R3=R2
 GOSUB L12500
 L4=L6
 RETURN
 ;
.L12500 ; CHECK IF LIGHT IN R3,L6=1
 IF R3<26 THEN L12600 ;NATURALLY LIGHT
 IF R3>225 THEN L12600 ;NATURALLY LIGHT
 IF R3<166 THEN L12520
 IF R3<169 THEN L12600 ;NATURALLY LIGHT
.L12520 L6=LIST3(C2) ;LAMP
 IF L6=C8 THEN L12590
 IF L6=R3 THEN L12590
 ; NO LAMP
.L12560 ; ROOM DARK
 L6=LIST3(28) ;PENTACLE
 IF L6=C8 THEN L12610
.L12570 L6=C0
 RETURN
.L12590 IF L1=C0 THEN L12560 ;LIGHT OFF
.L12600 ; ROOM IS LIGHT (HOORAY !)
 L6=LIST3(28) ;PENTACLE
 IF L6=C8 THEN L12570 ;ROOM DARK
.L12610 L6=C1
 RETURN
;-----
.L13000 ; MESSAGE DESCRIPTION OF OBJECT X1
 ; IN ROOM R3
 R2=LIST3(X1) ; prbably always in range
 IF R2=R3 THEN L13050
 RETURN
.L13050 LIST5(X1)=C1 ;OBJECT HAS BEEN SEEN
 ;X4=550;OBJECTS WHEN CARRIED
 ;IF R3=C8 THEN L13056
 ;X4=585;OBJECTS ON GROUND
.L13056 IF F2=C0 THEN L13150 ;CAGE EMPTY
 IF X1=7 THEN L13080 ;BIRD
 IF X1<>5 THEN L13150
 RETURN;NO DESC FOR OCCUPIED CAGE
.L13080 X4=631 ;BIRD IN CAGE ON FLOOR
 IF R3<>C8 THEN L13110
 X4=630 ;IN HAND
.L13110 GOTO L13650
 ;
.L13150 IF X1<>12 THEN L13250 ;BEAR
 IF F3=C0 THEN L13600 ;NOTHING SPECIAL
 X4=632
 ADD X4,F3
 GOTO L13650 ;MESSAGE DESC + RETURN
 ;
.L13250 ; MING VASE ON THE PILLOW
 IF X1=17 THEN L13300 ;VASE
 IF X1=16 THEN L13300 ;PILLOW
 GOTO L13400
 ;
.L13300 X2=LIST3(17) ;VASE ROOM
 X3=LIST3(16) ;PILLOW ROOM
 IF X2<>X3 THEN L13600 ;NO SPECIAL
 IF X2=C8 THEN L13600
 IF X1=16 THEN L13660 ;NO DESC FOR PILOW
 X4=635 ;VASE ON PILLOW
 GOTO L13650
.L13400 ; BOTTLE
 IF X1<>4 THEN L13500
 X4=636 ;CARRIED
 IF R2=C8 THEN L13440
 X4=639 ;ON GROUND
.L13440 ADD X4,F1 ;CONTENTS OF BOTTLE
 GOTO L13650
 ;
.L13500 ; LAMP
.L13600 ; MESSAGE 'NORMAL' DESCRIPTION
 X4=550;OBJECTS WHEN CARRIED
 IF R3=C8 THEN L13640
 X4=585;OBJECTS ON GROUND
.L13640 ADD X4,X1 ;CALCULATE DESCRIPTION NO.
.L13650 MESSAGE X4
 MESSAGE DOTCR ;>> 12 ;CR
.L13660 RETURN
 ;
;-----
.L14000 ; CHECK OBJECT O1 IS CARRIED
 IF O1>LIST3MAX THEN @SILLY ;>>MIKE 27/8/86
 O2=LIST3(O1)
 IF O2<>C8 THEN @L1400 ;DONT HAVE IT
 RETURN
;-----
.FEEDBEAR ;>>>
 I2=47    ;>>>

.L15000 ; FEED
 X1=LIST3(3) ;FOOD
 IF X1=C8 THEN L15050
 MESSAGE 71 ;HAVEN'T GOT ANY
 GOTO @L1000
.L15050
 IF I2=72 THEN L15100 ;DRAGON
 IF I2=73 THEN L15150 ;SNAKE
 IF I2=42 THEN L15200 ;BIRD
 IF I2=47 THEN L15250 ;BEAR
 IF I2=38 THEN L15250 ; FEED FOOD->FEED BEAR >>MIKE 28/8/86
 IF I2<>C0 THEN @L1250;CAN'T
 GOTO @L1100 ;DON'T UNDERSTAND
 ;
.L15100 X1=LIST1(68) ;DRAGON
 IF R1<>68 THEN @L1350 ;NOT HERE
.L15120 IF X1=C1 THEN @L1350
 MESSAGE 37 ;NOT HUNGRY
 GOTO @L1000
 ;
.L15150 X1=LIST1(52) ;SNAKE
 IF R1<>52 THEN @L1350 ;NOT HERE
 GOTO L15120
 ;
.L15200 X1=LIST3(7) ;BIRD
 IF X1<>R1 THEN @L1350 ;NOT HERE
 MESSAGE 27 ;NOT HUNGRY
 GOTO @L1000
 ;
.L15250 ; BEAR
 IF R1<>40 THEN @L1350 ;NOT HERE
 LIST3(3)=C9 ;FOOD DISAPPEARS
 F3=C1 ;BEAR PASSIVE
 MESSAGE 84 ;BEAR EATS IT !
 GOTO @L1000
;-----
.L15400 ; WAVE
 IF I2=41 THEN L15500 ;ROD
; IF I2<>C0 THEN L15450 ;SILLY
 IF R1=56 THEN L15650 ;WINDOW
 IF R1=108 THEN L15650 ;WINDOW
.L15450
.NOTHINGHAPPENS ;>>
 MESSAGE 23 ;NOTHING HAPPENS
 GOTO @L1000
 ;
.L15500 ; (WAVE) ROD
 O1=6 ;ROD
 GOSUB @L14000 ;CHECK IT'S CARRIED
 IF R1=195 THEN L15635
 IF R1=197 THEN L15635
 IF R1=58 THEN L15510
 IF R1<>59 THEN L15450
.L15510 X1=LIST1(58)
 IF X1=C0 THEN L15590
 ; BRIDGE THERE
 X1=C0
 MESSAGE 31 ;VANISHES
.L15560 LIST1(58)=X1
 LIST1(59)=X1
 GOTO @L1000
.L15590 ; BRIDGE NOT THERE
 X1=C1
 MESSAGE 30 ;BRIDGE APPEARS
 GOTO L15560
 ;
.L15635 ; ROD IN ENDGAME AT FISSURE
 IF B1=C0 THEN L15645
 B1=C0
 MESSAGE 31 ;VANISHES
 GOTO @L1000
.L15645 B1=C1
 MESSAGE 30 ;APPEARS
 GOTO @L1000
.L15650 ; WINDOW
 MESSAGE 36 ;FIGURE WAVES BACK
 GOTO @L1000
;-----
.L15700 ; SESAME ETC.
 GOSUB @L31000 ;DIE IN REPOSITORY
 MESSAGE 23 ;NOTHING HAPPENS
 GOTO @L1000
 ;
.L15800 ; SMASH
 GOSUB @L31000 ;DIE IN REPOSITORY
 IF I2<>86 THEN @L1250;CAN'T 
 MESSAGE 85;CAN'T BE REACHED (MIRROR)
 GOTO @L1000
 ;
.L15900 ; EAT
 IF I2<>38 THEN @L1250;CAN'T   
 X1=LIST3(3)
 IF X1<>C8 THEN L16030
 LIST3(3)=C9 ;GET RID OF FOOD
.L15950 MESSAGE 3 ;THANKS
 GOTO @L1000
 ;
.L16000 ; DRINK
 X1=LIST3(4) ;BOTTLE
 IF X1=C8 THEN L16050
.L16030 MESSAGE 71 ;HAVEN'T GOT ANY
 GOTO @L1000
.L16050 IF F1=C0 THEN L16030 ;BOTTLE EMPTY
 IF F1<>C1 THEN L16100
 IF I2<>29 THEN L16030 ;WATER
 F1=C0
 GOTO L15950 ;THANKS
.L16100 IF I2<>28 THEN L16030 ;OIL
 MESSAGE 86 ;NO THANKS!
 GOTO @L1000
;-----
.L16200 ; FEE,FIE,FOE,FOO
 ;X1=79
 ;SUB I1,X1
 ;IF I1=F7 THEN L16300
 ;MESSAGE 44;GET IT RIGHT
 ;F7=0
 ;GOTO L1000
 ;
.L16300 ADD F7,C1
 IF F7<4 THEN @L1000
 ; MOVE EGGS BACK TO GIANT ROOM
 F7=C0 
 X1=LIST3(15)
 IF X1=C8 THEN L16338
 IF X1<>R1 THEN L16340
.L16338 MESSAGE 647 ;EGGS VANISH
.L16340 X1=95
 LIST3(15)=X1
 IF X1<>R1 THEN L16360
 MESSAGE 646 ;EGGS REAPPEAR
.L16360 GOTO @L1000
;-----
.L16500 ; GIVE (TO TROLL)
 IF R1=93 THEN @L6000 ;THROW
 IF R1=32 THEN @L6000 ;THROW
 IF R1<>40 THEN L16500A ;>>MIKE 9/7/86
 IF I2=38 THEN @FEEDBEAR ;>> "GIVE FOOD" = "FEED BEAR"
.L16500A
 MESSAGE 87 ;TO WHOM ?
 GOTO @L1000
;-----
.L17000 ; BIRD BEING PICKED UP IS AWKWARD
 IF I2<>7 THEN L17500
 X1=LIST3(5) ;CAGE
 IF X1=C8 THEN L17100
 MESSAGE 24;CAN'T CATCH THE BIRD
 GOTO @L1000
.L17100 X1=LIST3(6)
 IF X1=C8 THEN L17200
 F2=C1
 RETURN
.L17200 ; OH DEAR, HE'S GOT THE ROD
 MESSAGE 25 ;BIRD FLIES AWAY
 GOTO @L1000
.L17500 IF I2<>12 THEN L17600 ;BEAR
 IF F3<>C2 THEN L17550
 MESSAGE 47 ;CAN'T GET NEAR IT
 GOTO @L1000
.L17550 IF F3<>C1 THEN L17600
 MESSAGE 51 ;BEAR CHAINED
 GOTO @L1000
.L17600 IF I2<>5 THEN L17700 ;CAGE
 IF F2=C0 THEN L17700
 LIST3(7)=C8
.L17700 RETURN

.L18000 ; GET YES/NO RESPONSE
 ; IF YES,Y1=1. NO,Y1=0
 Y1=C0
 MESSAGE 12 ;CR
 IF I1=75 THEN L18100 ;YES
 IF I1=76 THEN L18150 ;NO
 IF I1=C1 THEN L18150 ;NO(RTH)
 MESSAGE 5 ;DON'T UNDERSTAND
 Y1=C2 ;ERROR
 RETURN
.L18100 Y1=C1
.L18150 RETURN

.L19000 ; RUB
 IF I2<>37 THEN L19520 ;NOT LAMP 
 O1=C2 ;LAMP
 GOSUB @L14000 ;CHECK LAMP CARRIED
 MESSAGE 2 ;RUBBING LAMP IS BORING
 GOTO @L1000
.L19500 ; BLAST
 IF T2>C0 THEN L19550
.L19520 MESSAGE 23;NOTHING HAPPENS
 GOTO @L1000
.L19550 IF E1=C1 THEN L19520
 ; GIVE A BONUS FOR BLASTING
 S3=20
 MESSAGE 651 ;DYMANITE EXPLODES
 R2=LIST3(27) ;DYNAMITE
 IF R2=R1 THEN L19800 ;KILLS YOU
 IF R2=C8 THEN L19800
 IF R2<>170 THEN L19700
 ; DWARVES ARISE
 MESSAGE 652
 MESSAGE 653 ;ROOF KILLS DWARVES
 X1=80 ;KILL DWARVES
 ADD S3,X1
 GOTO @L24000 ;DEATH
.L19700 ; NOW HOLE IN WALL,ETC.
 E1=C1
 X1=180 ;KILLED DWARVES+LIVED!
 ADD S3,X1
 MESSAGE 654 ;DWARVES DEAD ETC.
 W2=169 ;WATER AT NE END
 W3=C1 ;WHICH IS FULL OF WATER
 GOTO @L1000
.L19800 ; SAME ROOM AS DYNAMITE
 MESSAGE 652 ;DWARVES WAKE + KILL YOU
 MESSAGE 653 ;ROOF KILLS YOU
 GOTO @L24000
.L20000 ; ENDGAME-PICTUREED FROM 1000
 IF T2>C0 THEN L20050
 RETURN
.L20050 ADD T2,C1
 IF T2<>6 THEN L20100
 ; ANOUNCE CLOSING
 MESSAGE 632
.L20090 RETURN
.L20100 IF T2<>20 THEN L20700
 ; NOW WE CATAPULT HIM INTO ENDGAME
 ; FIRST ASK HIM
 MESSAGE 667 ;ELF APPEARS
.L20113
;>> INPUT I1 I2 I3 I4
 GOSUB @ASKINPUT ;>>
 IF RESULT=FALSE THEN L20113 ;>>
 GOSUB @L18000 ;GET YES/NO
 IF Y1=C1 THEN L20121 ;YES
 IF Y1=C0 THEN L20119 ;NO
 GOTO L20113
.L20119 MESSAGE 668 ;REFUSED !
 GOTO @L1700 ;STOP GAME
.L20121 MESSAGE 669 ;ACCEPTED
 ; DROP/KILL TREASURE,
 ; OBJECTS INTO SW REPOSITORY
 X1=C1
.L20160 X2=LIST4(X1) ;VALUE
 IF X2=C1 THEN L20200
 IF X1=20 THEN L20300;GAZETTE (WIITS END)
 X2=170 ;REPOSITORY
.L20190 LIST3(X1)=X2 ; always in range
 GOTO L20300
.L20200 ; TREASURE
 X2=LIST3(X1) ; always in range
 IF X2<>C8 THEN L20250
 X2=14 ;TO BUILDING IF CARRIED
 GOTO L20190 ;DROP IT
.L20250 IF X2=14 THEN L20300 ;IN BUILDING
 X2=C0
 GOTO L20190 ;KILL TREASURE IF NOT
 ; IN BUILDING
.L20300 ADD X1,C1
 IF X1<28 THEN L20160
 ; NOW KILL BIRD+CAGE,BEAR,BOTTLE
 LIST3(7)=C9 ;BIRD
 LIST3(5)=C9 ;CAGE
 LIST3(12)=C9 ;BEAR
 LIST3(4)=C9 ;BOTTLE
 ; NOW INTO ENDGAME
 R1=170
 ; NOW TELL HIM WHAT'S GOING ON
 MESSAGE 644
 ; LAMP HAS NEW BATTERIES NOW
 L2=C8
 L1=C1
 GOTO @L1010 ;INPUT CLEAR STACK
.L20700 IF E1=C0 THEN L20090 ;NO WATER YET
 GOSUB L22000 ;CHECK IF HE DROWNS
 IF W4=C0 THEN L20710
 IF W5=C0 THEN L20710;NOT WATERED BEFORE
 MESSAGE 658 ;DROWNS
 GOTO @L24000 ;DIES
.L20710 ; NOW THE WATER STARTS TO RISE
 IF W2=197 THEN L20910 ;STOPPED RISING
 ADD W3,C1
 IF W3<C3 THEN L20750
 ADD W2,C1
 W3=C1
.L20750 ; NOW MESSAGE APPROPRIATE MESSAGES
 IF W2=196 THEN L24500 ;BANG!
 IF W3<>C2 THEN L20850
 X1=R1
 SUB X1,C1
 IF X1<>W2 THEN L20850
 ; ROOM NEXT DOOR IS FLOODED
 MESSAGE 655
 RETURN
.L20850 GOSUB L22000 ;W4=1 IF DROWNING LIKELY
 IF W4=C0 THEN L20900
 MESSAGE 656 ;ROOM FULL OF WATER
 RETURN
.L20900 IF W2<R1 THEN L20910
 MESSAGE 657 ;ROOM HALF FULL
.L20910 RETURN
.L21000 ; HAVE ALL TREASURES-27 BEEN FOUND?
 IF T2<>C0 THEN L21200 ;IN ENDGAME
 ; IF SO, START ENDGAME CLOCK
 X1=C1
.L21030 X2=LIST4(X1) ;VALUE
 IF X2=C0 THEN L21100 ;WORTHLESS
 X2=LIST5(X1) ;HAS IT BEEN FOUND?
 IF X2=C0 THEN L21200 ;NO
.L21100 ADD X1,C1
 IF X1<28 THEN L21030
 ; GOT THEM ALL !!!
 T2=C1
.L21200 RETURN
.L22000 ; W4=1 IF DROWNING LIKELY
 W4=C0
 IF W2<R1 THEN L22100
 IF W3<>C2 THEN L22100
 W4=C1
.L22100 RETURN
.L23000 ; 'LOCK'
 IF E3=C1 THEN @L1250 
 X1=LIST3(C1) ;KEYS
 IF X1=C8 THEN L23050
 MESSAGE 19 ;NOKEYS
 GOTO @L1000
.L23050 IF I2<>91 THEN @L1250;CAN'T
 IF R1<>199 THEN @L1350 ;NOT SEEN
 E3=C1
 MESSAGE 18 ;SHUTS
 GOTO @L1000
.L24000 ; ENDGAME DEATH
 GOTO @L1700
.L24500 ; LAVA BLOWS UP
 W3=C2 ;WATER LEVEL
 IF E3=C1 THEN L24600
 MESSAGE 660 ;DIES IN BLAST
 GOTO L24000 ;DEATH
.L24600 MESSAGE 659 ;SURVIVES BLAST
 GOTO @L1010
.L24700 ; WEB TEARS
 MESSAGE 674 ;FALL TO DEATH
 GOTO L24000
.L26000 ; ENDGAME SPECIAL MOVEMENT
 IF B1=C0 THEN L26100 ;NO BRIDGE
 IF R1<>197 THEN L26020 ;W BANK
 IF I1=15 THEN L26010  ;CROSS
 IF I1<>C3 THEN L26100 ;EAST
.L26010 R2=195
 RETURN
.L26020 IF R1<>195 THEN L26100
 IF I1=7 THEN L26030
 IF I1<>15 THEN L26100 ;CROSS
.L26030 R2=197
 RETURN
.L26100 ; ENDGAME DOOR
 IF R1<>199 THEN L26200
 IF R2<>198 THEN L26200
 IF E3=C1 THEN @L2070 ;NOTPOS
.L26200 ; WEB CAVERN
 RETURN
.L27000 ; GOLD (GO)
 I1=I2
 I2=I3
 I3=45 ;JEWELRY
 GOTO @L690
.L27500 ; SAY
 GOSUB @L31000 ;DIE IN REPOSITORY
 IF I2=32 THEN @L9600 ;PLUGH
 IF I2=33 THEN @L9700 ;XYZZY
 IF I2=34 THEN @L9800 ;PLOVER
 MESSAGE 23 ;NOTHING HAPPENS
 GOTO @L1000
 ; SPIDER CODE HENCEFORTH
.L28100 ; CAN'T GO UP WEB WHILE SPIDER LIVES
 IF R1<>216 THEN L28150 ;NOT WEB
 IF S5=C0 THEN L28150 ;SPIDER GONE
 IF R2<>219 THEN L28150
 IF S5=C8 THEN L24700 ;WEB TEARS
 MESSAGE 684 ;CAN'T GET PAST SPIDER
 GOTO @L1000
.L28150 RETURN
.L28200 ; THROW JADE SPIDER
 IF S5<>C8 THEN @L6040
 IF R1<>209 THEN L28300 ;NOT PINNACLE
 ; SPIDER GOES OVER THE SIDE
 LIST3(28)=C9 ;KILL JADE SPIDER
 S5=C0 ;KILL BIG SPIDER
 MESSAGE 685 ;TELL HIM ABOUT IT
 GOTO @L1000
.L28300 ; SPIDER EATS HIM
 MESSAGE 686 ;HO HO
 GOTO @L24000 ;ENDGAME
.L28400 ; MESSAGE DESCRIPTION FOR SPIDER
 IF S5=C8 THEN L28430
 IF R1=S5 THEN L28460 ;?
 RETURN
.L28430 MESSAGE 675 ;SPIDER HERE WITH EYES
 RETURN
.L28460 X1=LIST3(28) ;SPIDER
 IF X1<>C8 THEN L28465
 S5=C8
.L28465 MESSAGE 671 ;SPIDER HERE
 RETURN
.L28500 ; MESSAGE DESCRIPTIONS FOR CHAINED ELVES
 IF R1<>193 THEN L28600
 IF E5=C1 THEN L28550
 MESSAGE 688
.L28550 RETURN
.L28600 IF R1<>194 THEN L28550
 IF E6=C1 THEN L28550
 MESSAGE 688
 RETURN
.L29000 ; SAVE WORKSPACE
 S6=C0
 SAVE
 GOTO @L1000
.RESTORE ; RESTORE WORKSPACE
 MESSAGE 2328 ; YES or NO please >>MIKE 3/7/86
 GOSUB @ASKINPUT               ;>>MIKE 3/7/86
 IF RESULT=FALSE THEN RESTORE ;>>MIKE 3/7/86
 GOSUB @L18000                ;>>MIKE 3/7/86
 IF Y1=0 THEN @L1000 ; NO      >>MIKE 3/7/86
 IF Y1<>1 THEN RESTORE        ;>>MIKE 3/7/86
 GOSUB @LENSLOK
 IF RESULT=FALSE THEN @L1000
 RESTORE
 GOTO @AFTERRESTORE ;>> @L4000
.L30000 ; RETURN RATING IN S7
 IF S4>60 THEN L30200
 S7=676 ;GET BACK TO FARM
 RETURN
.L30200 IF S4>120 THEN L30300
 S7=677 ;NOVICE
 RETURN
.L30300 IF S4>200 THEN L30400
 S7=678 ;PLAYER
 RETURN
.L30400 IF S4>350 THEN L30500
 S7=679 ;GOOD PLAYER
 RETURN
.L30500 IF S4>750 THEN L30600
 S7=680 ;ADVENTURER
 RETURN
.L30600 IF S4>1099 THEN L30700
 S7=681 ;MASTER ADVENTURER
 RETURN
.L30700 S7=682 ;GRAND MASTER ADVENTURER
 RETURN
.L31000 ; KILL HIM IF IN REPOSITORY
 IF W2>168 THEN L31030
 IF R1=170 THEN L31100
 IF R1=169 THEN L31100
.L31030 RETURN
.L31100 MESSAGE 692 ;NOISE ECHOES
 GOTO @L1700
.L32000 ; HANDLE FEE,FIE PART 1
 IF F7=C0 THEN L32100
 X1=I1
 X2=79
 SUB X1,X2
 IF X1=F7 THEN L32100
 MESSAGE 44 ;GET IT RIGHT DUMMY
 F7=C0 ;STACK FEE FLAG
 I4=C0 ;KILL INPUT
.L32100 RETURN
;-----
.L32500 ; VOICE AT Y2
 IF R1<>55 THEN L32600 ;Y2
 RANDOM X1
 IF X1>20 THEN L32600
 MESSAGE 35 ;VOICE SAYS PLUGH
.L32600 RETURN
;-----
.L33000 ; HANDLE LAMP GOING OUT
 X2=LIST3(C2) ;LAMP ROOM
 IF L2<>252 THEN L33040
 L2=256 ;SKIP 254,255
.L33040 IF L2=C8 THEN L33200
 IF L1<>C1 THEN L33200
 ADD L2,C1 ;LAMP CONTINUES TO DIE
 IF L2<>265 THEN L33100
 IF X2<>C8 THEN L33200;NO LAMP
 MESSAGE 690 ;LAMP GOING DIM
.L33100 IF L2<>300 THEN L33200
 L1=C0 ;LAMP OFF
 L2=254 ;LAMP DEAD
 IF X2<>C8 THEN L33200 ;NO LAMP
 MESSAGE 691 ;LAMP GOES OUT
 GOTO @L659
.L33200
 RETURN
;-----
.ISOBJECTHERE
; return RESULT=TRUE if OBJECT is on floor in current room
 I2=OBJECT ;>>
.IOHI2
 GOSUB @CONVERTI2 ;>>

 GOSUB CHECKFIXEDOBJECT ;>>
 IF RESULT=TRUE THEN L33200 ;RETURN

 IF I2>LIST3MAX THEN @IOMFALSE ;>>** List 3 is only 33 long (?)

 IF I2=65529 THEN @IOMFALSE
 RESULT=LIST3(I2) ;>>

;>> IF OBJECT<MINOBJECT THEN IOMFALSE
;>> IF OBJECT>MAXOBJECT THEN IOMFALSE
;>>; IF OBJECT>153 THEN IOTUNTAKEABLE
;>> PARSEX2=OBJECT
;>> PARSEX1=35
;>> SUB PARSEX2,PARSEX1
;>> RESULT=LIST3(PARSEX2)

 IF RESULT=R1 THEN @IOMTRUE
;>> IF RESULT=C8 THEN IOMTRUE ;> Object carried
 GOTO @IOMFALSE
;
;-----
;
.CHECKFIXEDOBJECT ;>>
; for things in particular rooms, set PARSEX1=ROOM, and jump to CHECKROOM
 IF OBJECT=103 THEN @IOMTRUE ;>>Spider
 PARSEX1=32 ;>>Bridge
 IF OBJECT=102 THEN CHECKBRIDGE ;>>
 PARSEX1=92 ;Plant
 IF OBJECT=70 THEN CHECKROOM
 PARSEX1=93 ;Troll
 IF OBJECT=94 THEN CHECKTROLL
 PARSEX1=52 ;Snake
 IF OBJECT=73 THEN CHECKSNAKE
 PARSEX1=68 ;Dragon
 IF OBJECT=72 THEN CHECKDRAGON
 PARSEX1=103 ;Clam
 IF OBJECT=85 THEN CHECKROOM
 PARSEX1=69 ;Mirror
 IF OBJECT=86 THEN CHECKROOM
 PARSEX1=199 ;Door
 IF OBJECT=91 THEN CHECKROOM
 PARSEX1=71 ;Gate
 IF OBJECT=100 THEN CHECKGATE
 PARSEX1=4 ;Grate
 IF OBJECT=35 THEN CHECKGRATE
 GOTO @IOMFALSE

.CHECKBRIDGE ;>>
 IF B1=C1 THEN @IOMTRUE ;>>Engame bridge
 IF R1=58 THEN @IOMTRUE ;>>
 IF R1=59 THEN @IOMTRUE ;>>
 IF R1=93 THEN @IOMTRUE ;>>
 GOTO CHECKROOM        ;>>

.CHECKSNAKE
 PARSEX2=LIST1(52) ;Driven off ?
 IF PARSEX2=C1 THEN @IOMFALSE
 GOTO CHECKROOM

.CHECKDRAGON
 PARSEX2=LIST1(68) ;Killed ?
 IF PARSEX2=C1 THEN IOMFALSE
 GOTO CHECKROOM

.CHECKTROLL
 PARSEX2=LIST1(93) ;Troll at bridge ?
 IF PARSEX2<>C1 THEN IOMFALSE
 IF R1=32 THEN IOMTRUE
 GOTO CHECKROOM

.CHECKGRATE
 IF R1=17 THEN IOMTRUE
 GOTO CHECKROOM

;.CHECKMIRROR
; IF R1=56 THEN IOMTRUE
; GOTO CHECKROOM

.CHECKGATE
 IF R1=107 THEN IOMTRUE
.CHECKROOM
 IF R1=PARSEX1 THEN IOMTRUE
 GOTO IOMFALSE

;-----
;.IOTUNTAKEABLE
; RESULT=LIST1(OBJECT)
; IF RESULT=R1 THEN IOMTRUE
; GOTO IOMFALSE
;-----
.SILLY ;>>MIKE 27/8/86
 IF I2=69 THEN UNWISE ; dwarf
 IF I2=72 THEN UNWISE ; dragon
 IF I2=73 THEN UNWISE ; snake
 MESSAGE 694 ; don't be absurd
 GOTO @L1000
.UNWISE
 MESSAGE 695 ; that sounds positively lethal
 GOTO @L1000
;---
.CONVERTI2

; IF I2<>69 THEN NOTDWARF ;>> NICK 15/7/86
; IF D4=C0 THEN INVALIDOBJECT ;>> NICK 15/7/86
;.NOTDWARF ;>> NICK 15/7/86

 IF I2>104 THEN INVALIDOBJECT ;>>
 IF I2>101 THEN OBJECTOK ;>>
 IF I2=100 THEN OBJECTOK ;gate
 IF I2=94 THEN OBJECTOK
 IF I2=91 THEN OBJECTOK
 IF I2>86 THEN INVALIDOBJECT
 IF I2>84 THEN OBJECTOK
 IF I2>78 THEN INVALIDOBJECT ;>>**
 IF I2<MINOBJECT THEN INVALIDOBJECT
.OBJECTOK
 X1=35
 SUB I2,X1
 RETURN
.INVALIDOBJECT
 I2=65529
 RETURN
;-----
.ISOBJECTCARRIED
; return RESULT=TRUE if OBJECT is carried by player
 I2=OBJECT
 GOSUB CONVERTI2
 IF I2=65529 THEN IOMFALSE
 RESULT=LIST3(I2)
 IF RESULT=C8 THEN IOMTRUE
; IF RESULT=W7 THEN IOMTRUE
 GOTO IOMFALSE
;-----
.IOMFALSE
 RESULT=FALSE
 RETURN
.IOMTRUE
 RESULT=TRUE
 RETURN
;----- 
.SETUPROOM
 ROOM=R1
 RETURN
;-----
.AUTHORS ;>>
 MESSAGE 2319 ;>>
 GOTO @ENDCOMMAND ;>>
;-----
.HELP
 IF R1=226 THEN HELPFOREST
 IF R1<8 THEN HELPNFOREST
 IF R1>12 THEN HELPNFOREST
.HELPFOREST
 MESSAGE 1821 ; there is SOMETHING in the forest
 GOTO @ENDCOMMAND

.HELPNFOREST
 IF R1<>4 THEN HELPNGRATE
 MESSAGE 1822 ; can open the grate
 GOTO @ENDCOMMAND

.HELPNGRATE
 IF R1<>52 THEN HELPNSNAKE
 MESSAGE 1823 ; snake can be scared off
 GOTO @ENDCOMMAND

.HELPNSNAKE
 IF R1<>58 THEN HELPNBRIDGE
 MESSAGE 1824 ; magic might help
 GOTO @ENDCOMMAND

.HELPNBRIDGE
 IF R1=62 THEN HELPMAZE
 IF R1<>60 THEN HELPNMAZE
.HELPMAZE
 MESSAGE 1825 ; mazes can be mapped
 GOTO @ENDCOMMAND

.HELPNMAZE
 IF R1<>77 THEN HELPNWITT
 MESSAGE 1826 ; go south out of Witt's end
 GOTO @ENDCOMMAND

.HELPNWITT
 MESSAGE 1820 ; on your own
 GOTO @authors ;>>mike 21/9/86 ENDCOMMAND
;---
;-------------------------------------
;-------------------------------------
;-------------------------------------
;-------------------------------------
