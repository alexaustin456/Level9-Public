; Standard Adventure Asource
;
; M.J.Austin 29/7/85
;
; ACORE.TXT (CORE)
;
; THIS IS THE FIRST PART OF THE GAME TO BE COMPILED (After ACONST)
;

BEGIN
 GOTO @STARTGAME
;---
.DESCRIBEROOM
 LASTWORDPRINTED=DISABLEIT ; prevent printing of 'it'
 GOSUB @POSITIONSHADOWS
 GOSUB @CHECKIFLIGHT
 IF RESULT=TRUE THEN DESCROOM1
 MESSAGE 1108 ; It's dark
 LASTPICTURE=0
 CLS G ; clear graphics window

 RETURN

.DESCROOM1

 GOSUB @SPECIALPLAYER
 GOSUB @SETUPROOM
 SEARCHPOS=ROOM
 HISEARCHPOS=0

 X1=500 ; base for pictures
 ADD X1,ROOM
 IF ROOM<64 THEN DISPPIC1
 IF ROOM>75 THEN DISPPIC1 ; >>mike 13/11/86 - fix bug
 X1=564 ; use same picture for ET speed

.DISPPIC1
 if behemothfollowing<>stuck dispnbehemoth
 if room<223 then dispnbehemoth
 if room>225 then dispnbehemoth
 x1=725 ; picture of behemoth in chasm

.dispnbehemoth
 IF X1=LASTPICTURE THEN DESCROOM2
 CLS G
 PICTURE X1
 LASTPICTURE=X1

.DESCROOM2
 X1=ROOMDESCS
 ADD X1,ROOM
 MESSAGE X1
.DESCROOM3
 MESSAGE DOT
 GOSUB PRINTEXITS
 SEARCHPOS=ROOM
 HISEARCHPOS=0
 GOSUB @PRINTOBJECTS

 GOSUB DESCPLAYER
 GOSUB @SPECIALDESC
.DESCROOMRET
 RETURN
;---
.DESCPLAYER
; Print any special messages for player
; e.g. 'You are standing on the object'
 X1=HICURRENTPOS(PLAYER)
 IF X1=LIEON THEN DESCPLAY0
 IF X1=SITON THEN DESCPLAY0
 IF X1<>ON THEN DESCPLAY1
.DESCPLAY0
 MESSAGE 370 ; you
 X2=76
 ADD X1,X2
 MESSAGE X1 ; are standing on / sitting on / lying on etc.
 GOTO DESCPLAY5
.DESCPLAY1
 RETURN
.DESCPLAY5
 X1=CURRENTPOS(PLAYER)
 GOSUB @DESCTHEOBJX1
 MESSAGE DOT
 RETURN
;---
.PRINTEXITS
 OWTYPE=OWEXITS
 NUMEXITS=0
 DIR=1 ; current direction
.PELOOP
; PRINT AN EXIT ( IF VISIBLE)
 FROM=ROOM
 GOSUB @CHECKEXIT
 X1=0
 X2=0
 IF EXITVISIBLE=FALSE THEN PENEXTDIR
 X1=EXITDESCBASE
 ADD X1,DIR
; IF DOOR=FALSE THEN PENODOOR
; X2=12 ; base for door messages
; ADD X2,DOOROPEN
;.PENODOOR
 ADD NUMEXITS,C1
 IF NUMEXITS<>2 THEN PE2
 MESSAGE 10 ; EXITS ARE
.PE2
 GOSUB @OUTPUTWORDX1X2
.PENEXTDIR
 ADD DIR,C1
 IF DIR<14 THEN PELOOP
.PERET1
 IF NUMEXITS>1 THEN PERET2
 IF NUMEXITS=0 THEN PERET
 MESSAGE 11 ; ONLY EXIT IS
.PERET2
 X1=0
 X2=0
 GOSUB @OUTPUTWORDX1X2
.PERET
 RETURN
;---
.CHECKIFOBVIOUS
; see if 'OBJECT' is immediately obvious
 OBVIOUS=TRUE
 X1=HICURRENTPOS(OBJECT)
 IF X1=0 THEN CIORET ; things on ground are always obvious
 X1=CURRENTPOS(OBJECT)
 IF X1=PLAYER THEN CIORET ; player's contents are obvious
 ADD X1,X1
 ADD X1,C1 ; look at hi initial position of container
 X2=OBJECTSTART(X1)
 IF X2>127 THEN CIORET ; 'obvious' bit set
 IF SEARCHDEPTH=0 THEN CIORET
 OBVIOUS=FALSE
;
.CIORET
 RETURN
;---
.PRINTOBJECTS
; GO THROUGH CURRENT OBJECT POSITION LIST,
; AND DISPLAY ALL THE OBJECTS AT (HISEARCHPOS,SEARCHPOS)
 OWTYPE=OWOBJECTS
 GOSUB @INITGETOBJ
 LASTWORDPRINTED=0
 TOTALOBJECTFOUND=0
.PO1
 GOSUB GETNEXTOBJECT
 IF OBJECT=0 THEN PO1A
 GOSUB CHECKIFOBVIOUS
 IF OBVIOUS=FALSE THEN PO3
.PO1A
 IF NUMOBJECTFOUND<>1 THEN PO2
 ADD TOTALOBJECTFOUND,C1
 X1=0
 X2=0
 GOSUB @OUTPUTWORDX1X2 ; output any buffered words
 GOSUB @PRINTCONTAINMENT ; PRINT CONTAINER ON FIRST OBJECT FOUND ONLY
.PO2
 X1=OBJECT
 X2=0
 GOSUB @OUTPUTWORDX1X2 ; Output object name (delayed effect)
.PO3
 IF OBJECT<>0 THEN PO1
 LASTWORDPRINTED=DISABLEIT
 RETURN
;---
.GETNEXTOBJECT
; RETURN OBJECT=OBJECT FOUND
;    SEARCHPOS=ROOM OR CONTAINER WHERE FOUND
;  HISEARCHPOS=TYPE OF CONTAINMENT (=0 FOR ROOMS)
;
; BEFORE FIRST CALL, MUST SP=0, GOSUB INITGETOBJ AND SETUP HI,SEARCHPOS
; AT END OF SEARCH, ALL ZEROS ARE RETURNED
; RETURN NUMOBJECTFOUND=NUMBER OF OBJECT FOUND IN THIS PASS
;
; ALSO ADD ON WEIGHT OF THIS OBJECT TO 'WEIGHT'
 X1=138 ; MAXOBJECT ??
.GETNEXTOBJX1
 GETNEXT X1 HISEARCHPOS SEARCHPOS OBJECT NUMOBJECTFOUND SEARCHDEPTH
 IF OBJECT>X1 THEN GNOERR
.GNORET
 RETURN
.GNOERR
 OBJECT=0
 NUMOBJECTFOUND=0 ; ??
 RETURN
;---      
 
.INITGETOBJ
 SEARCHDEPTH=1
 X1=0
 GETNEXT X1 X1 X1 X1 X1 X1
 RETURN
;--- 
.PRINTCONTAINMENT
 X3=24 ; BASE FOR CONTAINMENT MESSAGES
 IF HISEARCHPOS=NONSPECIFIC THEN PCRET
 IF SEARCHPOS<>PLAYER THEN PC1
 X3=60 ; BASE for 'You are wearing' and 'You are carrying'
.PC1
 ADD X3,HISEARCHPOS
 MESSAGE X3
 IF HISEARCHPOS=0 THEN PCRET ; DON'T PRINT 'THE object' FOR 'YOU CAN SEE'
 X1=SEARCHPOS ; CONTAINER OBJECT NUMBER
 GOSUB @DESCTHEOBJX1
 X1=16
 ADD X3,X1 ; print second half of containment messages
 MESSAGE X3
.PCRET
 RETURN
;---
.OUTPUTWORDX1X2
; WE ARE OUTPUTING A LIST OF WORDS, SEPARATED BY COMMAS
; ETC. THIS ROUTINE TAKES A SERIES OF WORDS ONE AT A TIME
; AND OUTPUTS THEM. BEFORE CALLING, SET OUTPUTWORD=0
; AT THE END OF THE LIST, CALL THIS ROUTINE WITH X1=0 AND THE LIST
; WILL BE FINISHED OFF WITH A FULL STOP ETC.
; Call with X1=1 to finish off with a comma
 IF X1=0 THEN OWEND
; IF X1=1 THEN OWEND
 IF OUTPUTWORD=4 THEN OW1 ; SECOND WORD
; FIRST OR SECOND WORD - JUST ADD TO BUFFER
 OUTPUTBUFFER(OUTPUTWORD)=X1
 ADD OUTPUTWORD,C1
 OUTPUTBUFFER(OUTPUTWORD)=X2
 ADD OUTPUTWORD,C1
.OWRET
 RETURN
;
.OW1
; THIRD TO SUBSEQUENT WORDS - HANDLE BY A ROLLING FIFO BUFFER
 OUTPUTBUFFER(4)=X1
 OUTPUTBUFFER(5)=X2
 X1=OUTPUTBUFFER(0)
 X2=OUTPUTBUFFER(1)
 GOSUB OWPRINTX1X2
 X1=0
 X2=2
.OW2
 X3=OUTPUTBUFFER(X2)
 OUTPUTBUFFER(X1)=X3
 ADD X1,C1
 ADD X2,C1
 IF X1<6 THEN OW2
 MESSAGE COMMA
 RETURN
.OWEND
; X4=X1 ; save termination type
; HAVE RECEIVED THE TERMINATOR
 IF OUTPUTWORD=0 THEN OWRET ; NO WORDS OUTPUT AT ALL
; THERE MUST BE AT LEAST ONE WORD REMAINING IN BUFFER - SO OUTPUT IT
 X1=OUTPUTBUFFER(0)
 X2=OUTPUTBUFFER(1)
 GOSUB OWPRINTX1X2
 IF OUTPUTWORD=2 THEN OWSTOP ; ONLY ONE WORD OUTPUT - SO THATS IT !
; MUST HAVE OUTPUTWORD=2, SO 2 OR MORE WORDS OUTPUT.
; THEREFORE WANT TO FINISH OFF WITH AN AND
 MESSAGE AND
 X1=OUTPUTBUFFER(2)
 X2=OUTPUTBUFFER(3)
 GOSUB OWPRINTX1X2
.OWSTOP
 OUTPUTWORD=0
 MESSAGE DOT
 RETURN
 ;---
.OWPRINTX1X2
 IF OWTYPE=OWOBJECTS THEN @DESCANOBJX1
; Print normal words by message number
 MESSAGE X1
 MESSAGE X2
 RETURN
;
.DESCTHEOBJX1
; PRINT OUT 'THE <desc>'
; First check if can print 'it'
 IF X1<>LASTWORDPRINTED THEN DTOX2
 MESSAGE 382 ; 'it'
 RETURN
;
.DTOX2
 IF X1=PLAYER THEN DTOXNOTHE ; PREVENT 'the you own a ...'
 MESSAGE THE
.DTOXNOTHE
 GOTO DESCOBJX1A
;---
.DOSOME
 MESSAGE 23 ; SOME
 GOTO DESCOBJX1A
.DOAN
 MESSAGE 22 ; AN
;---
.DESCOBJX1A
 IF LASTWORDPRINTED=DISABLEIT THEN DOX1A2
 LASTWORDPRINTED=X1
.DOX1A2
 IF X1>MAXOBJECT THEN DESCOBJRET
 X2=OBJECTDESCBASE
 ADD X2,X1
 MESSAGE X2
.DESCOBJRET
 RETURN
;---
; .GETHIOBJECTPOS
; RETURN HIPOS=CONTAINMENT TYPE
; ALSO RETURN INCONTAINER=FALSE OR NONSPECIFIC ACCORDINGLY
;  HIPOS=HICURRENTPOS(OBJECT)
; X1=64
; ADD HIPOS,X1
;.GHOP1
; SUB HIPOS,X1
; IF HIPOS>63 THEN GHOP1
; INCONTAINER=FALSE
; IF HIPOS>0 THEN GHOPRET
; INCONTAINER=NONSPECIFIC
;.GHOPRET
; RETURN
;---
.CHECKEXIT
 GOSUB ABSCHECKEXIT
 GOTO @SPECIALEXITS ; Exits conditional on game
;---
.ABSCHECKEXIT
 ; EXIT ( From FROM diection DIR )
 ; return DEST, DOOR, EXITVISIBLE
; DOOR=0
 EXITVISIBLE=FALSE
 EXIT FROM DIR STATUS DEST
 IF DEST=0 THEN CERET
.CHECKEXITSTATUS
 X1=STATUS
 IF X1<4 THEN CE2
; DOOR=1
 SUB X1,C4
.CE2
 IF X1>1 THEN CERET
 EXITVISIBLE=TRUE
.CERET
 RETURN
;---
.CALCDIRECTION
; given 'FROM', 'TO' as adjacent locations,
; return 'DIR'=direction going from 'FROM' to 'TO'
 DIR=15
.CALCDIR1
 SUB DIR,C1
 GOSUB ABSCHECKEXIT
 IF DIR=0 THEN CALCDIRRET
 IF DEST<>TO THEN CALCDIR1
.CALCDIRRET
 RETURN
;---
.CRVARYMESSAGE
 MESSAGE CR
.VARYMESSAGE
; OUTPUT ONE OF THREE MESSAGES AT 'M1'
; GOSUB RAND3
; ADD M1,NUMBER
; MESSAGE M1
; RETURN
 ADD CURRENTMESSAGE,C1
 IF CURRENTMESSAGE<3 THEN VMOK
 CURRENTMESSAGE=0
.VMOK
 X1=M1
 ADD X1,CURRENTMESSAGE
 MESSAGE X1
 RETURN
;---
;----------------------------------------------------------
;----------------------------------------------------------
;-----------------------
