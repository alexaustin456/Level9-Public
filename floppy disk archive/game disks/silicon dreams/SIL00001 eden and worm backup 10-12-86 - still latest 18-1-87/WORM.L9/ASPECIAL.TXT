; Worm in Paradise special code handlers
;
; ASPECIAL.TXT
;
; M.J.Austin 18/10/85
;
; Some revisions and bug fixes done for inclusion in the
; Silicon Dreams by MJA, 15/10/86
;
CONST
 IRAM=36
 IOOPS=37

VAR
 notfirstdream ;>>mike 15/10/86 - also all references to this var
 currentadvert ;>>mike 27/10/86 - also all references to this var
BEGIN

.NEWLOCATION
; Given HI,CURRENTPOS(PLAYER)=old location, HI,DEST=new location
; print any messages which appear on moving through doors etc.
 GOSUB @SETUPROOM
 FROM=ROOM
 GOSUB @SPECIALMOVES
 IF RESULT=FALSE THEN @NLRET
;
.ABSNEWLOCATION
; as for newlocation, but don't do any checking for special
; case code. Use this routine where the move is forced, so
; don't want to trip any movement traps
;>> GOSUB @SAVEOOPS ; >>MIKE 18/8/86
 FROM=ROOM
 CURRENTPOS(PLAYER)=DEST
 HICURRENTPOS(PLAYER)=HIDEST
 TIMEINROOM=0
; DOOROPEN=FALSE
.NLEND
 DEST=ROOM ; not really DEST
 GOSUB @SETUPROOM
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN NLDESC
; on something, so just describe position
; player gets full description on LOOK
 GOSUB @DESCPLAYER
 GOTO @AFTERMOVE
;
.NLDESC
 GOSUB @DESCRIBEROOM
; now print any messages to be printed after entering certain moves
.AFTERMOVE
 IF ROOM<>110 THEN AMNOTCASINO
 MESSAGE 1566 ; BANDIT GREETING

.AMNOTCASINO
 IF ROOM<>102 THEN AMNOTBODYBANK
 IF HAVELOSTORGANS=FALSE THEN AMSELLORGANS
 IF CREDITS<NEGATIVE THEN AMNOTBANKRUPT
 MESSAGE 2054 ; consultant sees have no organs + no cash, so had chips.
 GOTO @DEATH

.AMNOTBANKRUPT
 MESSAGE 1911 ; want to buy your organs back
 GOTO AMCONSULTQUESTION

.AMSELLORGANS
 IF CONTROLLINGWALDROID=TRUE THEN @GETLOSTWALDROID
 MESSAGE 1901 ; consultant greeting
.AMCONSULTQUESTION
 GOSUB @YESORNO
 IF RESULT=TRUE THEN @VOLSELLORGANS
 MESSAGE 1909 ; consultant dissapointed
 RETURN

.AMNOTBODYBANK
 IF ROOM<>173 THEN AMNOTCAVE
 IF HAVETRIGGEREDALARM<2 THEN @TRIGGERALARM

.AMNOTCAVE
 IF ROOM<>174 THEN AMNOTSAUCER
 MESSAGE 1945 ; technician is astonished to see you
 OBJECT=DAGGET
 POS=ROOM 
 HIPOS=0
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN AMSAUCERDIE
 MESSAGE 1947 ; dagget bites technician by leg - offers to hold them off
 HAVEBEENSAUCER=TRUE
 SCOREINDEX=SCSAUCER
 GOSUB @ADDSCORE
 POS=ROOM
 HIPOS=0
 CONFIRMMESSAGE=0
 GOTO @MOVEOBJECT ; put dagget on ground in saucer
.AMSAUCERDIE
 MESSAGE 1946 ; technician shoots you
 GOTO @DEATH

.AMNOTSAUCER
 IF ROOM<>100 THEN AMNOTPOLICESTATION
 IF HAVEBEENSAUCER<>0 THEN @KNOWTOOMUCH ; fuzbot kills player
 GOSUB @ABSCALLTHECOPS
 IF M1<>0 THEN AMNOTPOLICESTATION
 MESSAGE 1870 ; fuzbot greeting

.AMNOTPOLICESTATION
 IF ROOM=103 THEN @JOBCENTRE
 IF ROOM<>135 THEN AMNOTMD
 MESSAGE 1993 ; do MD job
 X4=500 ; MD pay
.ENDJOB
 SCOREINDEX=SCJOB
 GOSUB @ADDSCORE
 TIMEPASSED=400
 ADD CREDITS,X4
 DAYSNOWORK=0
 DEST=58
 HIDEST=0
 GOSUB @CANDAGGETFOLLOW
 IF RESULT=FALSE THEN ENDJOB1
 CURRENTPOS(DAGGET)=DEST
 HICURRENTPOS(DAGGET)=HIDEST

.ENDJOB1
 GOTO @NEWLOCATION ; get thrown out after job

.AMNOTMD
 IF ROOM<>144 THEN AMNOTCLERK
 MESSAGE 1992 ; do clerk job
 X4=10 ; pay for clerk's job
 GOTO ENDJOB

.AMNOTCLERK
 IF ROOM<>145 THEN AMNOTWALDROID
 MESSAGE 1994 ; enter coccoon
 TIMEPASSED=300 ; working as waldroid
 GOSUB @TICKCLOCK
 GOSUB @SAVEPOSSESIONS
; now own nothing
 CURRENTPOS(WALDROID)=C0
 X1=146
 CURRENTPOS(PLAYER)=X1 ; can't use NEWLOCATION due to looping
 CONTROLLINGWALDROID=TRUE
 GOTO AMPOSTWALDROID

.AMNOTWALDROID
 IF ROOM<>146 THEN AMNOTWORKSHOP
 IF CONTROLLINGWALDROID=FALSE THEN AMNOTWORKSHOP
 MESSAGE 1996 ; return waldroid
 CURRENTPOS(WALDROID)=ROOM
 POS=PLAYER
 DEST=WALDROID ; put all possessions into waldroid
 HIDEST=NONSPECIFIC
 GOSUB @POSSLOOP
 X1=CURRENTPOS(VALVE)
 GOSUB @RESTOREPOSSESIONS ; give objects back to player
 X1=145
 CURRENTPOS(PLAYER)=X1 ; can't use NEWLOCATION due to looping
 CONTROLLINGWALDROID=FALSE

.AMPOSTWALDROID
 GOSUB @DESCRIBEROOM

.AMNOTWORKSHOP
 IF ROOM<>169 THEN AMNOTELEVATOR
 RANDOM X1
 IF X1<40 THEN AMRANDELEV1
 IF ELEVATOROFFSET<>8 THEN AMNOTELEVATOR
.AMRANDELEV1
 MESSAGE 2021 ; elevator starts of its own accord
.AMRE1
 GOSUB @RAND10
 IF NUMBER=9 THEN AMRE1
 IF NUMBER=ELEVATOROFFSET THEN AMRE1
 GOTO @MOVEELEVATOR

.AMNOTELEVATOR
 IF ROOM<>165 THEN AMNOTMAPROOM
 IF OBJECTTHROWN<>0 THEN AMJEMMYPARTITION
 MESSAGE 2026 ; the partition slides down
 CURRENTPOS(PARTITION)=ROOM
 GOTO AMNOTMAPROOM
.AMJEMMYPARTITION
 MESSAGE 2028 ; partition slides down onto the_
 X1=OBJECTTHROWN
 GOSUB @DESCTHEOBJX1
 CURRENTPOS(OBJECTTHROWN)=C0
 X1=OBJECTTHROWN
 OBJECTTHROWN=0
 IF X1<>BOX THEN AMCRUSHOBJECT
 MESSAGE 2030 ; stops, whirrs and goes back up
 SCOREINDEX=SCBOXCRUSHED
 GOSUB @ADDSCORE
 GOTO AMNOTMAPROOM
.AMCRUSHOBJECT
 MESSAGE 2029 ; and crushes it
 CURRENTPOS(PARTITION)=ROOM

.AMNOTMAPROOM
 IF ROOM<>166 THEN AMNOTDAIS
 MESSAGE 2031 ; foam starts to come from orifice
 CURRENTPOS(PARTITION)=ROOM
 SCOREINDEX=SCSEAT
 GOSUB @ADDSCORE

.AMNOTDAIS
 IF ROOM<>105 THEN AMNOTRECEPTION
; arriving at pyramid reception desk
 IF DUETOBEINTERVIEWED=FALSE THEN AMNOTRECEPTION
 DUETOBEINTERVIEWED=FALSE
 MESSAGE 1880 ; interviewed on tv
 PARTYMEMBER=TRUE
 X4=1000 ; reward for preventing alien invasion
 ADD CREDITS,X4
 SCOREINDEX=SCINTERVIEWED
 GOSUB @ADDSCORE
 CURRENTPOS(TICKET)=C0
 HICURRENTPOS(TICKET)=C0
 GOSUB @SAYHOME ; take player home

.AMNOTRECEPTION
 GOSUB @SETUPROOM
.NLRET
 RETURN
;---
.SPECIALMOVES
;
; Given ROOM, HIDEST, DEST, player is to move that way
; and exit has been validated as possible (and hence described)
; in SPECIALEXITS. The purpose of code here is when something
; happens on moving. If exit is to be blocked, code here should
; print the appropriate message and set RESULT=FALSE.
;
 RESULT=TRUE
 IF ROOM<>234 THEN SMNOTWALL
 IF DEST<>215 THEN SMNOTWALL
 MESSAGE 1520 ; scramble over wall

.SMNOTWALL
 IF DEST<>121 THEN SMNOTWORM
 IF ROOM<>234 THEN SMNOTWORM
; moving towards worm
 MESSAGE 1516 ; worm spits
 X1=CURRENTPOS(SHIELD)
 IF X1=PLAYER THEN SMWORMOK
 MESSAGE 1518 ; die
 GOTO @DEATH
.SMWORMOK
 MESSAGE 1519 ; acid bounces off shield 
 SCOREINDEX=SCDREAM
 GOSUB @ADDSCORE
 GOTO @PARADISEWAKEUP

.SMNOTWORM
 IF DEST<>BEHEMOTH THEN SMNOTRAVINE
 IF HIDEST=0 THEN SMNOTRAVINE
 X1=CURRENTPOS(SHIELD)
 IF X1<>BEHEMOTH THEN SMNOTRAVINE
 MESSAGE 1515 ; step on loose scale

.SMNOTRAVINE
 X1=HICURRENTPOS(VISOR)
 IF X1=0 THEN SMNOTVISOR
 IF ROOM>209 THEN SMNOTVISOR
 MESSAGE 1542 ; can't leave with visor
 GOTO @SMFAIL

.SMNOTVISOR
 IF ROOM<>111 THEN SMNOTCUSTODIAN
 IF DEST=87 THEN SMNOTCUSTODIAN
 IF BISON=TRUE THEN SMCUSTBISON
 X1=CURRENTPOS(INVITATION)
 IF X1=PLAYER THEN SMCUSTBISON
 MESSAGE 1650
 GOTO @SMFAIL
.SMCUSTBISON
; player is invited or is a bison, so let him enter
; (unless meeting in progress)
 IF HOUR<>6 THEN SMCUSTOK
 IF MINUTE<6 THEN SMCUSTOK
 IF MINUTE>15 THEN SMCUSTOK
 MESSAGE 1652 ; custodian says 'meeting in progress'
 GOTO @SMFAIL
.SMCUSTOK
 MESSAGE 1651 ; custodian lets you past

.SMNOTCUSTODIAN
 IF DEST<>84 THEN SMNOTTURNSTILE
 IF ROOM<>83 THEN SMNOTTURNSTILE
 IF CONTROLLINGWALDROID=TRUE THEN @GETLOSTWALDROID
 MESSAGE 1552 ; about to enter pleasure dome, cost is 9 creds
 X4=9
 GOSUB @DEBITX4
 IF RESULT=FALSE THEN @SMFAIL

.SMNOTTURNSTILE
 IF DEST<>63 THEN SMNOTROUND
 IF FROM<51 THEN SMNOTROUND
 IF FROM>53 THEN SMNOTROUND
; moving from to ET
; if from roundabout, set up colour code to be 000 000
 X1=0
.SMROUND1
 COLOURTABLE(X1)=C0
 ADD X1,C1
 IF X1<6 THEN SMROUND1
 X2=FROM
 X3=51
 SUB X2,X3
 COLOURTABLE(X1)=X2 ; X1=6 i.e. ET system

.SMNOTROUND
 IF DEST<63 THEN SMNOTET
 IF DEST>75 THEN SMNOTET
 IF FROM<63 THEN SMNOTET ; has player just entered ET?
 IF FROM>75 THEN SMNOTET
 IF DIR<2 THEN SMNOTET ; no move ( e.g. EXIT or NORTH )
 IF DIR=4 THEN SMNOTET
 X1=DEST
 X2=63
 SUB X1,X2
; now multiply by 6 to give INDEX into MATHSTABLE for movement
 INDEX=X1
 ADD INDEX,INDEX
 X1=INDEX
 ADD INDEX,INDEX
 ADD INDEX,X1
 MESSAGE 1690 ; picked up by force field
 IF DIR<6 THEN SMETEAST
.SMETWEST
 GOSUB @LONGSUB
 GOTO SMNOTET
.SMETEAST
 GOSUB @LONGADD

.SMNOTET
 IF FROM<>75 THEN SMNOTSPECIALET
 IF DEST<>63 THEN SMNOTSPECIALET
 MESSAGE 1690 ; picked up by force field

.SMNOTSPECIALET
 IF DEST<>53 THEN SMNOTLEAVEET
 IF FROM<>63 THEN SMNOTLEAVEET
; moving off ET to a location which will be determined
; by the colour code
 GOSUB @FINDROOM
 IF VALUE<>0 THEN SMLEAVEET
 MESSAGE 1691 ; door is locked
 GOTO @SMFAIL
.SMLEAVEET
 DEST=VALUE

.SMNOTLEAVEET
 IF FROM<>63 THEN SMNOTHABI
 POS=PLAYER
 HIPOS=NONSPECIFIC
 OBJECT=BROOCH
 IF DEST<>196 THEN SMNOTTRAITOR
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN @SMDENYACCESS
 X1=198 ; traitor bodymaint
 X2=199 ; traitor habihome
 X3=196 ; traitor habihall
 IF DEST=196 THEN SMHABI
.SMNOTTRAITOR
 OBJECT=BADGE
 GOSUB @CHECKOBJECTPOS
 IF DEST<>190 THEN SMNOTHABI
 IF RESULT=FALSE THEN @SMDENYACCESS
 X1=192 ; own bodymaint
 X2=193 ; own habihome
 X3=190 ; own habihall
 HAVEVISITEDHOME=TRUE
 SCOREINDEX=SCHABIHOME
 GOSUB @ADDSCORE
.SMHABI
 CURRENTPOS(NOZZLE)=X1
 CURRENTPOS(CHAIR)=X2
 CURRENTPOS(POSTER)=X2
 CURRENTPOS(CRACK)=X2 ; fold away bed
 CURRENTPOS(BED)=C0
 CURRENTPOS(WALLPAPER)=X3
 CURRENTPOS(PLAQUE)=X2 ; move information plaque

.SMNOTHABI
 IF DIR<>9 THEN SMNOTUNDERCITY ; moving up
 IF FROM<4 THEN SMUNDERCITY
 IF FROM>184 THEN SMNOTUNDERCITY
 IF FROM<175 THEN SMNOTUNDERCITY
.SMUNDERCITY
 X1=HICURRENTPOS(PLAYER)
 IF X1=ON THEN SMUCUP
 MESSAGE 1715 ; can't reach manhole from here
 GOTO @SMFAIL
.SMUCUP
 IF FROM<>184 THEN SMUCOK
 IF COVERLUBRICATED=TRUE THEN SMUCOK
 MESSAGE 1717 ; manhole cover seems to be stuck
 GOTO @SMFAIL

.SMUCOK
 MESSAGE 1714 ; you climb through manhole cover
 IF DEST<>171 THEN SMNOTUNDERCITY
; arrive in quad
 IF PARTYMEMBER=TRUE THEN SMNOTUNDERCITY
 MESSAGE 2062 ;droid sees you
 DEST=79 ; hall square
 HIDEST=0
 GOSUB @ABSNEWLOCATION
 RESULT=FALSE ; prevent any further movement
 RETURN

.SMNOTUNDERCITY
 IF FROM<>102 THEN SMNOTLEAVEBODYBANK
 IF DEST<>101 THEN SMNOTLEAVEBODYBANK
 IF CREDITS>NEGATIVE THEN SMLBODYBANKDEBT
 IF TOTRADEORGANS=FALSE THEN SMNOTLEAVEBODYBANK
; is allowed to leave
 MESSAGE 1903 ; do come back if change mind
 GOTO @SMOK
.SMLBODYBANKDEBT
 MESSAGE 1905 ; cannot leave whilst in debt
 GOTO @CANCELINPUT

.SMNOTLEAVEBODYBANK
 IF DEST<>114 THEN SMNOTCUTTINGROOM
 IF TOTRADEORGANS=TRUE THEN @CUTUPPLAYER
 GOTO @AMSELLORGANS

.SMNOTCUTTINGROOM
 IF DEST<>104 THEN SMNOTJOB
 IF CONTROLLINGWALDROID=TRUE THEN SMLABOURER
 IF HOUR<5 THEN SMJOB
 MESSAGE 1960 ; too late to start work
 OBJECT=JOBCENTRECARD
 GOSUB @KILLOBJECT
 GOTO @SMFAIL

.SMJOB
 X1=CURRENTPOS(JOBCENTRECARD)
 IF X1<>PLAYER THEN @SMNOJOB
 IF JOB=0 THEN @SMNOJOB
 CURRENTPOS(JOBCENTRECARD)=C0
 HICURRENTPOS(JOBCENTRECARD)=C0
 MESSAGE 1964 ; droid takes card
 IF JOB=MD THEN SMNOTJOB ; default destination of office
 IF JOB<>CLERK THEN SMNOTCLERK
 DEST=107

.SMNOTCLERK
 IF JOB<>LABOURER THEN SMNOTLABOURER
.SMLABOURER
 X4=10 ; pay for labourer
 ADD CREDITS,X4
 DEST=146

.SMNOTLABOURER
.SMNOTJOB
 IF DEST=104 THEN SMNOTCARPET
 X1=CURRENTPOS(CARPET)
 IF X1<>ROOM THEN SMNOTCARPET
.SMCARPET
 MESSAGE 1990 ; carpet snuggles up to you
 X1=CURRENTPOS(DAGGET)
 IF X1=ROOM THEN SMEATCARPET
 GOSUB @RAND3
 X4=NUMBER
 GOSUB @RAND3
 ADD X4,NUMBER ; X4=0..4
 DEST=136
 ADD DEST,X4 ; gives random location in office
 RANDOM X4
 IF X4>30 THEN SMCARPETEND
 DEST=104 ; give small chance of escape from carpet's clutches
 GOTO SMNOTCARPET
.SMEATCARPET
 MESSAGE 1991 ; dagget fights off carpet
 SCOREINDEX=SCMANAGER
 GOSUB @ADDSCORE
.SMCARPETEND
 MESSAGE DOT

.SMNOTCARPET
 IF FROM=23 THEN SMPOSRIVERBOAT
 IF FROM<>21 THEN SMNOTRIVERBOAT
.SMPOSRIVERBOAT
 IF DEST=23 THEN SMRIVERBOAT
 IF DEST<>21 THEN SMNOTRIVERBOAT
.SMRIVERBOAT
 X1=CURRENTPOS(TICKET)
 IF X1=PLAYER THEN SMRIVEROK
 MESSAGE 2010 ; can't cross without ticket
 GOTO @SMFAIL
.SMRIVEROK
 MESSAGE 2011 ; crossing river message

.SMNOTRIVERBOAT
 IF FROM<>169 THEN SMNOTELEVATOR
 DEST=106 ; reception desk
 IF ELEVATOROFFSET<>0 THEN SMNOTELEVATOR
 DEST=167
 IF ELEVATORBASE=0 THEN SMNOTELEVATOR
 DEST=168

.SMNOTELEVATOR
 IF FROM<>105 THEN SMNOTFOYER
 IF DEST=167 THEN SMFOYER
 IF DEST<>168 THEN SMNOTFOYER
.SMFOYER
 IF PARTYMEMBER=TRUE THEN SMFOYEROK
 MESSAGE 2014 ; receptionist stops you
 GOTO @SMFAIL

.SMFOYEROK
 MESSAGE 2015;waved past

.SMNOTFOYER
 IF DEST<>150 THEN SMNOTRECEPTION
 MESSAGE 2015;waved past
 IF ELEVATOROFFSET<>9 THEN SMNOTRECEPTION
 IF ELEVATORBASE<>10 THEN SMNOTRECEPTION
 SCOREINDEX=SCPYRAMID
 GOSUB @ADDSCORE
 DEST=160 ;to short corr.

.SMNOTRECEPTION
 IF DEST<>169 THEN SMNOTENTELEV
 X1=0
 IF FROM=167 THEN SMENTELEV
 X1=10
 IF FROM<>168 THEN SMNOTENTELEV
.SMENTELEV
 ELEVATORBASE=X1

.SMNOTENTELEV
 IF DEST<>164 THEN SMNOTMAPROOM
 IF FROM<>165 THEN SMNOTMAPROOM
 MESSAGE 2027 ; partition slides up
 CURRENTPOS(PARTITION)=C0

.SMNOTMAPROOM
 X1=HICURRENTPOS(PLAYER)
 IF X1<>0 THEN SMNOTDAIS
 X1=CURRENTPOS(PARTITION)
 IF X1<>FROM THEN SMNOTDAIS
 MESSAGE 2036 ; partition in way
 GOTO SMFAIL

.SMNOTDAIS
;
; the code controlling the dagget movement
; which follows must be the last code in SPECIALMOVES
 IF HIDEST<>0 THEN SMNOTDAGGET
 GOSUB CANDAGGETFOLLOW
 IF RESULT=TRUE THEN SMDAGGET
 GOTO SMNOTDAGGET

.CANDAGGETFOLLOW
 POS=ROOM
 HIPOS=0
 OBJECT=DAGGET
 GOSUB @CHECKOBJECTPOS
; X1=CURRENTPOS(DAGGET)
 IF RESULT=FALSE THEN CDFFAIL
 X1=HICURRENTPOS(DAGGET)
 IF X1=CARRIED THEN CDFFAIL
 IF X1=FORSALE THEN CDFFAIL
 RESULT=TRUE
 RETURN
.CDFFAIL
 RESULT=FALSE
 RETURN

.SMDAGGET
 IF DAGGETNOPOWER=TRUE THEN SMDAGGETFAREWELL
 IF DEST=FROM THEN SMDAGGOK ; e.g. getting off chair
 IF DIR>8 THEN SMDAGGETFAREWELL
 IF FROM=174 THEN SMDAGGETFAREWELL ; dagget never leaves saucer

.SMDAGGOK
 CURRENTPOS(DAGGET)=DEST
 HICURRENTPOS(DAGGET)=C0
 GOTO SMNOTDAGGET

.SMDAGGETFAREWELL
 M1=1826 ; goodbye master
 GOSUB @DAGGETMESSAGE

.SMNOTDAGGET
;
; do not insert more SMNO... code here
;
.SMOK
 RESULT=TRUE
 RETURN
;
.GETLOSTWALDROID
 MESSAGE 1997 ; waldroids not welcome here
 RESULT=FALSE
 RETURN
;
.SMNOJOB
 MESSAGE 1961 ; not registered for a job
 GOTO SMFAIL
;
.SMDENYACCESS
 MESSAGE 1694 ; access to habihome denied
.SMFAIL
 RESULT=FALSE
 RETURN
;---
.VOLSELLORGANS
 IF CONTROLLINGWALDROID=TRUE THEN GETLOSTWALDROID
 MESSAGE 1902 ; consultant is pleased
.SELLORGANS
; player has agreed for organs to be sold, so go ahead
 MESSAGE 1904 ; would you care to come this way
 TOTRADEORGANS=TRUE
 GOTO @CANCELINPUT
;---
.CUTUPPLAYER
; player is actually to be cut up
 TOTRADEORGANS=FALSE
;>>mike 19/10/86 MESSAGE 1906 ; enter cutting room

 IF HAVELOSTORGANS=FALSE THEN CUPSELL
; trying to buy back organs
 IF CREDITS>NEGATIVE THEN CUPDIE
 X4=900
 SUB CREDITS,X4
 HAVELOSTORGANS=FALSE
 GOTO CUP2

.CUPSELL
 HAVELOSTORGANS=TRUE
 X4=800
 ADD CREDITS,X4
 IF CREDITS>NEGATIVE THEN CUPDIE
 message 1906 ;>>mike 19/10/86 - cut up, etc.

.CUP2
 SCOREINDEX=SCORGANS
 GOSUB @ADDSCORE
 MESSAGE 1908 ; droid escorts you outside
.CUPEXIT
 DEST=101 ; hospital foyer
 HIDEST=0
 GOSUB @INCDAY
 HOUR=4
 MINUTE=0
 GOSUB @ABSNEWLOCATION
 GOTO @CANCELINPUT
;
.CUPDIE
 MESSAGE 1907 ; not enough money to cover debts
 GOTO @DEATH
;---
.EAT
 IF OBJECT<>APPLE THEN EATNOTAPPLE
 SCOREINDEX=SCAPPLE
 GOSUB @ADDSCORE
 MESSAGE 1500 ; apple is tasty , worm appears
 GOSUB @KILLOBJECT
 CURRENTPOS(APPLECORE)=ROOM ; HICURRENTPOS(APPLECORE)=0
 TRAMPTR=WORMTRAMBASE
 POS=ROOM
 GOSUB @POSITIONMOBILE
 WORMTRAMPTR=TRAMPTR
 CURRENTPOS(WORM)=ROOM ; HICURRENTPOS(WORM)=0
 RETURN
;
.EATNOTAPPLE
 IF OBJECT<>PIZZA THEN EATNOTPIZZA
 MESSAGE 1935 ; pizza is delicious
 GOTO @KILLOBJECT

.EATNOTPIZZA
.NOEAT
 MESSAGE 1154 ; Yeuch!
.EATRET
 RETURN
;---
.PRECOMMAND
 GOSUB @SETUPROOM
 X1=CURRENTPOS(WORM)
 IF X1=0 THEN PRECOM2
 OBJECT=WORM
 TRAMPTR=WORMTRAMPTR
 TRAMBASE=WORMTRAMBASE
 M1=1970 ; worm random messages
 GOSUB @MOVEMOBILE

 WORMTRAMPTR=TRAMPTR

.PRECOM2
 X4=CURRENTPOS(TOURISTS) ; preserve from
 IF X4<>ROOM THEN PCNOTTOURARREST
; check to see if player has done anything naughty
 GOSUB @CHECKIFCLOTHED
 IF RESULT=TRUE THEN PCNOTTOURNAKED
 MESSAGE 1668 ; a tourist sees you're naked and calls the cops
 GOSUB @CALLTHECOPS
 GOTO PCNOTTOURARREST ; prevent several sequential arrests

.PCNOTTOURNAKED
 GOSUB @TOURCALLTHECOPS ; see if anything illegal carried

.PCNOTTOURARREST
 X4=CURRENTPOS(TOURISTS) ; preserve from
 OBJECT=TOURISTS
 TRAMPTR=TOURTRAMPTR
 TRAMBASE=TOURISTTRAMBASE
 M1=1973 ; tourists random messages base
 GOSUB @MOVEMOBILE ; move tourist group
;
 TOURTRAMPTR=TRAMPTR
 X1=CURRENTPOS(TOURISTS)
 IF X4=X1 THEN PCTOUREXHIBITS
 IF X4<>86 THEN PCNOTMUSLEAVE
 IF X1<>85 THEN PCNOTMUSLEAVE
 X2=129
 CURRENTPOS(SCREEN)=X2
 X2=SCREEN
 CURRENTPOS(DUMMY)=X2 ; put dummy behind screen
 X2=BEHIND
 HICURRENTPOS(DUMMY)=X2
 IF ROOM<>129 THEN PCNOTMUSEUM
 MESSAGE 1662 ; screen slides back
.PCNOTMUSLEAVE
 IF X4<>86 THEN PCNOTMUSEUM
 IF X1<>129 THEN PCNOTMUSEUM
 CURRENTPOS(SCREEN)=C0
 X2=129
 CURRENTPOS(DUMMY)=X2 ; put dummy in room
 HICURRENTPOS(DUMMY)=C0
 IF X1<>ROOM THEN PCNOTMUSEUM
 MESSAGE 1660 ; tourists enter
 GOTO PCNOTMUSEUM

.PCTOUREXHIBITS
 IF X1<>ROOM THEN PCNOTMUSEUM
 MESSAGE 1661 ; tourists look at exhibits
.PCNOTMUSEUM
 GOSUB @TOURCALLTHECOPS ; see if anything illegal carried
;
; now move suspect round the garden
;
 X4=CURRENTPOS(SUSPECT) ; preserve X4
 IF X4=0 THEN PCNOTSUSPECT
 TRAMBASE=SUSTRAMBASE
 TRAMPTR=SUSTRAMPTR
 OBJECT=SUSPECT
 M1=1979 ; suspect mobile
 GOSUB @MOVEMOBILE
 SUSTRAMPTR=TRAMPTR
 IF ROOM<>X4 THEN PCNOTSUSPECT
 IF X4<>24 THEN PCNOTSUSPECT
 MESSAGE 1680 ; suspect is arrested
 CURRENTPOS(WALLET)=ROOM
 HICURRENTPOS(WALLET)=C0
 CURRENTPOS(SUSPECT)=C0
 SCOREINDEX=SCWALLET
 GOSUB @ADDSCORE

.PCNOTSUSPECT
; now move cleaning droids round unercity
 TRAMBASE=DROIDTRAMBASE
 TRAMPTR=DROID1TRAMPTR
 OBJECT=DROID1
 M1=1976 ; cleaning droid mobile
 GOSUB @MOVEMOBILE
 DROID1TRAMPTR=TRAMPTR
;
 TRAMBASE=DROIDTRAMBASE
 TRAMPTR=DROID2TRAMPTR
 OBJECT=DROID2
 M1=1976
 GOSUB @MOVEMOBILE
 DROID2TRAMPTR=TRAMPTR
;
 TRAMBASE=DROIDTRAMBASE
 TRAMPTR=DROID3TRAMPTR
 OBJECT=DROID3
 M1=1976
 GOSUB @MOVEMOBILE
 DROID3TRAMPTR=TRAMPTR
;
;
; garden events
;
 IF ROOM<210 THEN PCNOTGARDEN
 IF ROOM>218 THEN PCNOTGARDEN
 RANDOM X1
 IF X1>15 THEN PCNOTGARDEN
 X1=CURRENTPOS(WORM)
 M1=1521 ; nice garden events
 IF X1=0 THEN PCGARDEN
 M1=1524 ; nasty garden events
.PCGARDEN
 GOSUB @CRVARYMESSAGE

.PCNOTGARDEN
; desert events
 IF ROOM<220 THEN PCNOTDESERT
 IF ROOM<223 THEN PCDESERT
 IF ROOM<226 THEN PCNOTDESERT
 IF ROOM>231 THEN PCNOTDESERT
.PCDESERT
 RANDOM X1
 IF X1>15 THEN PCNOTDESERT
 M1=1527 ; desert events
 GOSUB @CRVARYMESSAGE

.PCNOTDESERT
 IF HOUR<>6 THEN PCNOTMEETING
 IF MINUTE<5 THEN PCNOTMEETING
 IF MINUTE>15 THEN PCNOTMEETING

 IF ROOM<>111 THEN PCNOTSEEPEOPLE
; see people coming/going
 IF MINUTE<>5 THEN PCNOTSEECOME
.PCSEEPEOPLE
 MESSAGE 1653 ; see people come into meeting
.PCNOTSEECOME
 IF MINUTE<>15 THEN PCNOTSEEPEOPLE
 MESSAGE 1654 ; see people leaving meeting

.PCNOTSEEPEOPLE
 IF ROOM<>112 THEN PCNOTMEETING
; join meeting
 X1=HICURRENTPOS(COLLAR)
 IF X1<>WORN THEN PCNOARRESTS
 MESSAGE 1659 ; fuzbots storm in, and fine everyone
 X4=FINEBISON
 GOSUB @ABSDEBITX4
 GOTO PCNOTMEETING

.PCNOARRESTS
 IF BISON=TRUE THEN PCMEETNOTJOIN
 X1=CURRENTPOS(VALVE)
 IF X1<>PLAYER THEN PCMEETNOTJOIN
 X1=CURRENTPOS(MEDALLION) ; PLUG
 IF X1<>PLAYER THEN PCMEETNOTJOIN
 MESSAGE 1656 ; WANT TO PAY?
 X4=100 ; BISON CHARGE
 GOSUB @DEBITX4
 IF RESULT=FALSE THEN PCDISSAPOINTBISON
 MESSAGE 1657 ; ceremony performed
 BISON=TRUE
 SCOREINDEX=SCBISON
 GOSUB @ADDSCORE
 GOTO PCMEETEND
.PCDISSAPOINTBISON
 MESSAGE 1658 ; Grand master bison is dissapointed
 GOTO PCMEETEND
.PCMEETNOTJOIN
 MESSAGE 1655 ; see someone else become bison
.PCMEETEND
 TIMEPASSED=20 ; ensure meeting has finished

.PCNOTMEETING
; the dagget
 RANDOM X1
 IF X1>160 THEN PCNOTDAGGET
 POS=CURRENTPOS(DAGGET)
 HIPOS=HICURRENTPOS(DAGGET)
 IF POS<>ROOM THEN PCNOTDAGFLOOR
 IF HIPOS<>0 THEN PCNOTDAGFLOOR
 M1=1806 ; dagget on ground events
 GOSUB @DAGGETVMESSAGE
.PCNOTDAGFLOOR
 IF POS<>PLAYER THEN PCNOTDAGCARRIED
 IF HIPOS=0 THEN PCNOTDAGCARRIED
 M1=1803 ; dagget carried events
 GOSUB @DAGGETVMESSAGE
.PCNOTDAGCARRIED
 IF POS<>PETSHOPDROID THEN PCNOTDAGGET
 IF HIPOS<>FORSALE THEN PCNOTDAGGET
 IF ROOM<>91 THEN PCNOTDAGGET
 M1=1800 ; dagget on sale messages
 GOSUB @VARYMESSAGE

.PCNOTDAGGET
 GOTO @GETCOMMAND
;---
.POSTCOMMAND
; Come here after everything associated with a command has been
; processed

 IF BEHEMOTHFOLLOWING=FALSE THEN PCNOTBEHEMOTH
 IF BEHEMOTHFOLLOWING=STUCK THEN PCNOTBEHEMOTH
 X1=CURRENTPOS(BEHEMOTH)
 IF X1<>ROOM THEN PCBEMOVED
 IF BEHEMOTHFOLLOWING=1 THEN PCBEMOVED ; ONLY JUST WOKEN UP
 BEHEMOTHFOLLOWING=4 ; catches up if player doesn't move

.PCBEMOVED
 X1=1505 ; behemoth following group
 ADD X1,BEHEMOTHFOLLOWING
 MESSAGE X1
 CURRENTPOS(BEHEMOTH)=ROOM
 ADD BEHEMOTHFOLLOWING,C1
 IF BEHEMOTHFOLLOWING<>5 THEN PCENDBEHEMOTH

; Behemoth's final charge
 IF ROOM<>223 THEN PCBENOTCLIFF
 MESSAGE 1511 ; behemoth stuck in chasm
 X1=CREVICE
 CURRENTPOS(BEHEMOTH)=X1
 X1=STUCK
 HICURRENTPOS(BEHEMOTH)=X1
 BEHEMOTHFOLLOWING=STUCK
 GOTO PCENDBEHEMOTH

.PCBENOTCLIFF
 IF ROOM<>230 THEN PCBENOTBRAMBLE
 MESSAGE 1512 ; behemoth stuck in brambles
 BEHEMOTHFOLLOWING=1
 GOTO PCENDBEHEMOTH

.PCBENOTBRAMBLE
 MESSAGE 1510 ; player dies
 GOTO @DEATH

.PCENDBEHEMOTH
 MESSAGE DOT

.PCNOTBEHEMOTH
 GOTO @PRECOMMAND
;---
.OBJECTTRIGGER
 RESULT=TRUE ; prevent processing unless reset
 IF OBJECT<>BEHEMOTH THEN OT1
 IF BEHEMOTHFOLLOWING<>FALSE THEN OTNORMAL
 IF VERB=ILOOK THEN OTNORMAL
 IF VERB=IEXAMINE THEN OTNORMAL
 MESSAGE 1505 ; behemoth rises to start chase
 BEHEMOTHFOLLOWING=TRUE
 RETURN
.OT1
 IF OBJECT<>MANHOLE THEN OTNOTMANHOLE
 X1=HICURRENTPOS(PLAYER)
 IF X1=ON THEN OTNOTMANHOLE
 MESSAGE 1715 ; can't reach manhole cover from here
 GOTO @OTPREVENT

.OTNOTMANHOLE
 IF OBJECT<>APPLE THEN OTNOTAPPLE
 X1=CURRENTPOS(APPLE)
 IF X1<>TREE THEN OTNORMAL
 X1=HICURRENTPOS(PLAYER)
 IF X1=ON THEN OTNORMAL
 MESSAGE 1530 ; apple out of reach
 GOTO OTPREVENT
.OTNOTAPPLE
.OTNORMAL
 IF OBJECT<>SHIELD THEN OTNOTSHIELD
 X1=CURRENTPOS(SHIELD)
 IF X1<>BEHEMOTH THEN OTOK
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN CANTREACH
 X1=CURRENTPOS(PLAYER)
 IF X1=BEHEMOTH THEN OTOK ; can only take shield if on behemoth
.CANTREACH
 MESSAGE 1143 ; can't reach that from here
 GOTO OTPREVENT

.OTNOTSHIELD
 GOSUB @GETPRICE ; returns price in X4
 IF X4=0 THEN OTNOSALE
 IF CONTROLLINGWALDROID=TRUE THEN OTGETLOSTWALDROID
 IF DAYSNOSHOWER>2 THEN GETLOSTSMELLY
.OTSHOPSOPEN
 IF OBJECT<>TICKET THEN OTNOTTICKET
 IF BISON=TRUE THEN OTBUYTICKET
 MESSAGE 2012 ; can't buy ticket
 GOTO OTPREVENT
.OTBUYTICKET
 MESSAGE 2013 ; buy ticket
.OTNOTTICKET
 MESSAGE 1575; salesdroid says: "cost is_
 GOSUB @DEBITX4
 IF RESULT=FALSE THEN OTPREVENT ; prevent operation
 MESSAGE 1576; sold!
 IF OBJECT<>DAGGET THEN OTSALENOTDAGGET
 MESSAGE 1809 ; dagget is delighted
.OTSALENOTDAGGET
 CURRENTPOS(OBJECT)=ROOM
 HICURRENTPOS(OBJECT)=C0 ; drop object even if can't take it for some readson
 GOTO OTOK ; prevent further action on bought items

.OTNOSALE
 POS=SCREEN
 HIPOS=NONSPECIFIC
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN OTNOTSCREEN
 MESSAGE 1663 ; the screen is in the way
 GOTO OTPREVENT

.OTNOTSCREEN
 IF OBJECT<>WINE THEN OTNOTWINE
; when it exists, is inside sealed bottle
 MESSAGE 1716 ; the bottle is corked
 GOTO OTPREVENT

.OTNOTWINE
;
.OTOK
 RESULT=FALSE
 RETURN
;
.OTGETLOSTWALDROID
 MESSAGE 1997 ; get lost metal man
.OTPREVENT ; prevent processing
 RESULT=TRUE
 RETURN
;---
.GETLOSTSMELLY
; sales droid doesn't like it if you
; haven't had a shower for 3+ days
 MESSAGE 2055 ; take a shower !
 RESULT=TRUE ; prevent processing ( for OBJECTTRIGGER )
 RETURN
;---
.INCDAY
 ADD DAY,C1
 ADD DAYSNOSHOWER,C1
 ADD DAYSNOWORK,C1
 RETURN
;---
.TICKCLOCK
 GOSUB @SETUPROOM ; shouldn't be necessary *
 IF TIMESINCEEVIDENCE=0 THEN TCNOTEVIDENCE
 ADD TIMESINCEEVIDENCE,C1

.TCNOTEVIDENCE
 ADD TIMEINROOM,TIMEPASSED
 ADD TIMEINDREAM,TIMEPASSED
; ADD PROGRAMLENGTH,TIMEPASSED
 X1=CURRENTPOS(BATPAK)
 IF X1=DAGGET THEN TCGOTBATPAK
 ADD TIMEWITHOUTPOWER,TIMEPASSED
 IF TIMEWITHOUTPOWER<50 THEN TCGOTBATPAK
 IF DAGGETNOPOWER=TRUE THEN TCGOTBATPAK ; have already told player
 POS=ROOM
 HIPOS=0
 OBJECT=DAGGET
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN TCGOTBATPAK
 MESSAGE 1823 ; dagget out of power
 DAGGETNOPOWER=TRUE

.TCGOTBATPAK
 PREVIOUSDAY=DAY
 PREVIOUSHOUR=HOUR
 PREVIOUSMINUTE=MINUTE
 ADD MINUTE,TIMEPASSED
 IF MINUTE<100 THEN TICKCLOCK2
.TICKCLOCKMINUTE
 X1=100
 SUB MINUTE,X1
 ADD HOUR,C1
 IF MINUTE>99 THEN TICKCLOCKMINUTE
; do all hourly activities here
; hourly chime
 X1=CURRENTPOS(TATTOO)
 IF X1<>PLAYER THEN TCMNOTCHIME
 IF TIMEPASSED>10 THEN TCMNOTCHIME ; don't chime after sleep etc.
 MESSAGE 1572 ; hourly chime

.TCMNOTCHIME
 IF HOUR<>9 THEN TCNOTCURFEWWARNING
 MESSAGE 2050 ; curfew starts in 1 hour

.TCNOTCURFEWWARNING
 IF HOUR<>10 THEN TCNOTMIDNIGHT
 MESSAGE 2053 ; curfew has just started
 MESSAGE 1591 ; v.tired

.TCNOTMIDNIGHT
 M1=1590 ; tired
 IF HOUR=9 THEN TCMTIRED
 M1=1592 ; exhausted
 IF HOUR<>1 THEN TCMNOTTIRED
.TCMTIRED
 MESSAGE M1

.TCMNOTTIRED
 IF HOUR<10 THEN TICKCLOCK2
.TICKCLOCKHOUR
 X1=10
 SUB HOUR,X1
 GOSUB @INCDAY
 IF HOUR>9 THEN TICKCLOCKHOUR

.TICKCLOCK2
 TIMEPASSED=1 ; TIME PASSED IN NEXT MOVE (UNLESS OVER-RIDDEN)
;
; All time-dependent code comes here
;
 IF ROOM<210 THEN TCNOTDREAM
 IF ROOM>234 THEN TCNOTDREAM
 IF TIMEINDREAM>50 THEN @TCPARADISEWAKEUP

.TCNOTDREAM
 X1=HICURRENTPOS(PLAYER)
 IF X1<>LIEON THEN TCNOTCOUCH
 X1=CURRENTPOS(PLAYER)
 IF X1<>COUCH THEN TCNOTCOUCH
 MESSAGE 1567 ; couch massages you
 X4=COSTMASSAGE
 GOSUB @ABSDEBITX4 ; debit without checking with player
; ADD CARRYCAPACITY,C1

.TCNOTCOUCH
; print messages for people milling about
 IF ROOM<17 THEN TCNOTPUBLIC
 IF ROOM>87 THEN TCNOTPUBLIC
 IF ROOM=185 THEN TCNOTPUBLIC ; in prison !

 RANDOM X1
 IF X1>60 THEN TCNOTPEOPLE
 M1=1586 ; curfew messages
 IF HOUR<3 THEN TCPEOPLE
 M1=1580 ; rush-hour people
 IF HOUR=3 THEN TCPEOPLE
 IF HOUR=8 THEN TCPEOPLE
 M1=1583 ; non rush-hour people
.TCPEOPLE
 GOSUB @VARYMESSAGE
; is player wearing the costume ?
 X1=HICURRENTPOS(COSTUME)
 IF X1<>WORN THEN TCNOTPEOPLE
 MESSAGE 2060 ; shot as spy
 GOTO @DEATH

.TCNOTPEOPLE
 RANDOM X1
 IF X1>20 THEN TCNOTFUZBOT
; police patrol
 GOSUB @CALLTHECOPS
 IF M1<>0 THEN TCNOTFUZBOT
 MESSAGE 1851 ; but walks on

.TCNOTFUZBOT

.TCNOTPUBLIC
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN TCNOTTV
 IF VERB=IEXAMINE THEN TCNOTTV ; prevent two programs in one turn
 GOSUB @DISPLAYTV

.TCNOTTV
 IF ROOM<>196 THEN TCNOTHABIHALL
; are in habi hall
 X1=CURRENTPOS(INVITATION)
 IF X1<>0 THEN TCNOTHABIHALL
 POS=ROOM
 HIPOS=0
 OBJECT=DAGGET
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN TCNOTHABIHALL
 MESSAGE 1816 ; dagget finds the invitation
 SCOREINDEX=SCINVITATION
 GOSUB @ADDSCORE
 CURRENTPOS(INVITATION)=ROOM

.TCNOTHABIHALL
 X1=CURRENTPOS(DAGGET)
 IF X1<>173 THEN TCNOTSAUCERCHASE

.TCNOTSAUCERCHASE
 X1=CURRENTPOS(PARTITION)
 IF X1<>0 THEN TCNOTPARTITION
 IF ROOM<>165 THEN TCNOTPARTITION
 IF TIMEINROOM<2 THEN TCNOTPARTITION
 MESSAGE 2026 ; partition comes back down
 CURRENTPOS(BOX)=C0
 CURRENTPOS(PARTITION)=ROOM

.TCNOTPARTITION
 IF ROOM<>166 THEN TCNOTDAIS
 IF TIMEINROOM<2 THEN TCNOTDAIS
 MESSAGE 2032 ;foam smothers you
 GOTO @DEATH

.TCNOTDAIS
 IF ROOM<>185 THEN TCNOTPRISON
 IF TIMEINROOM<4 THEN TCNOTPRISON
 MESSAGE 1879 ; relesed from prison
 DEST=80 ; in front of police station
 HIDEST=0
 GOTO @ABSNEWLOCATION

.TCNOTPRISON
 IF ROOM<90 THEN TCNOTSHOP
 IF ROOM>107 THEN TCNOTSHOP
 IF HOUR>2 THEN TCNOTSHOP
 MESSAGE 2052 ; droid calls the cops
 GOSUB CALLTHECOPS

.TCNOTSHOP
 IF ROOM<172 THEN TCNOTALIEN
 IF ROOM>173 THEN TCNOTALIEN
 X1=CURRENTPOS(DAGGET)
 IF X1=174 THEN TCNOTALIEN ; dagget keeps aliens busy
 ADD ALIENCOUNTER,C1
 IF ALIENCOUNTER<3 THEN TCNOTALIEN
 ALIENCOUNTER=C0
 MESSAGE 1951 ; alien sticks head out
 IF ROOM<>173 THEN TCALIENNOTDIE
 MESSAGE 1952 ; and sees you !
 GOTO @ABSTRIGGERALARM

.TCALIENNOTDIE
 MESSAGE 1953 ; looks about, and pops back in

.TCNOTALIEN
 IF HAVETRIGGEREDALARM<2 THEN TCNOTCHASE
 IF ROOM<>173 THEN TCNOTCHASE
 ADD HAVETRIGGEREDALARM,C1
 IF HAVETRIGGEREDALARM<>3 THEN TCCHASENOTDIE
 IF HAVETRIGGEREDALARM<>4 THEN TCNOTCHASE
 MESSAGE 1950 ; technician kills player
 GOTO @DEATH
.TCCHASENOTDIE
 X1=CURRENTPOS(DAGGET) ;>>MIKE 18/8/86
 IF X1<>174 THEN TCNOTCHASE ;>>MIKE 18/8/86
 MESSAGE 1949 ; dagget rushes up
 CURRENTPOS(DAGGET)=ROOM
 HICURRENTPOS(DAGGET)=C0

.TCNOTCHASE
 IF HOUR>2 THEN TCNOTARREST
 IF ROOM<82 THEN TCNOTARREST
 IF ROOM>134 THEN TCNOTARREST
 GOSUB CALLTHECOPS ; fuzbot appears, and will arrest player

.TCNOTARREST
;
 IF CONTROLLINGWALDROID=TRUE THEN TCRET
 IF HOUR=2 THEN @FORCESLEEP
.TCRET
 RETURN
;---
.TOURCALLTHECOPS
 X1=CURRENTPOS(TOURISTS)
 IF X1<>ROOM THEN TCRET
 GOSUB @CHECKPOSSESIONS
 IF X4<>FINEMUSEUM THEN TCRET
 MESSAGE 1667 ; tour guide calls the cops
; fall through to CALLTHECOPS
;---
.CALLTHECOPS
 IF TIMESINCEEVIDENCE<4000 THEN CTCNOTDIE
 MESSAGE 1878 ; fuzbots descend on you
 GOTO @DEATH

.CTCNOTDIE
 MESSAGE 1850 ; fuzbot appears and looks you over
.ABSCALLTHECOPS
 OBJECT=COSTUME
 POS=PLAYER
 HIPOS=NONSPECIFIC
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN ACTC1
 MESSAGE 2061 ; fuzbot kills you
 GOTO @DEATH

.ACTC1
 IF HOUR<3 THEN @CURFEWBREAKER

; now check if player is doing anything illegal
 M1=0
 IF CONTROLLINGWALDROID=TRUE THEN @CTCNOTGUILTY ; waldroids can do no wrong
; has broken collar appeared ?
 X1=HICURRENTPOS(COLLAR)
 IF X1=WORN THEN CTCNOTBROKEN
 X1=HICURRENTPOS(BROKENCOLLAR)
 IF X1=WORN THEN CTCNOTBROKEN
 CURRENTPOS(BROKENCOLLAR)=C0
 HICURRENTPOS(BROKENCOLLAR)=C0
 X1=PLAYER
 CURRENTPOS(COLLAR)=X1
 X1=WORN
 HICURRENTPOS(COLLAR)=X1
 M1=1862
 X4=FINECOLLAR
 GOSUB CTCFINE

.CTCNOTBROKEN
; is player carrying anything illegal ?
 GOSUB CHECKPOSSESIONS
 IF M1=0 THEN CTCNOPOSSESIONS
 GOSUB CTCFINE
 IF X4<>FINEMUSEUM THEN CTCNOPOSSESIONS
 GOSUB @INITANOBJECT ; confiscate museum objects
.CTCNOPOSSESIONS
; is player in debt
 IF CREDITS<NEGATIVE THEN CTCNOTDEBT
 X4=FINEDEBT
 M1=1867
 GOSUB CTCFINE
 DEST=102 ; body bank
 HIDEST=0
 GOSUB @NEWLOCATION
 GOTO @CANCELINPUT
;
.CTCFINE
 MESSAGE 1852 ; it fines you_
 GOSUB @ABSDEBITX4
 PRINT X4
 MESSAGE 1853 ; _creds.
 MESSAGE M1
 MESSAGE DOT
 RETURN
;---
.CTCNOTDEBT
 GOSUB @CHECKIFCLOTHED
 IF RESULT=TRUE THEN CTCNOTNAKED
 X4=FINENUDITY
 M1=1868 ; fine for nudity
 GOSUB CTCFINE

.CTCNOTNAKED
 IF DAYSNOWORK<4 THEN CTCNOTPARASITE
 DAYSNOWORK=0
 X4=200 ; fine for being a parasite
 M1=1864
 GOSUB CTCFINE

.CTCNOTPARASITE
.CTCNOTGUILTY
; player seems to be ok
 RETURN
;---
.CHECKPOSSESIONS
; rifle through player's possesions
; checking for anything carried which is
; illegal. return X4=fine and M1=appropriate message
; and OBJECT=illegal object
; first check for leotard - takes priority over lesser crimes
 OBJECT=LEOTARD
 M1=1861 ; museum fine
 X4=FINEMUSEUM
 X1=CURRENTPOS(LEOTARD)
 IF X1<>PLAYER THEN CHECKPOSS0
 X1=HICURRENTPOS(LEOTARD)
 IF X1<>WORN THEN CHECKPOSSRET ; caught
 X1=HICURRENTPOS(TRADCLADS)
 IF X1<>WORN THEN CHECKPOSSRET ; leotard seen unless worn under tradclad

.CHECKPOSS0
 OBJECT=1
.CHECKPOSS1
 X1=CURRENTPOS(OBJECT)
 IF X1<>PLAYER THEN CHECKPOSS2
 X1=HICURRENTPOS(OBJECT)
 IF X1=0 THEN CHECKPOSS2
 X4=FINEMUSEUM
 M1=1861 ; museum fine
 IF OBJECT=HELMET THEN CHECKPOSSRET
 IF X1=WORN THEN CHECKPOSS2 ; all other worn objects are OK
 IF OBJECT=CUP THEN CHECKPOSSRET
 IF OBJECT=HELMET THEN CHECKPOSSRET
; legal objects
 IF OBJECT=JOBCENTRECARD THEN CHECKPOSS2
 X4=FINEOBJECT
 M1=1860
.CHECKPOSSRET
 RETURN
.CHECKPOSS2
 ADD OBJECT,C1
 IF OBJECT<MAXMOVEABLE THEN CHECKPOSS1
 X4=0
 M1=0
 RETURN
;---
.CHECKIFCLOTHED
; return RESULT=TRUE if wearing clothes
 RESULT=TRUE
 X1=HICURRENTPOS(TRADCLADS)
 IF X1=WORN THEN CICOK
 X1=CURRENTPOS(LEOTARD)
 IF X1<>PLAYER THEN CICFAIL
 X1=HICURRENTPOS(LEOTARD)
 IF X1=WORN THEN CICOK
.CICFAIL
 RESULT=FALSE
.CICOK
 RETURN
;---
.WAIT
 MESSAGE 1155 ; time passes
 TIMEPASSED=10
 RETURN
;---
.SLEEP
; voluntarily go to sleep
 IF HOUR<4 THEN SLEEP1
 IF HOUR>8 THEN SLEEP1
 MESSAGE 1595 ; not sleepy
 RETURN
;
.SLEEP1
 X1=CURRENTPOS(PLAYER)
 IF X1<>BED THEN SLEEPNOTONBED
; X1=HICURRENTPOS(PLAYER)
; IF X1=0 THEN SLEEPNOTONBED
 MESSAGE 1600 ; go to sleep in bed
 GOTO ABSSLEEP
.SLEEPNOTONBED
 MESSAGE 1601 ; go to sleep where player is
 GOTO ABSSLEEP
;---
.FORCESLEEP
 MESSAGE 1594 ; player drops from exhaustion

.ABSSLEEP
 M1=1815 ; goodnight
 GOSUB @DAGGETMESSAGE
 IF ROOM<17 THEN SLEEPNOTARRESTED
 IF ROOM>87 THEN SLEEPNOTARRESTED
 MESSAGE 1606 ; wake in cells

.CURFEWBREAKER
 X4=FINECURFEW
 M1=1869
 GOSUB @CTCFINE
 GOSUB DOSLEEP
 DEST=185 ; prison cell
 HIDEST=0
 GOTO @NEWLOCATION
;
.SLEEPNOTARRESTED
 GOSUB DOSLEEP
 GOTO @CANCELINPUT
;---
.DOSLEEP
 GOSUB SLEEPDREAM
 X1=CURRENTPOS(PLAYER)
 IF X1<>BED THEN SLEEPNOTBED
; X1=HICURRENTPOS(PLAYER)
; IF X1<>0 THEN SLEEPNOTBED
 MESSAGE 1604 ; awake refreshed
 SCOREINDEX=SCSLEEP
 GOSUB @ADDSCORE
 GOTO SLEEPEND
.SLEEPNOTBED
 MESSAGE 1605 ; awake not refreshed
.SLEEPEND
 MINUTE=1 ; can't use TICKCLOCK due to scenery messages etc.
 MESSAGE 2051 ; curfew is over
 c4=4
 ADD HOUR,c4
 IF HOUR<10 THEN SLEEPEND2
 X1=10
 SUB HOUR,X1
 GOSUB @INCDAY
.SLEEPEND2
 IF HOUR<3 THEN SLEEPFORCEDAYTIME
 RETURN
.SLEEPFORCEDAYTIME ;>>MIKE 18/8/86
 HOUR=3
 RETURN
;---
.SLEEPDREAM
 RANDOM X1
 IF X1>80 THEN SDCLUEDREAM
 IF SCOREDTODAY=TRUE THEN SDSWEETDREAM
; hurry-up dream
 M1=1613
 GOTO SDEND
.SDSWEETDREAM
 SCOREDTODAY=FALSE
 M1=1610
.SDEND
 GOSUB @VARYMESSAGE
 MESSAGE DOT
 RETURN

.SDCLUEDREAM
 M1=0
 X1=CURRENTPOS(DAGGET)
 IF X1=0 THEN SDCNOTTRICK
 M1=1625 ; dream about being conned

.SDCNOTTRICK
 IF PHOTO=172 THEN SDCNOTPHOTO
 M1=1624 ; dream about alien invasion

.SDCNOTPHOTO
 IF HAVEGONEBEHINDWALLS=TRUE THEN SDCNOTWALLS
 M1=1623 ; dream about enemies behind the walls

.SDCNOTWALLS
 IF BISON=TRUE THEN SDCNOTBISON
 M1=1622 ; dream about bouncing buffalos

.SDCNOTBISON
 X1=CURRENTPOS(SUSPECT)
 IF X1=0 THEN SDCNOTATTACK
 M1=1621 ; dream about attack in park

.SDCNOTATTACK
 IF HAVEVISITEDHOME=TRUE THEN SDCNOTHOME
 M1=1620 ; dream about beautiful home

.SDCNOTHOME
 IF M1=0 THEN SDSWEETDREAM ; no clues to give
 MESSAGE M1
 MESSAGE DOT
 RETURN
;---
.TCPARADISEWAKEUP
; time-controlled wake-up
 MESSAGE 1540 ; time up
.PARADISEWAKEUP
 BEHEMOTHFOLLOWING=FALSE
 M1=1539 ; wake up from first dream - realisation etc.
 if notfirstdream=false then pwu1
 m1=1541 ; wake up from subsequent dreams

.pwu1
 message m1
 notfirstdream=true
 GOSUB @RESTOREPOSSESIONS
 DEST=121 ; In pleasure dome
 HIDEST=0
 GOSUB @ABSNEWLOCATION
 GOTO @CANCELINPUT
;---
.CONVERTVERB
; Given VERB,PREP, convert to standard forms of certain commands
; e.g. LOOK AT => EXAMINE ;     GET ON => STAND ON  ;  GET OFF => LEAVE
; This is called as soon as the first noun is found - so
; if it is necessary to separate prepositions before+after nouns,
; this is a good place to do it

 IF VERB<>ILOOK THEN CVNOTLOOK
 IF PREP<>AT THEN CVNOTLOOK
 VERB=IEXAMINE
 PREP=0

.CVNOTLOOK
 IF VERB<>ITAKE THEN CVNOTSTAND
 IF PREP=0 THEN CVNOTSTAND
 VERB=ISTAND

.CVNOTSTAND
 IF VERB<>32 THEN CVNOTON
 PREP=1
 VERB=ISTAND

.CVNOTON
 IF VERB<>33 THEN CVNOTOFF
 PREP=66
 VERB=ISTAND

.CVNOTOFF

.CVNOTONOFF
.CONVRET
 RETURN
;---
.SELECTOBJECTPOS
; given VERB,PREP
; set up HISEARCHPOS to show where the objects which follow on
; the input line must be - i.e.
; NOUNONGROUND, NOUNCARRIED or NONSPECIFIC
 HISEARCHPOS=NOUNONGROUND
 IF VERB=ITAKE THEN SOPRET
 IF VERB=ISTAND THEN SOPRET
 HISEARCHPOS=NOUNCARRIED
 IF VERB=IDROP THEN SOPRET
 HISEARCHPOS=NONSPECIFIC
.SOPRET
 RETURN
;---
.FUNNIES
; Come here at markobject stage. Have found a bit of scenery
; Value of noun in 'OBJECT' (offset from NOUNOFFSET)
; Also have 'VERB' and may have 'PREP' if one has been entered
; before this noun on the input line
; Already printed is 'OBJECT:'
; on return, DOTCR is printed
; Return RESULT=TRUE if a special message is printed in this routine

 RESULT=TRUE
 IF VERB<>ICLIMB THEN FNOTCLIMB
 IF OBJECT<381 THEN FNOTCLIMBWALL ; MESSAGE NUMBER FOR WALL
 IF OBJECT>389 THEN FNOTCLIMBWALL ; UPPER BOUND FOR WALL
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN FCLIMBWALL1
 X1=CURRENTPOS(PLAYER)
 IF X1<>BENCH THEN FCLIMBWALL1
 MESSAGE 1532 ; despite standing on bench, wall is too high
 RETURN
.FCLIMBWALL1
 MESSAGE 1531 ; wall to high to climb
 RETURN
;
.FNOTCLIMBWALL
.FNOTCLIMB
 IF VERB<>IPICK THEN FNOTPICK
 IF OBJECT<380 THEN FNOTPICKFLOWERS
 IF OBJECT>388 THEN FNOTPICKFLOWERS
 MESSAGE 2040 ; can't pick flowers
 RETURN

.FNOTPICKFLOWERS
.FNOTPICK
 IF VERB<>ISMELL THEN FNOTSMELL
 IF ROOM<210 THEN FSMELLNOTGARDEN
 IF ROOM>218 THEN FSMELLNOTGARDEN
 MESSAGE 2042 ; smell in garden
 RETURN
.FSMELLNOTGARDEN
 MESSAGE 1174 ; smell nothing special
 RETURN

.FNOTSMELL
 IF VERB<>ILISTEN THEN FNOTHEAR
 IF ROOM<210 THEN FHEARNOTGARDEN
 IF ROOM>218 THEN FHEARNOTGARDEN
 MESSAGE 2041 ; listen in garden
 RETURN
.FHEARNOTGARDEN
 MESSAGE 1173 ; hear nothing special
 RETURN

.FNOTHEAR
 RESULT=FALSE
 RETURN
;---
.DEBITX4
; remove X4 credits from player.
; return RESULT=TRUE if ok
; or     RESULT=FALSE if not done
 PRINT X4
 MESSAGE 1544 ; do you want to proceed ?
 GOSUB YESORNO
 IF RESULT=FALSE THEN DEBITX4CANCEL

.ABSDEBITX4
 SUB CREDITS,X4
 RETURN
;---
.REPORTWEALTH
 IF CREDITS<NEGATIVE THEN REPWPOS
 MESSAGE 1547 ; you are_
 X1=0
 SUB X1,CREDITS
 PRINT X1
 MESSAGE 1548 ; credits in debt
 RETURN
.REPWPOS
 MESSAGE 1545 ; you have
 PRINT CREDITS
 MESSAGE 1546 ; credits left
 RETURN
.DEBITX4CANCEL
 MESSAGE 1551
 RETURN
;---
.YESORNO
 nomoreinput=false
 supresschecking=true
 GOSUB @GETNEXTWORD
 IF EOL=TRUE THEN YESORNO
 SEARCHTYPE=NOUNTYPE
 GOSUB @CHECKTYPE
 IF VALUE=IYES THEN YONYES
 IF VALUE=INO THEN YONNO
 IF VALUE=INORTH THEN YONNO
 searchtype=verbtype
 gosub @checktype
 if value=iram then @askin2
 if value=ioops then @askin3

.yontryagain
 MESSAGE 1160 ; yes or no please
 GOSUB @CLEARINPUT
 GOTO YESORNO
.YONYES
 RESULT=TRUE
 RETURN
.YONNO
 RESULT=FALSE
 RETURN
;---
.INITDREAM
; have paid money, now select dream to see by which room player is in
 MESSAGE 1550 ; room dissolves around you
 IF ROOM=121 THEN INITPARAAGAIN
 SCOREINDEX=SCGAME
 GOSUB @ADDSCORE
 X1=1431 ; dreams are Eden( ROOM 123) =1553 and upwards to (128)=1558
 ADD X1,ROOM
 MESSAGE X1
 if room<>121 then afterinitdream ;>>mike 26/10/86
 MESSAGE 1541 ; the dream ends
 GOTO @CANCELINPUT
.afterinitdream ;>>mike 27/10/86
 message dot
 goto @cancelinput
;---
.INITPARAAGAIN
 GOSUB INITPARADISE
 GOTO @CANCELINPUT
;---
.INITPARADISE
 GOSUB SAVEPOSSESIONS
; now own nothing
; so reposition everything in dream section
 OBJECT=APPLE
 GOSUB INITANOBJECT
 OBJECT=APPLECORE
 GOSUB INITANOBJECT
 OBJECT=BENCH
 GOSUB INITANOBJECT
 OBJECT=HOLE
 GOSUB INITANOBJECT
 OBJECT=BEHEMOTH
 GOSUB INITANOBJECT
 OBJECT=SHIELD
 GOSUB INITANOBJECT
 OBJECT=WORM
 GOSUB INITANOBJECT
 BEHEMOTHFOLLOWING=FALSE
 DEST=212
 HIDEST=0
; timeindream=0 ;>>mike 15/10/86
 GOTO @ABSNEWLOCATION ; start dream
;---
.INITANOBJECT
 X4=OBJECT
 ADD X4,X4
 X1=OBJECTSTART(X4)
 CURRENTPOS(OBJECT)=X1
 ADD X4,C1
 X1=OBJECTSTART(X4)
 GOSUB @STRIPX1 ; remove top two bits
 HICURRENTPOS(OBJECT)=X1
 RETURN
;---
.SAVEPOSSESIONS
 POS=PLAYER
 DEST=SAVEPLAYER
 HIDEST=NONSPECIFIC
;
.POSSLOOP
; HIDEST=NONSPECIFC TO LEAVE HICURRENTPOS ALONE
; OR NEW HICURRENTPOS OF ALL OBJECTS
 OBJECT=0
.POSS1
 X1=CURRENTPOS(OBJECT)
 IF X1<>POS THEN POSS2
 X2=HICURRENTPOS(OBJECT)
 IF X2=0 THEN POSS2
 CURRENTPOS(OBJECT)=DEST
 IF HIDEST=NONSPECIFIC THEN POSS2
 HICURRENTPOS(OBJECT)=HIDEST
.POSS2
 ADD OBJECT,C1
 IF OBJECT<141 THEN POSS1
 RETURN
;---
.RESTOREPOSSESIONS
; drop everything carried in dream
 POS=PLAYER
 DEST=0
 HIDEST=0
 GOSUB POSSLOOP

 POS=SAVEPLAYER
 DEST=PLAYER
 HIDEST=NONSPECIFIC
 GOTO POSSLOOP
;---
.PUSH
 NUMBER=INUMBER
 IF INUMBER<10 THEN PUSHOK
 IF NOUN1<>BUTTONS THEN PUSHNOTBUTTONS
 MESSAGE 1568 ; must specify button to press
 RETURN
;
.PUSHNOTBUTTONS
 IF NOUN1<>MIRROR THEN @IMMOVABLE
 MESSAGE 2025 ; mirror revolves
 DEST=163
 HIDEST=0
 GOTO @NEWLOCATION
;---
.PUSHOK
 IF ROOM=169 THEN @MOVEELEVATOR
 BANDITNUMBER=INUMBER
 MESSAGE 1560 ; buttons go dark, then_
 GOSUB PRINTCOLOUR
 MESSAGE 1561; _button lights up
 RETURN
;---
.PRINTCOLOUR
 X1=NUMBERBASE
 ADD X1,NUMBER
 MESSAGE X1 ; print colour
 RETURN
;---
.RAND10
; return NUMBER=a ranom number between 0 and 9
 NUMBER=0
 X1=25
 RANDOM X2
.RAND10A
 IF X2>NEGATIVE THEN RAND10END
 IF X2<X1 THEN RAND10END
 ADD NUMBER,C1
 SUB X2,X1
 GOTO RAND10A
.RAND10END
 IF NUMBER>9 THEN RAND10
 RETURN
;---
.RAND3
; return NUMBER=a random number between 0 and 2
; no other variables to be corrupted
 RANDOM NUMBER
 IF NUMBER>80 THEN RAND3A
 NUMBER=0
 RETURN
.RAND3A
 IF NUMBER>160 THEN RAND3B
 NUMBER=1
 RETURN
.RAND3B
 NUMBER=2
 RETURN
;---
.PULL
 IF NOUN1<>ARM THEN @BADOBJECT
 MESSAGE 1562 ; lights come up:
 GOSUB RAND10
 X3=NUMBER
 GOSUB PRINTCOLOUR
 MESSAGE AND
 GOSUB RAND10
 GOSUB PRINTCOLOUR
 MESSAGE DOT
 IF X3=BANDITNUMBER THEN PULLWIN
 IF NUMBER=BANDITNUMBER THEN PULLWIN
 GOTO GAMBLEEND

.PULLWIN
 IF X3=NUMBER THEN PULLJACKPOT
 MESSAGE 1564 ; you win_
.PULLWIN1
 GOSUB RAND10
 X1=2
 ADD X1,NUMBER
 IF X1>8 THEN PULLWIN1 ; limit max payout to worsen odds
.ADDCREDITSX1
 PRINT X1 ; number of credits to win
 ADD CREDITS,X1
 MESSAGE 1565 ; credits
.GAMBLEEND
 X4=1
 GOTO @ABSDEBITX4
;
.PULLJACKPOT
 MESSAGE 1563 ; JACKPOT!!!
 X1=20
 GOTO ADDCREDITSX1
;---
.SPECIALEXAMINE
; print any special examine messages which follow
; the basic message
 RESULT=TRUE ; flag for punctuation already printed
 IF OBJECT<>TATTOO THEN SENOTTATTOO
 GOSUB DISPLAYTIME
 MESSAGE 1571 ; and that_
 GOSUB @REPORTWEALTH

.SENOTTATTOO
 IF OBJECT<>BADGE THEN SENOTBADGE
 INDEX=14 ; Own habihome
 GOTO @DISPLAYCOLOURCODE

.SENOTBADGE
 IF OBJECT<>BROOCH THEN SENOTBROOCH
 INDEX=21 ; traitor's habihome
 GOTO @DISPLAYCOLOURCODE

.SENOTBROOCH
 IF OBJECT<>POSTER THEN SENOTPOSTER
 IF TVON=FALSE THEN SEPOSTEROFF
 GOSUB @DISPLAYTV
.SEPOSTEROFF
 RESULT=TRUE
 RETURN

.SENOTPOSTER
 IF OBJECT<>MANHOLE THEN SENOTMANHOLE
 IF COVERLUBRICATED=TRUE THEN SENOTMANHOLE
 IF ROOM<>184 THEN SENOTMANHOLE
 MESSAGE 1717 ; manhole is stuck

.SENOTMANHOLE
 IF OBJECT<>BEHEMOTH THEN SPENOTBEHEMOTH
 IF BEHEMOTHFOLLOWING<>FALSE THEN SPENOTBEHEMOTH
 MESSAGE 1501 ; the behemoth is asleep

.SPENOTBEHEMOTH
 RESULT=FALSE
 RETURN
;---
.DISPLAYTIME
 MESSAGE 1570 ; time is_
 PRINT HOUR
 MESSAGE COLONNOSPACE
 PRINT MINUTE
 RETURN
;---
.GETPRICE
; return price of 'OBJECT' in X4
.GETPRICE2 ; OBJECT IS ON SALE
 POS=0 ; nonspecific
 HIPOS=FORSALE
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN GETPRICENOSALE
 IF VERB=IEXAMINE THEN GETPRICENOSALE
 OBJECT=X4 ; ensure player can not been sneaky by
; trying to buy something contained by an item on sale
 X4=9
 IF OBJECT=BOTTLE THEN GETPRICERET
 IF OBJECT=FLAG THEN GETPRICERET
 IF OBJECT=BOX THEN GETPRICERET
 IF OBJECT=PIZZA THEN GETPRICERET
 X4=90
 IF OBJECT=MEDALLION THEN GETPRICERET
 IF OBJECT=TICKET THEN GETPRICERET
; valve is free to waldroids, very expensive to humans
 X4=1000
 IF OBJECT<>VALVE THEN GPNOTVALVE
 IF CONTROLLINGWALDROID=FALSE THEN GETPRICERET
 MESSAGE 1999 ; get valve for nothing
 SCOREINDEX=SCVALVE
 GOSUB @ADDSCORE

.GPNOTVALVE
 X4=300
 IF OBJECT=CAMERA THEN GETPRICERET
 X4=19 
 IF OBJECT=BOUQUET THEN GETPRICERET
 IF OBJECT=WREATH THEN GETPRICERET
 IF OBJECT=PLANT THEN GETPRICERET
; calculate dagget price
 X1=DAY
 X4=1000
 X2=100

.CALCDAGPRICE
 SUB X4,X2
 SUB X1,C1
 IF X1<32000 THEN CALCDAGPRICE
 IF X4>32000 THEN CALCDAG1
 IF X4>499 THEN CALCDAG2
.CALCDAG1
 X4=500
.CALCDAG2
 IF OBJECT=DAGGET THEN GETPRICERET
.GETPRICENOSALE
 X4=0
.GETPRICERET
 RETURN
;---
.DAGGETVMESSAGE
 GOSUB @RAND3
 ADD M1,NUMBER
.DAGGETMESSAGE
 IF DAGGETNOPOWER=TRUE THEN DAGMESRET
; is dagget nearby ?
 POS=ROOM
 HIPOS=0
 OBJECT=DAGGET
 GOSUB @CHECKOBJECTPOS ; check if carried or on ground
 IF RESULT=FALSE THEN DAGMESRET
 MESSAGE CR
 IF M1<1810 THEN DAGMESPRINT
 MESSAGE 1810 ; the dagget says:

.DAGMESPRINT
 MESSAGE M1
.DAGMESRET
 RETURN
;---
.SAY
; called from CHECKNOUN when OBJECT has been found
; as the highest value
 OBJECT=0
 INDEX=0
 VERB=0 ; PREVENT PRESENTMULTIPLE ACTING ON IT
.SAY0
 SEARCHTYPE=NOUNTYPE
 GOSUB @CHECKTYPEMORE
 IF VALUE=0 THEN SAY1
 OBJECT=VALUE
 GOTO SAY0

.SAY1
 IF OBJECT<>IBED THEN SAYNOTBED
; first caluclate destination for objects/player swept up by bed
 DEST=COLOURTABLE(6) ; which pyramid is this habihome in ?
 X1=180 ; undercity base
 ADD DEST,X1
 HIDEST=0
 X1=CURRENTPOS(CRACK)
 IF X1<>ROOM THEN SAYNOTCRACK
; generate bed
 MESSAGE 1700 ; bed swings down
 CURRENTPOS(CRACK)=C0
 CURRENTPOS(BED)=ROOM
 RETURN

.SAYNOTCRACK
 X1=CURRENTPOS(BED)
 IF X1<>ROOM THEN SAYNOTBED
 MESSAGE 1701 ; bed swings away
 CURRENTPOS(BED)=C0
 CURRENTPOS(CRACK)=ROOM
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN BEDNOTPLAYER
 X1=CURRENTPOS(PLAYER)
 IF X1<>BED THEN BEDNOTPLAYER
; player was on bed, so give it a rude awakening
 MESSAGE 1703 ; with you on it!
 HAVEGONEBEHINDWALLS=TRUE
 GOSUB @BEDOBJECTS ; throw all objects with player
 SCOREINDEX=SCUNDERCITY
 GOSUB @ADDSCORE
 GOTO @NEWLOCATION
;
.BEDNOTPLAYER
 MESSAGE 1702
 GOTO @BEDOBJECTS ;throw everything into undercity
;---
.SAYNOTBED
 X1=CURRENTPOS(POSTER)
 IF X1<>ROOM THEN SAYNOTTV
 if object=poster then saytvon
 IF OBJECT<>ION THEN SAYTVNOTON
.saytvon
 MESSAGE 1720 ; the poster lights up
 TVON=TRUE
 RETURN

.SAYTVNOTON
 IF OBJECT<>IOFF THEN SAYTVNOTOFF
 MESSAGE 1720 ; poster goes dark
 TVON=FALSE
 RETURN

.SAYTVNOTOFF
.SAYNOTTV
 IF ROOM=192 THEN SAYSHOWER
 IF ROOM<>198 THEN SAYNOTSHOWER
.SAYSHOWER
 IF OBJECT<>ION THEN SAYNOTSHOWER
 GOSUB @CHECKIFCLOTHED
 IF RESULT=FALSE THEN SAYSHOWERGO
 MESSAGE 1704 ;remove clothes before showering
 RETURN
.SAYSHOWERGO
 MESSAGE 1705 ; get showered
 DAYSNOSHOWER=0
 RETURN

.SAYNOTSHOWER
 IF ROOM<>100 THEN SAYNOTPOLICESTATION
 MESSAGE 1871 ; fuzbot looks blank
 RETURN

.SAYNOTPOLICESTATION
 IF ROOM<>102 THEN SAYNOTBODYBANK
 IF OBJECT=IYES THEN @VOLSELLORGANS
 MESSAGE 1910 ; consultant ignores you
 RETURN

.SAYNOTBODYBANK
 IF OBJECT<>IEXIT THEN SAYNOTEXIT
 IF ROOM<63 THEN SAYNOTEXIT
 IF ROOM>75 THEN SAYNOTEXIT
 MESSAGE 1690 ; force field grabs you
; zero colour code
 X1=0
.SAYEXIT1
 COLOURTABLE(X1)=C0
 ADD X1,C1
 IF X1<6 THEN SAYEXIT1
 DEST=63
 HIDEST=0
 DIR=0
 GOTO @NEWLOCATION

.SAYNOTEXIT
 IF OBJECT<>IHOME THEN SAYNOTHOME
 IF ROOM>81 THEN SAYNOTHOME
 IF ROOM<17 THEN SAYNOTHOME
.SAYHOME
 MESSAGE 3907 ; robots appear and carry you home
 X1=0
 X2=14 ; address of own habihome
.SAYHOME1
 X3=COLOURTABLE(X2)
 COLOURTABLE(X1)=X3
 ADD X2,C1
 ADD X1,C1
 IF X1<7 THEN SAYHOME1
 DIR=0
 DEST=63
 HIDEST=0
 TIMEPASSED=20
 GOTO @NEWLOCATION

.SAYNOTHOME
.SAID
 MESSAGE 1156 ; said
.SAYRET
 RETURN
;---
.BEDOBJECTS
; everything on bed (including player) is thrown into undercity
 POS=BED
 HIPOS=NONSPECIFIC
 HIDEST=0
 GOTO @POSSLOOP
;---
.OPEN
;
 IF NOUN1<>BOTTLE THEN OPENNOTBOTTLE
.REMOVECORK
 X1=CURRENTPOS(WINE)
 IF X1=0 THEN ALREADYOPEN
 MESSAGE 1712 ; WINE SPRAYS OUT
 CURRENTPOS(WINE)=C0
 CURRENTPOS(CORK)=ROOM
 HICURRENTPOS(CORK)=C0
 IF ROOM<>184 THEN OPENRET
 MESSAGE 1713 ; LUBRICATING THE MANHOLE COVER
 COVERLUBRICATED=TRUE

.OPENRET
 RETURN

.OPENNOTBOTTLE
 IF ROOM<>184 THEN OPENNOTMANHOLE
 IF NOUN1<>MANHOLE THEN OPENNOTMANHOLE
 IF COVERLUBRICATED=TRUE THEN OPENNOTMANHOLE
 MESSAGE 1717 ; it seems to be stuck
 RETURN

.OPENNOTMANHOLE
.DONTNEEDTODOTHAT
 MESSAGE 1148 ; don't need to do that
 RETURN
;---
.ALREADYOPEN
 MESSAGE 1117 ; already open
.DISPLAYTVRET
 RETURN
;---
.DISPLAYTV
; show current tv program / advert / newsflash
 X1=CURRENTPOS(POSTER)
 IF X1<>ROOM THEN DISPLAYTVRET
 IF TVON=FALSE THEN DISPLAYTVRET
 SCOREINDEX=SCPOSTER
 GOSUB @ADDSCORE
 MESSAGE 1736 ;current program is
 RANDOM X1
 IF X1>160 THEN TVREALPROGRAM
; have a news item or commercial break
 IF X1>80 THEN TVNEWS
.TVAD
;>> changes over next ten lines (adverts) mike 27/10/86
 add currentadvert,c1 ;>>
 if currentadvert<5 then tvadok ;>>
 currentadvert=1 ;>>
.tvadok ;>>

 if currentadvert<>4 then tvadnottravelagent ;>>
;>> RANDOM X1
;>> IF X1>63 THEN TVADNOTTRAVELAGENT
 MESSAGE 1737 ; travel agent advert
 INDEX=42
 GOTO TVADFINISH

.TVADNOTTRAVELAGENT
;>> GOSUB @RAND3
 X1=1721 ;>>mike 27/10/86 - was 1722
 ADD X1,currentadvert ;>>NUMBER
 MESSAGE X1
 INDEX=28 ; florists
 IF currentadvert=1 THEN TVADFINISH
 INDEX=35 ; hardware warehouse
 IF currentadvert=2 THEN TVADFINISH
 INDEX=49 ; jobcentre
.TVADFINISH
 GOTO @DISPLAYCOLOURCODE
;---
.TVNEWS
; have a news bulletin if anything interesting has happened
 RANDOM X1
 IF X1>128 THEN TVRANDNEWS
 M1=0
 X1=CURRENTPOS(SUSPECT)
 IF X1<>0 THEN TVNEWSNOTSUSPECT
 M1=1725 ;suspect arrested
;
.TVNEWSNOTSUSPECT
 IF PARTYMEMBER=FALSE THEN TVNEWSNOTYOU
 M1=1726 ; see yourself on TV
 IF TIMESINCEEVIDENCE<2000 THEN TVNEWSNOTYOU
 M1=1727 ; alien death threats against you

.TVNEWSNOTYOU
 IF M1=0 THEN TVRANDNEWS
 MESSAGE M1
 RETURN

.TVRANDNEWS
 M1=1730 ; random news messages
 GOTO @VARYMESSAGE
;---
.TVREALPROGRAM
; real, randomly - generated program
 X4=1740 ; base for random TV generation messages
.TVRP1
 GOSUB @RAND10
 IF NUMBER>3 THEN TVRP1
 ADD NUMBER,X4
 MESSAGE NUMBER
 X2=4
 ADD X4,X2
 MESSAGE X4 ; punctuation etc.
 ADD X4,C1
 IF X4<1765 THEN TVRP1
 RETURN
;---
.CHECKIFINRIGHTPLACE
; check if OBJECT is somewhere which the police will accept as legal
 RESULT=FALSE
 X1=CURRENTPOS(OBJECT)
 IF X1=WALLET THEN CIRPOK
 IF X1>200 THEN CIRPRET ; can't be habihome
 IF X1>195 THEN CIRPOK ; traitor's habihome
 IF X1=0 THEN CIRPOK ; destroyed / in police station
 IF X1<>100 THEN CIRPRET ;in police station
.CIRPOK
 RESULT=TRUE
.CIRPRET
 RETURN
;---
.GIVE
 IF ROOM<>100 THEN GIVENOTFUZBOT
 CURRENTPOS(NOUN1)=C0 ;object retained for inspection
 HICURRENTPOS(NOUN1)=C0
 MESSAGE 1872 ;fuzbot has object examined
 IF NOUN1<>WALLET THEN GIVENOTWALLET
 MESSAGE 1874 ; wallet belonged to suspect
 OBJECT=INVITATION
 GOSUB CHECKIFINRIGHTPLACE
 IF RESULT=FALSE THEN GIVESTOLEN
 OBJECT=BROOCH
 GOSUB CHECKIFINRIGHTPLACE
 IF RESULT=FALSE THEN GIVESTOLEN
 MESSAGE 1875 ; get reward
 X4=REWARDWALLET
 ADD CREDITS,X4
 SCOREINDEX=SCREWARD
 GOTO @ADDSCORE
;
.GIVESTOLEN
 MESSAGE 1876 ; something stolen
 X4=FINECONCEAL
 GOTO @ABSDEBITX4
;
.GIVENOTWALLET
 IF NOUN1<>CAMERA THEN GIVENOTCAMERA
 IF PHOTO<>172 THEN GIVENOTCAMERA ; wiggly roots
 MESSAGE 1877 ; prevented alien invasion - get reward at pyramid
 DUETOBEINTERVIEWED=TRUE
 TIMESINCEEVIDENCE=1
 RETURN

.GIVENOTCAMERA
 MESSAGE 1873 ; no sign of criminal activity
 RETURN

.GIVENOTFUZBOT
 MESSAGE 1172 ; nobody wants it
 RETURN
;---
.JOBCENTRE
 IF CONTROLLINGWALDROID=TRUE THEN @GETLOSTWALDROID
 X1=CURRENTPOS(JOBCENTRECARD)
 IF X1=0 THEN SELECTJOBS
 MESSAGE 1932 ; sorry, you already have a job
 RETURN

.SELECTJOBS
 MESSAGE 1922 ; try YTS course ?
 GOSUB @YESORNO
 JOB=YTSCOURSE
 IF RESULT=FALSE THEN NOTYTS
 MESSAGE 1962 ; do YTS course
 GOTO ENDCOURSE

.NOTYTS
 MESSAGE 1923 ; try YOP course ?
 GOSUB @YESORNO
 JOB=YOPCOURSE
 IF RESULT=FALSE THEN NOTYOP
 MESSAGE 1963 ; do YOP course
 GOTO ENDCOURSE

.NOTYOP
 IF HOUR>5 THEN JOBSGONE
 MESSAGE 1924 ; do you really need money ?
 GOSUB @YESORNO
 IF RESULT=FALSE THEN JOBSGONE
 MESSAGE 1925 ; really ?
 GOSUB @YESORNO
 IF RESULT=FALSE THEN JOBSGONE
 MESSAGE 1927 ; droid rummages under counter ...
 MESSAGE 1928 ; how about being a clerk ?
 GOSUB @YESORNO
 JOB=CLERK
 IF RESULT=TRUE THEN GOTJOB
 MESSAGE 1929 ; how about being a labourer
 GOSUB @YESORNO
 JOB=LABOURER
 IF RESULT=TRUE THEN GOTJOB
 IF BISON=FALSE THEN JOBSGONE
 X1=SCORETABLE(SCMANAGER)
 IF X1<>0 THEN JOBSGONE ; have already done MD job
 MESSAGE 1926 ;sorry, all jobs have gone
 MESSAGE 1930 ;my mistake, be MD ?
 GOSUB @YESORNO
 JOB=MD
 IF RESULT=TRUE THEN GOTJOB

.JOBSGONE
 JOB=0
 MESSAGE 1926 ; sorry, all the jobs have gone
 RETURN

.GOTJOB
 MESSAGE 1931 ; take card to workplace
 X1=JOBCENTREDROID
 CURRENTPOS(JOBCENTRECARD)=X1
 X1=CARRIED
 HICURRENTPOS(JOBCENTRECARD)=X1
 RETURN
;---
.ENDCOURSE
; have done a YTS/YOP course
 X4=10
 ADD CREDITS,X4
 DAYSNOWORK=0
 TIMEPASSED=400 ; working as YTS
 DEST=149 ; porch in jobcentre
 HIDEST=0
 GOTO @ABSNEWLOCATION
;---
.CHECKIFGASSED
 RESULT=TRUE
 X1=CURRENTPOS(HELMET)
 IF X1<>PLAYER THEN CIGASSED
 X1=HICURRENTPOS(HELMET)
 IF X1<>WORN THEN CIGASSED
 X1=CURRENTPOS(LEOTARD)
 IF X1<>PLAYER THEN CIGASSED
 X1=HICURRENTPOS(LEOTARD)
 IF X1<>WORN THEN CIGASSED
 RESULT=FALSE
.CIGASSED
 RETURN
;---
.THROW
 IF ROOM<>172 THEN THROWNOTROOTS
; from wiggly roots
 POS=173 ; floodlit cave
 HIPOS=0
 CONFIRMMESSAGE=1153 ; ok
 GOSUB @MOVEOBJECT

.TRIGGERALARM
; general entry point for all alarm-activating events
 IF HAVETRIGGEREDALARM>1 THEN FCFRET
.ABSTRIGGERALARM
 MESSAGE 1941 ; an alarm sounds
 GOSUB CHECKIFGASSED
 IF RESULT=TRUE THEN FLOODCAVEDIE
 IF ROOM=172 THEN FLOODCAVEFALSEALARM
 MESSAGE 1950 ; seen by technician, had chips
 GOTO @DEATH

.FLOODCAVEFALSEALARM
 ADD HAVETRIGGEREDALARM,C1
 IF HAVETRIGGEREDALARM<>1 THEN FCF1
; first triggering
 MESSAGE 1943 ; technician says 'false alarm'
 RETURN

.FCF1
; second triggering
 MESSAGE 1944 ; technician turns off alarm circuit

.FCFRET
 RETURN
;---
.FLOODCAVEDIE
; killed by gas
 MESSAGE 1942 ; gas kills you
 GOTO @DEATH
;---
.THROWNOTROOTS
 IF ROOM<>164 THEN @DROP
; X1=NOUN1
; GOSUB @DESCTHEOBJX1
 MESSAGE 2037 ; goes under partition
 X1=165
 CURRENTPOS(NOUN1)=X1
 HICURRENTPOS(NOUN1)=C0
 OBJECTTHROWN=NOUN1
 RETURN
;---
.KNOWTOOMUCH
; after visiting saucer
 MESSAGE 1878 ; fuzbot kills you
 GOTO @DEATH
;---
.SQUEEZE
; camera
 IF NOUN1<>CAMERA THEN @BADOBJECT ; can't squueze that
 MESSAGE 1955 ; camera goes buzz
 PHOTO=ROOM
 RETURN
;---
.SPECIALPLAYER
; describe player
 IF CONTROLLINGWALDROID=TRUE THEN SP1
 MESSAGE 14 ; %You are_
 RETURN
.SP1
 MESSAGE 1998 ; %Your waldroid is_
 RETURN
;---
.ADDSCORE
 SCORETABLE(SCOREINDEX)=C1
 SCOREDTODAY=TRUE
 RETURN
;---
.MOVEELEVATOR
;move to floor NUMBER
 IF ELEVATOROFFSET=NUMBER THEN @NOEFFECT
 IF NUMBER<>9 THEN MOVEELEVNOTTOP
 IF ELEVATORBASE<>10 THEN MOVEELEVNOTTOP
; floor 9 from northeast elevator is special - go to
; floor 8 unless press button on 8th floor
 NUMBER=8
 IF ELEVATOROFFSET<>8 THEN MOVEELEVNOTTOP
 NUMBER=9

.MOVEELEVNOTTOP
 MESSAGE 2020 ; elevator moves_
 IF ELEVATOROFFSET<NUMBER THEN MOVEELEVUP
 MESSAGE 110 ; down
 X1=ELEVATOROFFSET
 SUB X1,NUMBER
 IF ELEVATORBASE<>10 THEN MOVEELEVDISPLAY
 IF NUMBER<>0 THEN MOVEELEVDISPLAY
 X2=9
 ADD X1,X2
 GOTO MOVEELEVDISPLAY

.MOVEELEVUP
 MESSAGE 109 ; up
 X1=NUMBER
 SUB X1,ELEVATOROFFSET
 IF ELEVATORBASE<>10 THEN MOVEELEVDISPLAY
 IF ELEVATOROFFSET<>0 THEN MOVEELEVDISPLAY
 X2=9
 ADD X1,X2

.MOVEELEVDISPLAY
; display number of dots
 MESSAGE DOT
 SUB X1,C1
 IF X1<>0 THEN MOVEELEVDISPLAY
 MESSAGE 2022 ; arrives
 ELEVATOROFFSET=NUMBER
 RETURN
;---
.GETME
 CURRENTPOS(INUMBER)=ROOM
 HICURRENTPOS(INUMBER)=C0
 RETURN
;---
.GOTO
 DEST=INUMBER
 HIDEST=0
 GOTO @ABSNEWLOCATION
;---
.HELP ;>>MIKE 18/8/86
 if room<210 then helpngarden
 if room>218 then helpngarden
 x1=currentpos(hole)
 if x1<>0 then helpntree
 message 2100 ; try examining the tree
 return

.helpntree
; worm has appeared, still in garden
 message 2101 ; follow the worm
 return

.helpngarden
 if room<>223 then helpnbehemoth
 message 2103 ; chasm looks behemoth sized
 return

.helpnbehemoth
 if behemothfollowing=false then helpnchase
 message 2102 ; maybe behemoth can be delayed
 return

.helpnchase
 if room<63 then helpnet
 if room>75 then helpnet
 message 2104 ; et system help message
 return

.helpnet
 if room<190 then helpnhome
 if room>201 then helpnhome
 message 2105 ; voice-activated home
 return

.helpnhome
 if room<17 then helpnpark
 if room>36 then helpnpark
 message 2106 ; park is dedicated to sf
 return

.helpnpark
 message 2107 ;' can't help - try clue-sheet
; drop through to authors
;
.AUTHORS      ;>> ANDY 18/8/86
 MESSAGE 4319 ;>> ANDY 18/8/86
 RETURN       ;>> ANDY 18/8/86 
;------------------------------------------------------------------
;------------------------------------------------------------------
