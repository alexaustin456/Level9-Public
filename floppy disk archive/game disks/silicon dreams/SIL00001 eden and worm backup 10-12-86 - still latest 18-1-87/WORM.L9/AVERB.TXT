; Standard Adventure Asource
;
; M.J.Austin 29/7/85
;
; Verb Handlers
;
; changes made after CBM silicon dreams release:
;
; just before disppic1 >>mike 13/11/86 - in time for atari worm
;
; Most constants are game-specific.

CONST

;Values allowed in 'VERSION':
 GAMECODE=6 ;Unique game number
 PREVIOUSGAMECODE=5 ;Only required for second/final part

;Values allowed in 'GAMESOLVED':
 JUSTPLAYING=0 ;Not solved previous part(s), Not an end position
 ENDPOSITION=1 ;End position for game.
 SOLVING=2 ;Solved previous part(s), Not an end position

 FIRSTPART=0 ;If non-zero prevents RESTORE from chaining
 FINALPART=1 ;If non-zero at end of game does not SAVE

BEGIN

;.WEIGHOBJECT
;; RETURN WEIGHT OF 'OBJECT' IN 'WEIGHT'
; OBJECTSAVE=OBJECT
; SEARCHPOS=OBJECT
; HISEARCHPOS=NONSPECIFIC
; GOSUB INITGETOBJ
; WEIGHT=0
;.WO1
; GOSUB GETNEXTOBJECT ; ADDS ON WEIGHT OF THIS OBJECT TO 'WEIGHT'
; IF OBJECT<>0 THEN WO1
; OBJECT=OBJECTSAVE
; RETURN
;---
.DESCANOBJX1
; For obj X1, print 'a desc' or 'an desc'
 IF X1=WALLPAPER THEN DOSOMEVEC
 IF X1=WINE THEN DOSOMEVEC
 IF X1=CUSTARD THEN DOSOMEVEC
 IF X1=TRADCLADS THEN DOSOMEVEC
 IF X1=TOURISTS THEN DOSOMEVEC
 IF X1=BUTTONS THEN DOSOMEVEC
 IF X1=SCANNERS THEN DOSOMEVEC
 IF X1=APPLE THEN @DOAN
 IF X1=ARM THEN @DOAN
 IF X1=INVITATION THEN @DOAN
 IF X1=ORIFICE THEN @DOAN
 IF X1=COSTUME THEN @DOAN
 MESSAGE 20 ; a_
 GOTO @DESCOBJX1A

.DOSOMEVEC
 GOTO @DOSOME
;---
.WORDS
 SCREEN T ; TURN OFF GRAPHICS SCREEN. THIS COMMENT IS ESSENTIAL
 GOTO @DONE
;---
.PICTURE
 if prep=66 then words ; picture off
 SCREEN G C0 ; TURN ON GRAPHICS SCREEN. THIS COMMENT IS ESSENTIAL
 LASTPICTURE=0
 GOTO @DESCRIBEROOM
;---
.MOVE
; MOVEMENT. CLIMB AND OTHER TRANSITIVE MOVEMENT VERBS DO NOT COME HERE
 IF PREP<>0 THEN @DONTUNDERSTAND
 IF OBJECT<>NULLOBJECT THEN @DONTUNDERSTAND
 DIR=VERB
 FROM=ROOM
 GOSUB @CHECKEXIT
 IF DEST=0 THEN NOEXIT
; IF DOOR=FALSE THEN MOVE2
; IF DOOROPEN=TRUE THEN MOVEDOOR
; MESSAGE 1115 ; DOOR IN WAY
; RETURN
;.MOVEDOOR
; MESSAGE 1116 ; DOOR CLOSES BEHIND YOU
.MOVE2
; check if player is carrying too much - if so, drop something
; IF WEIGHTCARRIED<CARRYCAPACITY THEN MOVEOK
; SEARCHPOS=PLAYER
; HISEARCHPOS=NONSPECIFIC
; GOSUB INITGETOBJ
;.MOVE3
; GOSUB GETNEXTOBJECT
; IF OBJECT=0 THEN MOVEOK
; POS=ROOM
; HIPOS=0
; CONFIRMMESSAGE=0
; GOSUB MOVEOBJECT
; IF RESULT=FALSE THEN MOVE3 ; can't drop that, so try another object
; MESSAGE 1170 ; a you move,_
; X1=OBJECT
; GOSUB DESCTHEOBJX1
; MESSAGE 1171 ; slips from your hand

.MOVEOK
 gosub @saveoops
 GOTO @NEWLOCATION
;
.NOEXIT
 MESSAGE 1114 ; NO EXIT
 RETURN
;---
.WIN
 MESSAGE 2033 ; block orifice
 OBJECT=COSTUME
 POS=PLAYER
 HIPOS=NONSPECIFIC
 GOSUB @CHECKOBJECTPOS
 IF RESULT=TRUE THEN WIN1
 MESSAGE 2034 ; no costume, so die
 GOTO @DEATH

.WIN1
.DIE ;> NICK 12/8/86 restored current end game position
 MESSAGE 2035 ; win outright
 SCOREINDEX=SCEND
 GOSUB @ADDSCORE

 GOSUB @SCORE
 GOSUB @ASKFINAL ;> NICK 12/8/86
 GOTO @CQUITPLAYAGAIN
;---
.DEATH
 IF ROOM<210 THEN NORMALDEATH
 IF ROOM<235 THEN @PARADISEWAKEUP
.NORMALDEATH
 MESSAGE 1206 ; you are dead
 GOSUB @SCORE
 GOTO @CQUITPLAYAGAIN
;
.QUIT
 MESSAGE 1200 ; really restart ?
 GOSUB @YESORNO
 IF RESULT=FALSE THEN SAVERET
.QUITPLAYAGAIN
 MESSAGE 1201 ; press space to play again
.QUITLOOP
 X1=3 ; OSRDCH
 LINPUT(C0)=X1
 DRIVER
 X1=LINPUT(C1)
 IF X1<>32 THEN QUITLOOP
 MESSAGE CR
 GOTO @STARTGAME
;---
.SAVE
 SAVE
 M1=1820
 GOSUB @DAGGETMESSAGE
.SAVERET
 RETURN
;---
.RESTORE
 MESSAGE 1202 ; really restore ?
 GOSUB @YESORNO
 IF RESULT=FALSE THEN RESTOREEND
 gosub @lenslok
 RESTORE
 if version<>gamecode then @afterrestore ;>>mike 27/10/86
 if c0<>0 then @afterrestore
 if c1<>1 then @afterrestore
 if c2<>2 then @afterrestore
 LASTPICTURE=0 ; allow picture to be drawn
 M1=1820
 GOSUB @DAGGETMESSAGE
.RESTOREEND
 goto @afterrestore ;>>mike 27/10/86
;>> GOSUB @DESCRIBEROOM
;>> NOMOREINPUT=FALSE
;>> EOL=FALSE
;>> GOSUB @GETNEXTWORD
;>> GOTO @CANCELINPUT
;---
.EXAMINE
 IF PREP<>0 THEN @WHERE
 X1=700
 ADD X1,OBJECT
 MESSAGE X1
 GOSUB @SPECIALEXAMINE ; returns RESULT=TRUE if a dot was printed
 IF RESULT=TRUE THEN EXAMNODOT
 MESSAGE DOT
.EXAMNODOT
 SEARCHPOS=OBJECT
 HISEARCHPOS=NONSPECIFIC
 GOTO @PRINTOBJECTS
;---
.INVENTORY
 SEARCHPOS=PLAYER
 HISEARCHPOS=NONSPECIFIC
 GOSUB @PRINTOBJECTS
 IF TOTALOBJECTFOUND<>0 THEN INVENTRET
; Nothing found, so print appropriate null message
 MESSAGE 1106 ; nothing carried
.INVENTRET
 RETURN
;---
;*.CHECKIFPLAYERON
;; See if player can manipulate 'OBJECT'
;; i.e. is player standing on it or something
;; Return RESULT=TRUE if ok to take it
; OBJECTSAVE=OBJECT
; X1=HICURRENTPOS(PLAYER)
; IF X1=0 THEN CIPOOK
; X1=CURRENTPOS(PLAYER)
; IF X1=OBJECT THEN CIPOFAIL
;; Now scan objects contained by what he's standing on
;; to see if there is any clash
; GOSUB @INITGETOBJ
; OBJECT=0
; HISEARCHPOS=NONSPECIFIC
; SEARCHPOS=OBJECT
;.CIPO1
; X1=PLAYER
; GOSUB @GETNEXTOBJX1 ; X1=MAXOBJECT
; IF OBJECT=0 THEN CIPOOK
; IF OBJECT=PLAYER THEN CIPO1
;.CIPOFAIL
; RESULT=FALSE
; OBJECT=OBJECTSAVE
; RETURN
;.CIPOOK
; OBJECT=OBJECTSAVE
; RESULT=TRUE
 RETURN
;---
.TAKE
; Check for prepositions e.g. GET ON, GET OFF
 IF PREP<>0 THEN @WHERE
.TAKEIT
 POS=PLAYER
 HIPOS=CARRIED
 CONFIRMMESSAGE=1150 ; taken
 GOTO @MOVEOBJECT
;---

.STAND
 IF PREP=ON THEN STANDON
 IF PREP=OFF THEN STANDOFF
 IF PREP=UP THEN STANDUP
 GOTO @DONTUNDERSTAND
;---
.STANDOFF
.STANDUP
 DEST=ROOM
 HIDEST=0
 GOTO @NEWLOCATION
;
.STANDON
 HIDEST=PREP ; ON
 GOTO STANDDEFAULTS
;---
.CANTSEEOBJECT ; can't see object to VERB PREP. (e.g. STAND ON)
 MESSAGE 1042 ; can't see anything to
 M1=VERBOFFSET
 ADD M1,VERB
 MESSAGE M1
 M1=PREPOFFSET
 ADD M1,PREP
 MESSAGE M1
 MESSAGE 1045 ; that.
 RETURN
;---
.LIE
 HIDEST=LIEON
 GOTO STANDDEFAULTS
.SIT
 HIDEST=SITON

.STANDDEFAULTS
; e.g. SIT DOWN, LIE DOWN
 PREP=ON

.STANDDEF1
 PROCESSED=FALSE ; will be set true when has stood on something
 IF VERB<>ILIE THEN SNONOTLIE
; things which can only default for lieing on
 OBJECT=BED
 GOSUB TRYON

.SNONOTLIE
 OBJECT=BENCH
 GOSUB TRYON
 OBJECT=COUCH
 GOSUB TRYON
 OBJECT=CHAIR
 GOSUB TRYON
 OBJECT=BEHEMOTH
 GOSUB TRYON
 OBJECT=JUNKHEAP
 GOSUB TRYON
 IF VERB<>ISIT THEN SNONOTSIT
; things which can only default for sitting on
 OBJECT=CHAIR
 GOSUB TRYON

.SNONOTSIT
 IF PROCESSED=FALSE THEN CANTSEEOBJECT
 RETURN
;---
.TRYON
 IF PROCESSED=TRUE THEN TRYONRET
 X1=CURRENTPOS(OBJECT)
 IF X1<>ROOM THEN TRYONRET
 IF NOUN2=NULLOBJECT THEN STDEFAULTOK
 IF NOUN2=OBJECT THEN STANDONOK
.TRYONRET
 RETURN
;---
.ECHOCOMMAND
 MESSAGE 1190 ; (
 X1=VERBOFFSET
 ADD X1,VERB
 MESSAGE X1
 X1=NOUN1
 GOSUB @DESCOBJX1A
 X1=PREPOFFSET
 ADD X1,PREP
 MESSAGE X1
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 MESSAGE 1191 ; )
 RETURN

.STDEFAULTOK
 GOSUB ECHOCOMMAND
.STANDONOK
 PROCESSED=TRUE
 DEST=OBJECT
 GOTO @NEWLOCATION
;---
.DONE
 MESSAGE 1153 ; done
 RETURN
;---
.WEAR
 X1=HICURRENTPOS(NOUN1)
 IF X1<>WORN THEN WEAR1
 X1=CURRENTPOS(NOUN1)
 IF X1<>PLAYER THEN WEAR1
 MESSAGE 1147 ; already wearing that
 RETURN
.WEAR1
 IF PREP<>0 THEN WHERE
 POS=PLAYER
 HIPOS=WORN
 CONFIRMMESSAGE=1152 ; worn
 GOTO @MOVEOBJECT
;---
.REMOVE
 POS=PLAYER
 HIPOS=CARRIED
 CONFIRMMESSAGE=OK
 GOTO @MOVEOBJECT
;---
.DROP
 IF PREP<>0 THEN WHAT
 POS=ROOM
 HIPOS=0
 CONFIRMMESSAGE=1151 ; dropped
 GOTO @MOVEOBJECT
;---
.WHAT
 MESSAGE 1132 ; WHAT?
 RETURN
;;---
.WHERE
 MESSAGE 1133 ; WHERE?
 RETURN
;---
.ATTACK
 IF NOUN1=COLLAR THEN ATTACKCOLLAR
.WAKE
 M1=1144 ; don't bother group
 GOTO @VARYMESSAGE

.ATTACKCOLLAR
 X1=CURRENTPOS(COLLAR)
 CURRENTPOS(BROKENCOLLAR)=X1
 CURRENTPOS(COLLAR)=C0 ; KILL IT
 HICURRENTPOS(COLLAR)=C0
 X1=CARRIED
 HICURRENTPOS(BROKENCOLLAR)=X1 ; so that player has to conciously
; wear it
 MESSAGE 1855 ; collar is now broken
 ITWORD=BROKENCOLLAR
 RETURN
;---
.PUT
 IF NOUN2=NULLOBJECT THEN WHERE
 OBJECT=NOUN2
 GOSUB @CHECKIFACCESSIBLE
 IF RESULT=FALSE THEN @IMMOVABLE
 OBJECT=NOUN1

 IF PREP=ON THEN PUTON
 IF PREP=UNDER THEN PUTUNDER
 IF PREP=IN THEN PUTIN

.CANTPUTTHERE
 MESSAGE 1123 ; CAN'T PUT ANYTHING THERE
 RETURN
;
.PUTON
; CHECK IF DESTINATION HAS 'CON' SET
 IF NOUN2=BENCH THEN PUTOK
 IF NOUN2=BEHEMOTH THEN PUTOK
 IF NOUN2=COUCH THEN PUTOK
 IF NOUN2=BED THEN PUTOK
 IF NOUN2=CHAIR THEN PUTOK
 GOTO CANTPUTTHERE
;---
.PUTOK
 OBJECT=NOUN1
 POS=NOUN2 ; DESTINATION
 if room<>165 then notdefaultorifice ;>>mike 27/10/86
 if noun1<>cork then notdefaultorifice ;>>
 noun2=orifice ;>>

.notdefaultorifice ;>>
 HIPOS=PREP
 CONFIRMMESSAGE=OK
 GOTO @MOVEOBJECT

.PUTIN
 IF NOUN1=COSTUME THEN PUTIN1
 IF NOUN1>NOUN2 THEN PIDWONTFIT  ; too big
 IF NOUN2=WALLET THEN PUTINOK
.PUTIN1
 IF NOUN2=DAGGET THEN PUTINOK
 IF NOUN1=CORK THEN PUTOK
 GOTO CANTPUTTHERE

.PUTINOK
 GOSUB @INITGETOBJ
 OBJECT=0
 HISEARCHPOS=NONSPECIFIC
 SEARCHPOS=NOUN2
 X1=PLAYER ; MAXOBJECT
 GOSUB @GETNEXTOBJX1
 IF OBJECT=0 THEN PID1
.PIDWONTFIT
 MESSAGE 1936 ; won't fit
 RETURN

.PID1
 GOSUB PUTOK
 IF OBJECT=BATPAK THEN @ACTIVATEDAGGET
 RETURN

.PUTUNDER
 IF NOUN2=TREE THEN PUTOK
 GOTO CANTPUTTHERE

;---
.SETUPROOM
; Return 'ROOM'=current position of player
; If player is contained, trace back until find the
; location in which the container(s) rests.
 X4=PLAYER
.SETUPROOMX4
; Return 'ROOM'=current pos of object X4
 ROOM=CURRENTPOS(X4)
 X1=HICURRENTPOS(X4)
 IF X1=0 THEN SETUPROOMRET
 X4=ROOM
 GOTO SETUPROOMX4
.SETUPROOMRET
 RETURN
;---
.POSITIONMOBILE
; Return X1=TRAMLINES pointer for mobile in room 'POS'
; whose tramline block starts at 'TRAMPTR'
 X3=TRAMPTR
.POSMOB1
 X2=TRAMLINES(X3)
 IF X2=POS THEN POSMOBEND
 IF X2=0 THEN POSMOBEND ; not found, return start of list
 ADD X3,C1
 GOTO POSMOB1
.POSMOB2
 X3=0
.POSMOBEND
 TRAMPTR=X3
 RETURN
;---
.MOVEMOBILE
; move mobile 'OBJECT' whose tramline pointer is TRAMPTR
; Describe object movement in relation to player.
; GOSUB SETUPROOM ; ???
 X1=CURRENTPOS(OBJECT)
 IF X1<>ROOM THEN MMNOTRANDMESSAGES
 RANDOM X1
 IF X1>160 THEN MMNOTRANDMESSAGES
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 MESSAGE SPACE
 GOSUB @VARYMESSAGE
 MESSAGE DOT

.MMNOTRANDMESSAGES
 IF OBJECT<DROID1 THEN MOVEMOBGO
 IF OBJECT>DROID3 THEN MOVEMOBGO
; special case for cleaning droid - make it stop and clean sometimes
 RANDOM X1
 IF X1>160 THEN MOVEMOBGO
 X1=CURRENTPOS(OBJECT)
 X2=CURRENTPOS(SIGNBOARD)
 IF X1<>X2 THEN MMNOTPILE
; droid may deposit something on heap
 X3=CURRENTPOS(JUNKHEAP)
 IF X3=X2 THEN MMDROPJUNK
 IF X3<>0 THEN MOVEMOBGO ; junkheap not picked up yet
.MMDROPJUNK
 CURRENTPOS(JUNKHEAP)=X1
 IF X1<>ROOM THEN MOVEMOBGO ; can't see it happening
 MESSAGE 1711 ; droids drops some rubbish
 RETURN ; no further action on this occasion
.MMNOTPILE
 RANDOM X2
 IF X2>160 THEN MOVEMOBGO
 X2=CURRENTPOS(SIGNBOARD)
 IF X1=X2 THEN MMLEAVEPILEHERE
 X2=CURRENTPOS(JUNKHEAP)
 IF X1<>X2 THEN MMLEAVEPILEHERE
 CURRENTPOS(JUNKHEAP)=C0 ; kill off junkheap untill dropped at sign

 X1=15 ; waterfall room
 CURRENTPOS(GRILL)=X1 ; make grill appear

.MMLEAVEPILEHERE
; now describe droid collecting rubbish if near player
 IF X1<>ROOM THEN MMNPRET
 MESSAGE 1710 ; droid sweeps up rubbish

.MMNPRET
 RETURN
; 
.MOVEMOBGO
 TO=TRAMLINES(TRAMPTR)
 IF TO=255 THEN @MOVEMOBRET ; object now stationary
 ADD TRAMPTR,C1
 IF TO<>0 THEN MOVEMOB1
 TRAMPTR=TRAMBASE
 GOTO @MOVEMOBILE

.MOVEMOB1
 TO=TRAMLINES(TRAMPTR)
 IF TO<>0 THEN MOVEMOB1A
 TRAMPTR=TRAMBASE
 GOTO MOVEMOB1

.MOVEMOB1A
 IF TO=255 THEN MOVEMOBRET ; object now stationary
 FROM=CURRENTPOS(OBJECT)
 CURRENTPOS(OBJECT)=TO
 HICURRENTPOS(OBJECT)=C0 ; security - ensure it is on ground

 IF FROM=TO THEN MOVEMOBRET

 M1=96 ; leaves by
 IF OBJECT<>TOURISTS THEN MMLEAVESBY
 M1=98 ; leave by
.MMLEAVESBY

 IF FROM=ROOM THEN MOVEMOB2
 IF TO<>ROOM THEN MOVEMOBNODESC ; have already moved it, but don't describe
; describe mobile moving towards player 

 M1=95 ; enters from
 IF OBJECT<>TOURISTS THEN MMENTERSFROM
 M1=97 ; enter from
.MMENTERSFROM

 X1=TO   ; swap over to/from for direction calculation
 TO=FROM ; for mobile entering player's room
 FROM=X1
.MOVEMOB2
; first print description of object
 X1=OBJECT
 IF M1=96 THEN MOVEMOBTHE ; mobile is leaving
 IF M1=98 THEN MOVEMOBTHE ; mobile is leaving
.MOVEMOBA
 GOSUB @DESCANOBJX1
 GOTO MOVEMOB2A
.MOVEMOBTHE
 GOSUB @DESCTHEOBJX1
.MOVEMOB2A
 MESSAGE M1
 GOSUB @CALCDIRECTION ; now calculate direction
 X1=EXITDESCBASE ; direction verbs offset
 ADD X1,DIR
 MESSAGE X1
;
.MOVEMOBNODESC
; Have described movement ( if visible )

; Now do any special cases to do with movement of mobiles

 X1=CURRENTPOS(OBJECT)
 IF X1<>219 THEN MMNOTHOLE
 IF ROOM<>211 THEN MMHOLE
 MESSAGE 1504 ; worm smashes hole in wall
.MMHOLE
 CURRENTPOS(HOLE)=X1 ; POSITION HOLE

.MMNOTHOLE
 X1=CURRENTPOS(OBJECT)
 IF X1<>225 THEN MMNOTCLIFF
 IF ROOM<>223 THEN MMNOTCLIFF
 MESSAGE 1503 ; worm climbs down precipice, disappears

.MMNOTCLIFF
 IF FROM=ROOM THEN MMDOT
 IF TO<>ROOM THEN MOVEMOBRET
.MMDOT
 MESSAGE DOT
.MOVEMOBRET
 RETURN
;---

.KILLOBJECT
 POS=0
 HIPOS=0
 CONFIRMMESSAGE=0
; fall through to moveobject
.MOVEOBJECT
; Move 'OBJECT' to 'POS', 'HIPOS'
; Return RESULT=TRUE if move was successful
; must have CONFIRMMESSAGE set to the message to printed
; as confirmation if the move is successful
 IF OBJECT=SIGNBOARD THEN MOVEOBJ0A
 IF OBJECT=DUMMY THEN @IMMOVABLE
 IF OBJECT>MAXMOVEABLE THEN @IMMOVABLE
.MOVEOBJ0A
 IF POS<>PLAYER THEN MONOTPLAYER
 IF HIPOS=0 THEN MONOTPLAYER
; player to gain this object, check if is able to carry it
; GOSUB WEIGHOBJECT
; ADD WEIGHTCARRIED,WEIGHT
; IF WEIGHTCARRIED>CARRYCAPACITY THEN HANDSFULL
 IF HIPOS<>WORN THEN MOVEOBJ1
; is it wearable ?
 IF OBJECT=TATTOO THEN MOVEOBJ1
 IF OBJECT=TRADCLADS THEN MOVEOBJ1
 IF OBJECT=COLLAR THEN MOVEOBJ1
 IF OBJECT=LEOTARD THEN MOVEOBJ1
 IF OBJECT<18 THEN @CANTWEAR
 IF OBJECT>21 THEN @CANTWEAR
.MOVEOBJ1
; Now check if the player is standing on it or some similar problem
 OBJECTSAVE=OBJECT
 DEST=POS ; save value
 HIDEST=HIPOS ; save it
 POS=OBJECT
 HIPOS=NONSPECIFIC
 OBJECT=PLAYER
 GOSUB @CHECKOBJECTPOS
 OBJECT=OBJECTSAVE
 POS=DEST ; restore value
 HIPOS=HIDEST ; restore value
 IF RESULT=TRUE THEN @MOVEOBJFAIL
 RESULT=TRUE
 GOSUB @SPECIALTAKES ; game-specific take messages
 IF RESULT=FALSE THEN MOVEOBJRET

;

.MONOTPLAYER
 X1=CURRENTPOS(OBJECT)
 IF X1<>PLAYER THEN MMNOTLOSE
 X1=HICURRENTPOS(OBJECT)
 IF X1=0 THEN MMNOTLOSE
; player is to lose object
 IF X1<>WORN THEN MMLOSENOTWORN
; player is wearing object, if it was specified using EVERYTHING,
; then don't drop it as this could cause embarrasment
 IF EVERYTHING=FALSE THEN MMLOSENOTWORN
 MESSAGE 1134 ; you're wearing it
 RESULT=FALSE
 RETURN

.MMLOSENOTWORN
 GOSUB @SPECIALDROPS
 IF RESULT=FALSE THEN MOVEOBJRET

.MMNOTLOSE
 IF OBJECT<>CORK THEN MMNOTCORK
 X1=CURRENTPOS(WINE)
 IF X1=BOTTLE THEN @REMOVECORK

.MMNOTCORK
 IF POS=OBJECT THEN @CANTPUTTHERE
;
 FROM=CURRENTPOS(OBJECT)
 CURRENTPOS(OBJECT)=POS
 HICURRENTPOS(OBJECT)=HIPOS
 MESSAGE CONFIRMMESSAGE
;
; now some odd special cases for after the move has been completed
;
 IF POS<>DAGGET THEN MMNOTDAGGET
 IF HIPOS=0 THEN MMNOTDAGGET
 IF OBJECT<>BATPAK THEN MMDAGGETNOTBATPAK
 GOSUB @ACTIVATEDAGGET
 GOTO MMNOTDAGGET

.MMDAGGETNOTBATPAK
 M1=1821 ; mind what putting in there
 GOSUB @DAGGETMESSAGE

.MMNOTDAGGET
 IF OBJECT<>BATPAK THEN MMNOTBATPAK
 X1=CURRENTPOS(BATPAK)
 IF X1=DAGGET THEN MMNOTBATPAK
 IF FROM<>DAGGET THEN MMNOTBATPAK
 MESSAGE 1822 ; dagget says has to run on emergency power now

.MMNOTBATPAK
 IF POS<>ORIFICE THEN MMNOTORIFICE
 IF OBJECT<>CORK THEN MMNOTORIFICE
; cork stops the flow of extinguisher foam
 GOTO @WIN

.MMNOTORIFICE
;
; end of special cases
;
 RESULT=TRUE
.MOVEOBJRET
 RETURN

.MOVEOBJFAIL
 MESSAGE 1131 ;  you're on it
 RETURN

;.HANDSFULL
; MESSAGE 1107
; RETURN
;---
.IMMOVABLE
 M1=1120 ; INVALID OBJECT GROUP
 GOTO @VARYMESSAGE
;---
.CANTWEAR
 MESSAGE 1142 ; Can't wear group
 RETURN
;---
.SCORE
 MESSAGE 1204 ; you score
 GOSUB CALCSCORE
 PRINT X4
 MESSAGE 1205 ; out of 1000
 M1=1250 ; beginner
 IF X4<150 THEN GOTSCORE
 M1=1251 ; player
 IF X4<300 THEN GOTSCORE
 M1=1252 ; adventurer
 IF X4<500 THEN GOTSCORE
 M1=1253 ; experienced adventurer
 IF X4<800 THEN GOTSCORE
 M1=1254 ; grand master adventurer
.GOTSCORE
 MESSAGE M1
 MESSAGE DOT
 GOTO @SOLVEDPREVIOUS ;> NICK 12/8/86
;---
.CALCSCORE
 X4=0
 X3=SCSTART
 X2=40
.CALCSCORE1
 X1=SCORETABLE(X3)
 IF X1=0 THEN CALCSCORE2
 ADD X4,X2
.CALCSCORE2
 ADD X3,C1
 IF X3<SCMAX THEN CALCSCORE1
; now misc other items of scoring
 X1=HICURRENTPOS(FLAG)
 IF X1=FORSALE THEN CALCSNOTFLAG
 ADD X4,X2

.CALCSNOTFLAG
 X1=CURRENTPOS(WREATH)
 IF X1<>24 THEN CALCSNOTWREATH
 ADD X4,X2

.CALCSNOTWREATH
 X1=CURRENTPOS(PIE)
 IF X1<>0 THEN CALCSNOTPIE
 X1=20
 SUB X4,X1

.CALCSNOTPIE
 X1=HICURRENTPOS(NEWSPAPER)
 IF X1=FORSALE THEN CALCSNOTPAPER
 X1=20
 SUB X4,X1

.CALCSNOTPAPER
 X1=SCORETABLE(SCEND)
 IF X1=0 THEN CALCSNOTBOX
 ADD X4,X2 ; extra score for winning

.CALCSNOTBOX
; check the remote possibility of a negative score
 IF X4<30000 THEN CALCSRET
 X4=0

.CALCSRET
 RETURN
;---
.BADOBJECT
 MESSAGE 1044 ; I can't
 M1=VERBOFFSET
 ADD M1,VERB
 MESSAGE M1
 MESSAGE 1045 ; that
 RETURN
;---
.STARTGAME
;>>MIKE 12/8/86 MESSAGE 1100 ; welcome to game
;>>MIKE 12/8/86 GOSUB @INITOOPS

 CLEAR
 VERSION=GAMECODE    ;> NICK 12/8/86
 GAMESOLVED=JUSTPLAYING ;> NICK 12/8/86
.RETURNTOSTART       ;>>MIKE 12/8/86 - must be after CLEAR
 C1=1 ;> NICK 12/8/86
 C2=2 ;> NICK 12/8/86
 C3=3 ;> NICK 12/8/86
 c4=4 ;>>mike 19/10/86

 SCREEN G C0         ;> NICK 12/8/86
 LASTPICTURE=712     ;> NICK 12/8/86
 CLS G               ;> NICK 12/8/86
 PICTURE LASTPICTURE ;> NICK 12/8/86

 MESSAGE 1100 ; welcome to game ;>>MIKE 12/8/86
 GOSUB @INITOOPS ;>>MIKE 12/8/86

 CREDITS=100

 ITWORD=BENCH ; first object seen
 ITNUMBER=NULLNUMBER
 NOMOREINPUT=TRUE ; have come to end of input line
; CARRYCAPACITY=NORMALCAPACITY
 GOSUB INITOBJECTS
 HOUR=5
 HOURSAWAKE=2
 TOURTRAMPTR=TOURISTTRAMBASE
 SUSTRAMPTR=SUSTRAMBASE
 DROID1TRAMPTR=DROID1TRAMBASE
 DROID2TRAMPTR=DROID2TRAMBASE
 DROID3TRAMPTR=DROID3TRAMBASE
 ALIENTRAMPTR=ALIENTRAMBASE
 SOMETHINGPROCESSED=TRUE ; force printing of WHAT NEXT ?
 GOSUB RANDOMIZEADDRESSES
 GOSUB @INITPARADISE
 GOTO @PRECOMMAND
;---
.INITOBJECTS
 OBJECT=0
.INITOBJ2
 GOSUB @INITANOBJECT
 ADD OBJECT,C1
 IF OBJECT<141 THEN INITOBJ2
; and clear out SCORETABLE
 X1=0
.INITOBJ3
 COLOURCODES(X1)=C0
 SCORETABLE(X1)=C0
 IF X1>32 THEN INITOBJ4
 OBJECTTABLE(X1)=C0
 LINPUT(X1)=C0
.INITOBJ4
 ADD X1,C1
 IF X1<SCMAX THEN INITOBJ3
 RETURN
;---
.RANDOMIZEADDRESSES
; generate random addresses on transport system for
; homes and shops
; first clear table
 INDEX=56
.RANDADDR0
 COLOURTABLE(INDEX)=C0
 SUB INDEX,C1
 IF INDEX>0 THEN RANDADDR0
; now go through randomizing each digit
; except digit 4 which is dictated by the number
; of the location represented
; COLOURTABLE(7) is set to 0
 INDEX=14
 X4=0
.RANDADDR1
 DIGIT=0
.RANDADDR2
 GOSUB @RAND10
 X1=DIGIT
 ADD X1,INDEX
 COLOURTABLE(X1)=NUMBER ; random digit
 IF DIGIT<>4 THEN RANDADDR3
 COLOURTABLE(X1)=X4
.RANDADDR3
 IF DIGIT<>6 THEN RANDADDR4
; digit 6 is 0 - 2
 GOSUB @RAND3 ; corrupts nothing except NUMBER
 COLOURTABLE(X1)=NUMBER
.RANDADDR4
 ADD DIGIT,C1
 IF DIGIT<7 THEN RANDADDR2
 ADD X4,C1 ; number of position being randomized
 X1=7
 ADD INDEX,X1
 IF INDEX<56 THEN RANDADDR1
 RETURN
;---
.LONGADD
; add 6 byte values (lo byte first)
; COLOURTABLE(0) = COLOURTABLE(0) + MATHSTABLE(INDEX)
 VALUE=0 ; use VALUE as carry
 X3=C0
 X4=INDEX
.LONGADD1
 X1=COLOURTABLE(X3)
 X2=MATHSTABLE(X4)
 ADD X1,X2
 ADD X1,VALUE ; add in carry
; now process carry
 X2=10
 VALUE=0
.LONGADD2
 IF X1<X2 THEN LONGADD3
 SUB X1,X2
 ADD VALUE,C1 ; increment carry
 GOTO LONGADD2
.LONGADD3
 COLOURTABLE(X3)=X1
 ADD X3,C1
 ADD X4,C1
 IF X3<6 THEN LONGADD1
 RETURN
;---
.LONGSUB
; for comments, see LONGADD
 VALUE=0
 X3=C0
 X4=INDEX 

.LONGSUB1
 X1=COLOURTABLE(X3)
 X2=MATHSTABLE(X4)
 SUB X1,X2
 SUB X1,VALUE ; subtract carry
; now process carry
 X2=10
 VALUE=0
.LONGSUB2
 IF X1<10 THEN LONGSUB3
 ADD X1,X2
 ADD VALUE,C1
 GOTO LONGSUB2
.LONGSUB3
 COLOURTABLE(X3)=X1
 ADD X3,C1
 ADD X4,C1
 IF X3<6 THEN LONGSUB1
 RETURN
;---
.FINDROOM
; see if the current colour code accesses any known rooms
 INDEX=7
 GOSUB LONGCOMPARE
 IF RESULT=FALSE THEN FINDROOM1
 X1=COLOURTABLE(6)
 VALUE=51
 ADD VALUE,X1
 RETURN

.FINDROOM1
 GOSUB LONGCOMPARE
 VALUE=190 ; own habihome
 IF RESULT=TRUE THEN FINDROOMOK
 GOSUB LONGCOMPARE
 VALUE=196 ; traitor's habihome
 IF RESULT=TRUE THEN FINDROOMOK
 GOSUB LONGCOMPARE
 VALUE=95 ; florist's
 IF RESULT=TRUE THEN FINDROOMOK
 GOSUB LONGCOMPARE
 VALUE=96 ; hardware warehouse
 IF RESULT=TRUE THEN FINDROOMOK
 GOSUB LONGCOMPARE
 VALUE=97 ; travel agent's
 IF RESULT=TRUE THEN FINDROOMOK
 GOSUB LONGCOMPARE
 VALUE=149 ; job centre
 IF RESULT=TRUE THEN FINDROOMOK
 VALUE=0 ; no room found
.FINDROOMOK
 RETURN
;---
.LONGCOMPARE
; return RESULT=TRUE if
; COLOURTABLE(INDEX) = COLOURTABLE (0)
; also ADD INDEX,7
 RESULT=FALSE
 X3=0
 X4=INDEX
.LONGCOMP1
 X1=COLOURTABLE(X3)
 X2=COLOURTABLE(X4)
 IF X1<>X2 THEN LONGCOMPFALSE
 ADD X3,C1
 ADD X4,C1
 IF X3<6 THEN LONGCOMP1
 RESULT=TRUE
.LONGCOMPFALSE
 X1=7
 ADD INDEX,X1
 RETURN
;---
.DISPLAYCOLOURCODE
; display colour code at INDEX
 DIGIT=6
.DCC1
 X1=INDEX
 ADD X1,DIGIT
 NUMBER=COLOURTABLE(X1)
 GOSUB @PRINTCOLOUR
 SUB DIGIT,C1
 IF DIGIT<NEGATIVE THEN DCC1
 MESSAGE DOT
 RETURN
;---
.NOEFFECT
 M1=1109
 GOTO @VARYMESSAGE
;---
.SPECIALDESC
; come here after all objects and exits printed
; no return necessary
 IF ROOM<63 THEN SDNOTCOLOUR
 IF ROOM>75 THEN @SDNOTCOLOUR
 MESSAGE 1692 ; colour code on floor is
 INDEX=0
 GOSUB DISPLAYCOLOURCODE

.SDNOTCOLOUR
 IF ROOM<>172 THEN SDNOTGAS
 GOSUB @CHECKIFGASSED
 IF RESULT=FALSE THEN SDNOTGAS ; wearing breathing equipment
 MESSAGE 1940 ; there's a smell of gas

.SDNOTGAS
.SDESCRET
 RETURN
;---
.ACTIVATEDAGGET
 DAGGETNOPOWER=FALSE
 TIMEWITHOUTPOWER=0
 M1=1824 ; dagget is delighted
 GOTO @DAGGETMESSAGE
;---
.SPECIALEXITS
; Given 'FROM' 'DIR' 'DEST'
; Modify if an exit is blocked for some reason
 HIDEST=0 ; normally can't move onto a container through an exit
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN SEORDINARY
; Player is on something - so exits may be different
 POS=BEHEMOTH
 HIPOS=NONSPECIFIC
 OBJECT=PLAYER
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN SENOTBEHEMOTH
; Exits from behemoth ( blocking crevice )
 IF DIR<>EAST THEN SEBENOTE
 DEST=223
 GOTO SEORDINARY
.SEBENOTE
 IF DIR<>WEST THEN SEFALSE
 DEST=224
 EXITVISIBLE=TRUE
.SENOTBEHEMOTH
; Standing on something, but can take as being an
; ordinary exit from the room in which the container is ('ROOM')
.SEORDINARY
; now check any other modified exits
 IF FROM<>15 THEN SENOTWATERFALL
 IF DEST<>12 THEN SENOTWATERFALL
 X1=CURRENTPOS(JUNKHEAP)
 IF X1=FROM THEN SEFALSE

.SENOTWATERFALL
 X1=CURRENTPOS(HOLE)
 IF X1>0 THEN SE2
 IF FROM<>211 THEN SE1
 IF DEST=219 THEN SEFALSE
.SE1
 IF FROM<>219 THEN SE2
 IF DEST=211 THEN SEFALSE

.SE2
 IF DEST<>225 THEN SE3
 IF BEHEMOTHFOLLOWING<>STUCK THEN SEFALSE
 DEST=BEHEMOTH
 HIDEST=ON
.SE3
 IF DEST<>145 THEN SENOTCOCOON
 IF CONTROLLINGWALDROID=TRUE THEN SEFALSE ; can't enter coccon as wald

.SENOTCOCOON
 RETURN

.SEFALSE
; Exit is not present
 DEST=0
 EXITVISIBLE=FALSE
; DOOR=FALSE
 RETURN
;---
.SPECIALDROPS
; trying to drop 'OBJECT'. Print appropriate message if desired
; and return RESULT=FALSE if player is to be prevented from dropping it
 IF OBJECT=TATTOO THEN SDFAIL
 IF OBJECT=COLLAR THEN SDFAIL
 IF OBJECT<>LEOTARD THEN SDNOTLEOTARD
 X1=HICURRENTPOS(TRADCLADS)
 IF X1<>WORN THEN SDOK
 x1=hicurrentpos(leotard)
 if x1<>worn then sdok ; can drop leotard if not worn
 MESSAGE 1666 ; must drop tradclads first
 GOTO SDFALSE

.SDNOTLEOTARD
 IF ROOM<63 THEN SDNOTET
 IF ROOM>75 THEN SDNOTET
 IF HIPOS<>0 THEN SDNOTET ; ALLOWED TO PUT THINGS IN DAGGET
 MESSAGE 1693 ; cleaning robot pushes it back into hands
 GOTO SDFALSE

.SDNOTET

.SDOK
 RESULT=TRUE
.SDRET
 RETURN
.SDFAIL
 MESSAGE 1128 ; can't drop that
.SDFALSE
 RESULT=FALSE
 RETURN
;---
.SPECIALTAKES
; trying to take 'OBJECT'. Print appropriate message if desired
; and return RESULT=FALSE if player is to be prevented from taking it
 RESULT=TRUE
 IF VERB<>IWEAR THEN STNOTWEAR

; special code for wearing certain items
;
 IF OBJECT<>VISOR THEN @STWNOTVISOR
 MESSAGE 1543 ; about to enter a dream
 X4=COSTDREAM
 GOSUB @ABSDEBITX4
 IF RESULT=TRUE THEN @INITDREAM
 GOTO @CANCELINPUT
 
.STWNOTVISOR
 if object<>tradclads then stwnottradclad
 x1=currentpos(leotard)
 if x1<>player then stwnottradclad
 x1=hicurrentpos(leotard)
 if x1<>worn then stwnottradclad
 confirmmessage=1856 ; the tradclads hide the leotard

.stwnottradclad
 IF OBJECT<>LEOTARD THEN STWNOTLEOTARD
 X1=HICURRENTPOS(TRADCLADS)
 IF X1<>WORN THEN STWLEOTARD
 MESSAGE 1665 ; can't wear leotard over tradclads
 GOTO STPREVENT
.STWNOTLEOTARD
 RETURN
.STWLEOTARD
 SCOREINDEX=SCLEOTARD
 GOSUB @ADDSCORE
;        drop through to STOK
.STNOTWEAR
;
; Code for taking items (part of SPECIALTAKES)
;
.STOK
 RESULT=TRUE
.STRET
 RETURN
.STPREVENT
 RESULT=FALSE
 RETURN
;---
.POSITIONSHADOWS
; Make objects follow the player around where applicable. e.g. DOORS.
 IF ROOM=211 THEN POHOLE
 IF ROOM<>219 THEN PONOTHOLE
.POHOLE
 X1=CURRENTPOS(HOLE)
 IF X1=0 THEN PONOTHOLE
 CURRENTPOS(HOLE)=ROOM

.PONOTHOLE
 IF ROOM=223 THEN POCREVICE
 IF ROOM<>224 THEN PONOTCREVICE
.POCREVICE
 CURRENTPOS(CREVICE)=ROOM

.PONOTCREVICE
 OBJECT=VISOR
 IF ROOM<121 THEN PONOTVISOR
 IF ROOM>128 THEN PONOTVISOR
 IF ROOM=127 THEN PONOTVISOR
.POVISOR
 X1=HICURRENTPOS(VISOR)
 IF X1<>0 THEN PONOTVISOR
 CURRENTPOS(VISOR)=ROOM

.PONOTVISOR
 IF ROOM<>110 THEN PONOTBANDIT
 X1=BANDIT
 CURRENTPOS(BUTTONS)=X1
 X1=ON
 HICURRENTPOS(BUTTONS)=X1

.PONOTBANDIT
 IF ROOM<175 THEN PONOTMANHOLE
 IF ROOM>184 THEN PONOTMANHOLE
 IF ROOM>182 THEN POMANHOLE
 IF ROOM>179 THEN PONOTMANHOLE
.POMANHOLE
 CURRENTPOS(MANHOLE)=ROOM

.PONOTMANHOLE
 IF ROOM<>100 THEN PONOTPOLICESTATION
 CURRENTPOS(FUZBOT)=ROOM

.PONOTPOLICESTATION
 IF ROOM<>102 THEN PONOTBODYBANK
 CURRENTPOS(CONSULTANT)=ROOM

.PONOTBODYBANK
 IF ROOM<136 THEN PONOTCARPET
 IF ROOM>143 THEN PONOTCARPET
 CURRENTPOS(CARPET)=ROOM

.PONOTCARPET
 IF ROOM<>169 THEN PONOTELEVATOR
 CURRENTPOS(BUTTONS)=ROOM
 HICURRENTPOS(BUTTONS)=C0

.PONOTELEVATOR
 IF ROOM=105 THEN PORECEPTION
 IF ROOM<>106 THEN PONOTRECEPTION
.PORECEPTION
 CURRENTPOS(RECEPTIONIST)=ROOM

.PONOTRECEPTION
 RETURN
;---
.FASTEN
; NOUN1 TO NOUN2
 IF NOUN2<>TRADCLADS THEN @CANTPUTTHERE
 IF NOUN1=BROOCH THEN FASTENOK
 IF NOUN1<>BADGE THEN @CANTPUTTHERE
.FASTENOK
 CURRENTPOS(NOUN1)=NOUN2
 X1=FASTENEDTO
 HICURRENTPOS(NOUN1)=X1
 GOTO @DONE
;---

.JUMP
 IF ROOM<63 THEN JUMPNOTET
 IF ROOM>75 THEN JUMPNOTET
; MESSAGE 1690 ; a force field grabs you
 DEST=75 ; inner platform
 HIDEST=0
 DIR=0
 GOTO NEWLOCATION

.JUMPNOTET
 IF ROOM<>223 THEN JUMPNOTCHASM
 MESSAGE 1513
 RETURN

.JUMPNOTCHASM
 MESSAGE 1129 ; whee - jump on spot
 RETURN
;---
.CLIMB
;
 IF OBJECT<>TREE THEN CLIMBNOTTREE
 MESSAGE 1514 ; tree too slippery
 RETURN
.CLIMBNOTTREE
 IF NOUN2=JUNKHEAP THEN CLIMBJUNKHEAP
 IF OBJECT<>JUNKHEAP THEN CLIMBNOTJUNKHEAP
.CLIMBJUNKHEAP
 DEST=JUNKHEAP
 HIDEST=ON
 GOTO NEWLOCATION
;
.CLIMBNOTJUNKHEAP

 MESSAGE 1127 ; can't climb that
.CLIMBRET
 RETURN
;---
;---------------------------------------------------------------
;---------------------------------------------------------------
;---------------------------------------------------------------
